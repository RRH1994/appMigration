#Область ПроцедурыОбработчикиСобытийФормы

&НаКлиенте
Процедура ПриОткрытии(Отказ)
	
	ОбновитьКроссТаблицу();
	
КонецПроцедуры

&НаСервере
Процедура ПриСозданииНаСервере(Отказ, СтандартнаяОбработка)
	
	Адрес_ДобавленныеРеквизитыКроссТаблицы 	= ПоместитьВоВременноеХранилище(Новый Массив, УникальныйИдентификатор);
	Адрес_ОписаниеКолонок 					= ПоместитьВоВременноеХранилище(Новый Структура, УникальныйИдентификатор);

КонецПроцедуры

#КонецОбласти

#Область ПроцедурыОбработчикиТабличногоПоляКроссТаблица

&НаКлиенте
Процедура КроссТаблицаПриАктивизацииСтроки(Элемент)
	
	ОбновитьКонтекстноеМеню(Элемент);

КонецПроцедуры

&НаКлиенте
Процедура КроссТаблицаПриАктивизацииПоля(Элемент)
	
	ОбновитьКонтекстноеМеню(Элементы.КроссТаблица);
	
КонецПроцедуры
 	 
&НаКлиенте
Процедура КроссТаблицаПередНачаломДобавления(Элемент, Отказ, Копирование, Родитель, Группа, Параметр)
	
	Отказ = Истина;
	
КонецПроцедуры

&НаКлиенте
Процедура КроссТаблицаПередУдалением(Элемент, Отказ)
	
	Отказ = Истина;
	
КонецПроцедуры

// <Описание процедуры>
//
// Параметры:
//  <Параметр1>  - <Тип.Вид> - <описание параметра>
//                 <продолжение описания параметра>
//  <Параметр2>  - <Тип.Вид> - <описание параметра>
//                 <продолжение описания параметра>
//
&НаКлиенте
Процедура Подключаемый_ПриИзмененииЗначенияКолонки(Элемент)
	
	ОписаниеЯчейки = СформироватьСтруктуруЗначенийЯчейкиКроссТаблицы(Элементы.КроссТаблица.ТекущаяСтрока, Элемент.Имя);
	
	ИзменитьЗначенияКолонокНаСервере(
		ОбщегоНазначенияКлиентСервер.ЗначениеВМассиве(ОписаниеЯчейки),
		ОписаниеЯчейки.Коэффициент,
		СтароеЗначениеЯчейки);
	
	ОбновитьКонтекстноеМеню(Элементы.КроссТаблица);

КонецПроцедуры // Подключаемый_ПриИзмененииЗначенияКолонки()

#КонецОбласти                                          

#Область ПроцедурыОбработчикиСобытийЭлементовФормы

&НаКлиенте
Процедура ОтборСценарийПриИзменении(Элемент)
	
	ОтборСценарийПриИзмененииНаСервере();
	
	ОбновитьКроссТаблицуНаКлиенте();
	
КонецПроцедуры
    
// <Описание процедуры>
//
// Параметры:
//  <Параметр1>  - <Тип.Вид> - <описание параметра>
//                 <продолжение описания параметра>
//  <Параметр2>  - <Тип.Вид> - <описание параметра>
//                 <продолжение описания параметра>
//
&НаСервере
Процедура ОтборСценарийПриИзмененииНаСервере()

	Если Не ЗначениеЗаполнено(ОтборСценарий) Тогда
		Возврат;
	КонецЕсли;

	РеквизитыСценария 	= ОбщегоНазначения.ЗначенияРеквизитовОбъекта(ОтборСценарий, "НачалоДействия, ОкончаниеДействия");
	ОтборПериод			= Новый СтандартныйПериод(РеквизитыСценария.НачалоДействия, РеквизитыСценария.ОкончаниеДействия); 	

КонецПроцедуры // Отбор()

&НаКлиенте
Процедура ОтборПериодПриИзменении(Элемент)
	
	Если Не ЗначениеЗаполнено(ОтборПериод.ДатаНачала)
	 Или Не ЗначениеЗаполнено(ОтборПериод.ДатаОкончания)
	Тогда
		ОбщегоНазначенияКлиентСервер.СообщитьПользователю(
			"Выберите дату начала, окончания!", , "ОтборПериод", "");
		Возврат;
		
	Иначе
		НоваяДатаНачала 	= НачалоГода(ОтборПериод.ДатаНачала);
		НоваяДатаОкончания 	= КонецГода(ОтборПериод.ДатаОкончания);
		Если ОтборПериод.ДатаНачала <> НоваяДатаНачала
		 Или ОтборПериод.ДатаОкончания <> НоваяДатаОкончания
		Тогда
			ОтборПериод = Новый СтандартныйПериод(НоваяДатаНачала, НоваяДатаОкончания);
		КонецЕсли;	
	КонецЕсли;
	
	ОбновитьКроссТаблицуНаКлиенте();
	
КонецПроцедуры
      
&НаКлиенте
Процедура ОтборВидНачисленияПриИзменении(Элемент)
	
	ОбновитьКроссТаблицуНаКлиенте();
	
КонецПроцедуры
    
&НаКлиенте
Процедура ОтборВидНачисленияОчистка(Элемент, СтандартнаяОбработка)
	
	ОбновитьКроссТаблицуНаКлиенте();
	
КонецПроцедуры
	
#КонецОбласти

#Область ПроцедурыОбработчикиКоманд

&НаКлиенте
Процедура КомандаОбновить(Команда)
	
	Если Не ПроверитьЗаполнение() Тогда
		Возврат;
	КонецЕсли;
	
	ОбновитьКроссТаблицуНаКлиенте();
	
КонецПроцедуры


&НаКлиенте
Процедура КомандаСкопироватьДоКонцаПериода(Команда)
	
	СкопироватьДоНачалоКонцаПериода(1);
	
КонецПроцедуры
 
&НаКлиенте
Процедура КомандаСкопироватьДоНачалоПериода(Команда)
	
	СкопироватьДоНачалоКонцаПериода(-1);
	
КонецПроцедуры

// <Описание процедуры>
//
// Параметры:
//  <Параметр1>  - <Тип.Вид> - <описание параметра>
//                 <продолжение описания параметра>
//  <Параметр2>  - <Тип.Вид> - <описание параметра>
//                 <продолжение описания параметра>
//
&НаКлиенте
Процедура СкопироватьДоНачалоКонцаПериода(Направление)
	
	// пока не используем
	ЭлементКолонка	= Элементы.КроссТаблица.ТекущийЭлемент;
	Если ЭлементКолонка = Неопределено Тогда
		Возврат;
	КонецЕсли;
	
	ОписаниеЯчейки	= СформироватьСтруктуруЗначенийЯчейкиКроссТаблицы(Элементы.КроссТаблица.ТекущаяСтрока, ЭлементКолонка.Имя);
	
	Если ОписаниеЯчейки = Неопределено Тогда
		Возврат;
	КонецЕсли;
	      
	мЯчейки 			= Новый Массив;
	мОписаниеКолонок 	= ПолучитьИзВременногоХранилища(Адрес_ОписаниеКолонок);
	
	Если Направление = 1 Тогда
		
		Для Каждого ОписаниеКолонки Из мОписаниеКолонок Цикл
			
			Если ОписаниеКолонки.Ссылка < ОписаниеЯчейки.Дата Тогда
				// колонки слева от текущей
				Продолжить;
			КонецЕсли;
			
			мЯчейки.Добавить(
				СформироватьСтруктуруЗначенийЯчейкиКроссТаблицы(
					Элементы.КроссТаблица.ТекущаяСтрока,
					СтрШаблон("КроссТаблица%1_Значение", ОписаниеКолонки.Ключ)));
					
		КонецЦикла;	

	Иначе
		
		Для Каждого ОписаниеКолонки Из мОписаниеКолонок Цикл
			
			Если ОписаниеКолонки.Ссылка > ОписаниеЯчейки.Дата Тогда
				// колонки слева от текущей
				Продолжить;
			КонецЕсли;
			
			мЯчейки.Добавить(
				СформироватьСтруктуруЗначенийЯчейкиКроссТаблицы(
					Элементы.КроссТаблица.ТекущаяСтрока,
					СтрШаблон("КроссТаблица%1_Значение", ОписаниеКолонки.Ключ)));
					
		КонецЦикла;	

	КонецЕсли;
	
	ИзменитьЗначенияКолонокНаСервере(
		мЯчейки,
		ОписаниеЯчейки.Коэффициент);
	
КонецПроцедуры // ¶КонецПроцедуры()



&НаКлиенте
Процедура КомандаСкопироватьНаПредыдущийГод(Команда)
	
	КомандаСкопироватьНаГод(-1);
	
КонецПроцедуры

&НаКлиенте
Процедура КомандаСкопироватьНаСледующийГод(Команда)
	
	КомандаСкопироватьНаГод(1);
	
КонецПроцедуры

// <Описание процедуры>
//
// Параметры:
//  <Параметр1>  - <Тип.Вид> - <описание параметра>
//                 <продолжение описания параметра>
//  <Параметр2>  - <Тип.Вид> - <описание параметра>
//                 <продолжение описания параметра>
//
&НаКлиенте
Процедура КомандаСкопироватьНаГод(Направление)
	
	ТекущиеДанные = Элементы.КроссТаблица.ТекущиеДанные;
	Если ТекущиеДанные = Неопределено Тогда
		Возврат;
	КонецЕсли;
	
	
	Отказ = Ложь;
	
	ОписаниеЯчейки = СформироватьСтруктуруЗначенийЯчейкиКроссТаблицы(Элементы.КроссТаблица.ТекущаяСтрока, Элементы.КроссТаблица.ТекущийЭлемент.Имя);
	
	СкопироватьНаДругойГодНаСервере(ОписаниеЯчейки.Дата, ДобавитьМесяц(ОписаниеЯчейки.Дата, Направление * 12), Отказ);
	
	Если Не Отказ Тогда
		
		ОбновитьКроссТаблицуНаКлиенте();
		
		ПоказатьОповещениеПользователя("Данные обновлены.");
		
	КонецЕсли;	

КонецПроцедуры // КомандаСкопироватьНаГод()


&НаКлиенте
Процедура КомандаРедактироватьВыбранные(Команда)
	
	ЭлементКолонка	= Элементы.КроссТаблица.ТекущийЭлемент;
	Если ЭлементКолонка = Неопределено Тогда
		Возврат;
	КонецЕсли;
	
	мЯчейки = Новый Массив;
	
	Для Каждого ВыделеннаяСтрока Из Элементы.КроссТаблица.ВыделенныеСтроки Цикл
		
		мЯчейки.Добавить(
			СформироватьСтруктуруЗначенийЯчейкиКроссТаблицы(ВыделеннаяСтрока, ЭлементКолонка.Имя));
			
	КонецЦикла;
		
	ПоказатьВводЧисла(
		Новый ОписаниеОповещения("КомандаРедактироватьВыбранныеВводЗначенияЗавершение",
			ЭтаФорма,
			Новый Структура("мЯчейки", мЯчейки)),
		0,
		"Коэффициент:",
		5,
		2);
	
КонецПроцедуры

// <Описание процедуры>
//
// Параметры:
//  <Параметр1>  - <Тип.Вид> - <описание параметра>
//                 <продолжение описания параметра>
//  <Параметр2>  - <Тип.Вид> - <описание параметра>
//                 <продолжение описания параметра>
//
&НаКлиенте
Процедура КомандаРедактироватьВыбранныеВводЗначенияЗавершение(Результат, ДополнительныеПараметры) Экспорт
	
	Если Результат = Неопределено Тогда
		// пользователь отказался от ввода
		Возврат;
	КонецЕсли;
	
	ИзменитьЗначенияКолонокНаСервере(
		ДополнительныеПараметры.мЯчейки,
		Результат);

КонецПроцедуры // КомандаРедактироватьВыбранныеВводЗначенияЗавершение()


&НаКлиенте
Процедура КомандаСкопироватьИзДругогоСценария(Команда)
	
	ОткрытьФорму(
		"Справочник.Сценарии.ФормаВыбора",
		Новый Структура("Заголовок", "Выберите сценарий-источник"),
		ЭтаФорма,,,,
		Новый ОписаниеОповещения("КомандаСкопироватьИзДругогоСценарияВыборСценарияЗавершение", ЭтаФорма),
		РежимОткрытияОкнаФормы.БлокироватьВесьИнтерфейс);
	
КонецПроцедуры

// <Описание процедуры>
//
// Параметры:
//  <Параметр1>  - <Тип.Вид> - <описание параметра>
//                 <продолжение описания параметра>
//  <Параметр2>  - <Тип.Вид> - <описание параметра>
//                 <продолжение описания параметра>
//
&НаКлиенте
Процедура КомандаСкопироватьИзДругогоСценарияВыборСценарияЗавершение(Результат, ДополнительныеПараметры) Экспорт

	Если Не ЗначениеЗаполнено(Результат) Тогда
		ПоказатьПредупреждение(, "Не выбран сценарий для копирования!");
		Возврат;
	КонецЕсли;
	
	ТекстВопроса = СтрШаблон("Данные по сценарию <%1>, за период <%2>, %3 будут замещены данными сценария <%4>, продолжить?",
					ОтборСценарий,
					ОтборПериод,
					?(Не ЗначениеЗаполнено(ОтборВидНачисления), "по всем видам начислений", СтрШаблон("по виду начисления <%1>", ОтборВидНачисления)),
					Результат);
					
	ПоказатьВопрос(
		Новый ОписаниеОповещения("КомандаСкопироватьИзДругогоСценарияПодтвереждениеОперацииЗавершение", ЭтаФорма, Новый Структура("СценарийИсточник", Результат)),
		ТекстВопроса,
		РежимДиалогаВопрос.ДаНет);
		
КонецПроцедуры // КомандаСкопироватьИзДругогоСценария()

// <Описание процедуры>
//
// Параметры:
//  <Параметр1>  - <Тип.Вид> - <описание параметра>
//                 <продолжение описания параметра>
//  <Параметр2>  - <Тип.Вид> - <описание параметра>
//                 <продолжение описания параметра>
//
&НаКлиенте
Процедура КомандаСкопироватьИзДругогоСценарияПодтвереждениеОперацииЗавершение(Результат, ДополнительныеПараметры) Экспорт
	
	Если Не Результат = КодВозвратаДиалога.Да Тогда
		Возврат;
	КонецЕсли;
	
	Отказ = Ложь;
	
	СкопироватьИзДругогоСценарияНаСервере(ДополнительныеПараметры.СценарийИсточник, Отказ);
	
	Если Не Отказ Тогда
		
		ОбновитьКроссТаблицуНаКлиенте();
		
		ПоказатьОповещениеПользователя("Данные обновлены.");
		
	КонецЕсли;	

КонецПроцедуры // ()


&НаКлиенте
Процедура КомандаУдалитьВыбранное(Команда)

	ТекстВопроса = СтрШаблон("Данные по сценарию <%1>, за период <%2>, %3, будут удалены, продолжить?",
					ОтборСценарий,
					ОтборПериод,
					?(Не ЗначениеЗаполнено(ОтборВидНачисления), "по всем видам начислений", СтрШаблон("по виду начисления <%1>", ОтборВидНачисления)));
					
	ПоказатьВопрос(
		Новый ОписаниеОповещения("КомандаУдалитьВыбранноеПодтвереждениеОперацииЗавершение", ЭтаФорма),
		ТекстВопроса,
		РежимДиалогаВопрос.ДаНет);
	// Вставить содержимое обработчика.
КонецПроцедуры

// <Описание процедуры>
//
// Параметры:
//  <Параметр1>  - <Тип.Вид> - <описание параметра>
//                 <продолжение описания параметра>
//  <Параметр2>  - <Тип.Вид> - <описание параметра>
//                 <продолжение описания параметра>
//
&НаКлиенте
Процедура КомандаУдалитьВыбранноеПодтвереждениеОперацииЗавершение(Результат, ДополнительныеПараметры) Экспорт
	
	Если Не Результат = КодВозвратаДиалога.Да Тогда
		Возврат;
	КонецЕсли;
	
	Отказ = Ложь;
	
	УдалитьВыбранныеДанныеНаСервере(Отказ, Истина, ОтборСценарий, ОтборПериод.ДатаНачала, ОтборПериод.ДатаОкончания, ОтборВидНачисления);
	
	Если Не Отказ Тогда
		
		ОбновитьКроссТаблицуНаКлиенте();
		
		ПоказатьОповещениеПользователя("Данные обновлены.");
		
	КонецЕсли;	

КонецПроцедуры // КомандаУдалитьВыбранноеПодтвереждениеОперацииЗавершение()

 
&НаКлиенте
Процедура КомандаВыгрузитьВExcel(Команда)
	
	РаботаСФайламиКлиент.СохранитьФайлКак(КроссТаблицуВТабличныйДокументДляВыгрузкиВExcel());
	
КонецПроцедуры
   
   
&НаКлиенте
Процедура КомандаЗагрузитьИзExcel(Команда)
	
	Если Не ЗначениеЗаполнено(ОтборСценарий) Тогда
		ОбщегоНазначенияКлиентСервер.СообщитьПользователю(
			"Укажите сценарий, для которого необходимо загрузить данные!",, "ОтборСценарий",,);
		Возврат;
	КонецЕсли;
	
	ПоказатьВопрос(
		Новый ОписаниеОповещения("КомандаЗагрузитьИзExcelПодтверждениеЗагрузки", ЭтаФорма),
		СтрШаблон("Данные по сценарию <<%1>> будут обновлены данными файла, продолжить?", ОтборСценарий),
		РежимДиалогаВопрос.ДаНет);
		
КонецПроцедуры

&НаКлиенте
Процедура КомандаЗагрузитьИзExcelПодтверждениеЗагрузки(Результат, ДополнительныеПараметры) Экспорт
	
	Если Не Результат = КодВозвратаДиалога.Да Тогда
		Возврат;
	КонецЕсли;

	Диалог 						= Новый ДиалогВыбораФайла(РежимДиалогаВыбораФайла.Открытие);
	Диалог.Заголовок 			= "Выберите файл для загрузки...";
	Диалог.МножественныйВыбор 	= Ложь;
	Диалог.Фильтр 				= "XLS (*.xls;*.xlsx)|*.xls;*.xlsx";
	
	Диалог.Показать(Новый ОписаниеОповещения("ЗагрузитьИзExcelПодтверждениеЗагрузкиВыборФайла", ЭтаФорма));

КонецПроцедуры

// <Описание процедуры>
//
// Параметры:
//  <Параметр1>  - <Тип.Вид> - <описание параметра>
//                 <продолжение описания параметра>
//  <Параметр2>  - <Тип.Вид> - <описание параметра>
//                 <продолжение описания параметра>
//
&НаКлиенте
Процедура ЗагрузитьИзExcelПодтверждениеЗагрузкиВыборФайла(Результат, ДополнительныеПараметры) Экспорт
	
	Если Результат = Неопределено Тогда
		Возврат;
	Иначе
		ПутьКФайлу = Результат[0];
	КонецЕсли;

	Попытка
			
		ExcelОбъект = ПолучитьCOMОбъект(ПутьКФайлу); 
		Страница 	= ExcelОбъект.Sheets(1);
		Диапазон	= Страница.Cells(1,1).SpecialCells(11);
		Данные 		= Страница.Range(Страница.Cells(1,1), Страница.Cells(Диапазон.Row, Диапазон.Column)).Value.Выгрузить();

	Исключение
			
		ОбщегоНазначенияКлиентСервер.СообщитьПользователю(
			СтрШаблон("Не удалось прочитать файл: %1", ОписаниеОшибки()));
		Возврат;
		
	КонецПопытки;
	
	ExcelОбъект = Неопределено;
	
	Отказ = Ложь;
	
	ОчиститьСообщения();
	
	ЗагрузитьИзExcelНаСервере(Данные, Отказ);
	
	Если Не Отказ Тогда
		ОбновитьКроссТаблицу();
		ПоказатьОповещениеПользователя("Данные загружены.");
	КонецЕсли;
	
КонецПроцедуры // ЗагрузитьИзExcelПодтверждениеЗагрузкиВыборФайла()


#КонецОбласти

#Область ЗаполнениеДанных

Функция СформироватьОписаниеКолонок() 
	
	мОписаниеКолонок = Новый Массив;

	ТекНачалоГода = НачалоГода(ОтборПериод.ДатаНачала);
	
	Пока ТекНачалоГода <= НачалоГода(ОтборПериод.ДатаОкончания) Цикл
		
		ОписаниеКолонки = Новый Структура("Ключ, Представление, Ссылка",
										СтрШаблон("Колонка_%1", Формат(ТекНачалоГода, "ДФ=ddMMyyyy")),
										СтрШаблон("%1", Формат(ТекНачалоГода, "ДФ=yyyy")) + ", %",
										ТекНачалоГода);
		мОписаниеКолонок.Добавить(ОписаниеКолонки);
		
		ТекНачалоГода 	= ДобавитьМесяц(ТекНачалоГода, 12);

	КонецЦикла;

	ПоместитьВоВременноеХранилище(мОписаниеКолонок, Адрес_ОписаниеКолонок);
	
	Возврат мОписаниеКолонок;
	
КонецФункции	

// <Описание функции>
//
// Параметры:
//  <Параметр1>  - <Тип.Вид> - <описание параметра>
//                 <продолжение описания параметра>
//  <Параметр2>  - <Тип.Вид> - <описание параметра>
//                 <продолжение описания параметра>
//
// Возвращаемое значение:
//   <Тип.Вид>   - <описание возвращаемого значения>
//
&НаСервере
Функция ПодготовитьЗапросДляКроссТаблицы()
	
	Запрос 			= Новый Запрос;
	Запрос.Текст 	= 
	"ВЫБРАТЬ
	|	0 КАК Число
	|ПОМЕСТИТЬ ВТ_Числа
	|
	|ОБЪЕДИНИТЬ ВСЕ
	|
	|ВЫБРАТЬ
	|	1
	|
	|ОБЪЕДИНИТЬ ВСЕ
	|
	|ВЫБРАТЬ
	|	2
	|
	|ОБЪЕДИНИТЬ ВСЕ
	|
	|ВЫБРАТЬ
	|	3
	|
	|ОБЪЕДИНИТЬ ВСЕ
	|
	|ВЫБРАТЬ
	|	4
	|
	|ОБЪЕДИНИТЬ ВСЕ
	|
	|ВЫБРАТЬ
	|	5
	|
	|ОБЪЕДИНИТЬ ВСЕ
	|
	|ВЫБРАТЬ
	|	6
	|
	|ОБЪЕДИНИТЬ ВСЕ
	|
	|ВЫБРАТЬ
	|	7
	|
	|ОБЪЕДИНИТЬ ВСЕ
	|
	|ВЫБРАТЬ
	|	8
	|
	|ОБЪЕДИНИТЬ ВСЕ
	|
	|ВЫБРАТЬ
	|	9
	|;
	|
	|////////////////////////////////////////////////////////////////////////////////
	|ВЫБРАТЬ РАЗЛИЧНЫЕ
	|	ДОБАВИТЬКДАТЕ(&НачалоПериода, ГОД, ВТ_Числа.Число + ВТ_Числа1.Число * 10) КАК Год
	|ПОМЕСТИТЬ ВТ_Годы
	|ИЗ
	|	ВТ_Числа КАК ВТ_Числа
	|		ПОЛНОЕ СОЕДИНЕНИЕ ВТ_Числа КАК ВТ_Числа1
	|		ПО (ИСТИНА)
	|ГДЕ
	|	ДОБАВИТЬКДАТЕ(&НачалоПериода, ГОД, ВТ_Числа.Число + ВТ_Числа1.Число * 10) <= &КонецПериода
	|	И ВЫБОР
	|			КОГДА &НачалоПериода = ДАТАВРЕМЯ(1, 1, 1)
	|					ИЛИ &КонецПериода = ДАТАВРЕМЯ(1, 1, 1)
	|				ТОГДА ЛОЖЬ
	|			ИНАЧЕ ИСТИНА
	|		КОНЕЦ
	|;
	|
	|////////////////////////////////////////////////////////////////////////////////
	|ВЫБРАТЬ РАЗРЕШЕННЫЕ
	|	ВидыНачислений.Ссылка КАК ВидНачисления,
	|	ЕСТЬNULL(СправочникДоступныеПериодыХраненияЗначений.Период, ЗНАЧЕНИЕ(Перечисление.ПериодыХраненияЗначений.ПустаяСсылка)) КАК ПериодДействия,
	|	&Сценарий КАК Сценарий,
	|	ЕСТЬNULL(СправочникДоступныеПериодыХраненияЗначений.НомерСтроки, 0) КАК ПорядокПериодаДействия
	|ПОМЕСТИТЬ ВТ_ПустыеКоэффициенты
	|ИЗ
	|	Справочник.ВидыНачислений КАК ВидыНачислений
	|		ПОЛНОЕ СОЕДИНЕНИЕ Справочник.ВидыНачислений.ДоступныеПериодыХраненияЗначений КАК СправочникДоступныеПериодыХраненияЗначений
	|		ПО ВидыНачислений.Ссылка = СправочникДоступныеПериодыХраненияЗначений.Ссылка
	|ГДЕ
	|	НЕ ВидыНачислений.ПометкаУдаления
	|	И ВидыНачислений.УстанавливаютсяКоэффициенты
	|	И (&ПоВсемВидамНачислений
	|			ИЛИ ВидыНачислений.Ссылка = &ВидНачисления)
	|;
	|
	|////////////////////////////////////////////////////////////////////////////////
	|ВЫБРАТЬ РАЗРЕШЕННЫЕ
	|	ВТ_Годы.Год КАК Год,
	|	ВТ_ПустыеКоэффициенты.ВидНачисления КАК ВидНачисления,
	|	ВТ_ПустыеКоэффициенты.ПериодДействия КАК ПериодДействия,
	|	ВТ_ПустыеКоэффициенты.Сценарий КАК Сценарий,
	|	0 КАК Коэффициент,
	|	ВТ_ПустыеКоэффициенты.ПорядокПериодаДействия КАК ПорядокПериодаДействия
	|ПОМЕСТИТЬ ВТ_ПустыеКоэффициентыПоГодам
	|ИЗ
	|	ВТ_ПустыеКоэффициенты КАК ВТ_ПустыеКоэффициенты
	|		ВНУТРЕННЕЕ СОЕДИНЕНИЕ ВТ_Годы КАК ВТ_Годы
	|		ПО (ИСТИНА)
	|;
	|
	|////////////////////////////////////////////////////////////////////////////////
	|ВЫБРАТЬ РАЗРЕШЕННЫЕ
	|	НАЧАЛОПЕРИОДА(УстановкаКоэффициентовНачислений.Дата, ГОД) КАК Колонка,
	|	УстановкаКоэффициентовНачислений.Сценарий КАК Сценарий,
	|	УстановкаКоэффициентовНачислений.ВидНачисления КАК ВидНачисления,
	|	УстановкаКоэффициентовНачислений.ПериодДействия КАК ПериодДействия,
	|	УстановкаКоэффициентовНачислений.Коэффициент КАК ЗначениеКолонки,
	|	ЕСТЬNULL(ВидыНачисленийДоступныеПериодыХраненияЗначений.НомерСтроки, 0) КАК ПорядокПериодаДействия,
	|	УстановкаКоэффициентовНачислений.ВидНачисления.Наименование КАК ПорядокВидаНачисления
	|ИЗ
	|	Документ.УстановкаКоэффициентовНачислений КАК УстановкаКоэффициентовНачислений
	|		ЛЕВОЕ СОЕДИНЕНИЕ Справочник.ВидыНачислений.ДоступныеПериодыХраненияЗначений КАК ВидыНачисленийДоступныеПериодыХраненияЗначений
	|		ПО УстановкаКоэффициентовНачислений.ВидНачисления = ВидыНачисленийДоступныеПериодыХраненияЗначений.Ссылка
	|			И УстановкаКоэффициентовНачислений.ПериодДействия = ВидыНачисленийДоступныеПериодыХраненияЗначений.Период
	|ГДЕ
	|	УстановкаКоэффициентовНачислений.Сценарий = &Сценарий
	|	И (&ПоВсемВидамНачислений
	|			ИЛИ УстановкаКоэффициентовНачислений.ВидНачисления = &ВидНачисления)
	|	И УстановкаКоэффициентовНачислений.Проведен
	|	И УстановкаКоэффициентовНачислений.Дата МЕЖДУ &НачалоПериода И &КонецПериода
	|
	|ОБЪЕДИНИТЬ ВСЕ
	|
	|ВЫБРАТЬ
	|	ВТ_ПустыеКоэффициентыПоГодам.Год,
	|	ВТ_ПустыеКоэффициентыПоГодам.Сценарий,
	|	ВТ_ПустыеКоэффициентыПоГодам.ВидНачисления,
	|	ВТ_ПустыеКоэффициентыПоГодам.ПериодДействия,
	|	ВТ_ПустыеКоэффициентыПоГодам.Коэффициент,
	|	ВТ_ПустыеКоэффициентыПоГодам.ПорядокПериодаДействия,
	|	ВТ_ПустыеКоэффициентыПоГодам.ВидНачисления.Наименование
	|ИЗ
	|	ВТ_ПустыеКоэффициентыПоГодам КАК ВТ_ПустыеКоэффициентыПоГодам
	|
	|УПОРЯДОЧИТЬ ПО
	|	ПорядокВидаНачисления,
	|	ПорядокПериодаДействия,
	|	Колонка
	|ИТОГИ
	|	МАКСИМУМ(Сценарий),
	|	СУММА(ЗначениеКолонки)
	|ПО
	|	ВидНачисления,
	|	ПериодДействия,
	|	Колонка";
	
	Запрос.УстановитьПараметр("НачалоПериода", 			ОтборПериод.ДатаНачала);
	Запрос.УстановитьПараметр("КонецПериода", 			ОтборПериод.ДатаОкончания);
	Запрос.УстановитьПараметр("Сценарий", 				ОтборСценарий);
	Запрос.УстановитьПараметр("ПоВсемВидамНачислений", 	Не ЗначениеЗаполнено(ОтборВидНачисления));
	Запрос.УстановитьПараметр("ВидНачисления", 			ОтборВидНачисления);
	
	Возврат Запрос.Выполнить();
	

КонецФункции // ПодготовитьЗапросДляКроссТаблицы()

// <Описание процедуры>
//
// Параметры:
//  <Параметр1>  - <Тип.Вид> - <описание параметра>
//                 <продолжение описания параметра>
//  <Параметр2>  - <Тип.Вид> - <описание параметра>
//                 <продолжение описания параметра>
//
&НаКлиенте
Процедура ОбновитьКроссТаблицуНаКлиенте()

	ОбновитьКроссТаблицу();
	
КонецПроцедуры // ОбновитьКроссТаблицуНаКлиенте()
  
// <Описание процедуры>
//
// Параметры:
//  <Параметр1>  - <Тип.Вид> - <описание параметра>
//                 <продолжение описания параметра>
//  <Параметр2>  - <Тип.Вид> - <описание параметра>
//                 <продолжение описания параметра>
//
&НаСервере
Процедура ОбновитьКроссТаблицу()

	#Область РеквизитыФормы
		
	мДобавляемыеРеквизитыИмя 	= ПолучитьИзВременногоХранилища(Адрес_ДобавленныеРеквизитыКроссТаблицы);
	мПутиУдаляемыхРеквизитов 	= Новый Массив;
	Для Каждого РеквизитУдалить Из мДобавляемыеРеквизитыИмя Цикл
		
		Если Найти(РеквизитУдалить, "КроссТаблицаГруппа") = 0 Тогда
			мПутиУдаляемыхРеквизитов.Добавить("КроссТаблица." + РеквизитУдалить); 
		КонецЕсли;
		
		НайденныйЭлемент = Элементы.Найти(РеквизитУдалить);
		Если Не НайденныйЭлемент = Неопределено Тогда
			Элементы.Удалить(НайденныйЭлемент);
		КонецЕсли;
		
	КонецЦикла;	
	
	ЭтаФорма.ИзменитьРеквизиты(, мПутиУдаляемыхРеквизитов);
	
	мОписанияКолонок	= СформироватьОписаниеКолонок();

	ОписаниеТипаСтрока150			= ОбщегоНазначения.ОписаниеТипаСтрока(150);
	ОписаниеТипаЧисло3_2			= ОбщегоНазначения.ОписаниеТипаЧисло(5, 2);
	ОписаниеТипаДаты				= ОбщегоНазначения.ОписаниеТипаДата(ЧастиДаты.ДатаВремя);
	
	мДобавляемыеРеквизиты 			= Новый Массив;
	мДобавляемыеРеквизитыИмя 		= Новый Массив;
	мДобавляемыеЭлементыФормы 		= Новый Массив;
	
	сОписаниеКолонок				= Новый Соответствие;
	
	Для Каждого ОписаниеКолонки Из мОписанияКолонок Цикл
		
		сОписаниеКолонок.Вставить(ОписаниеКолонки.Ссылка, ОписаниеКолонки);
		
		мКолонкиНаФорму = Новый Массив;
		
		ИмяГруппыКолонок = СтрШаблон("КроссТаблицаГруппаКолонокПредставление%1", ОписаниеКолонки.Ключ);
		мДобавляемыеРеквизитыИмя.Добавить(ИмяГруппыКолонок);
		
		// колонка, содержащая ключ представление значения
		ИмяРеквизита = СтрШаблон("КроссТаблица%1_Представление", ОписаниеКолонки.Ключ);
		мДобавляемыеРеквизиты.Добавить(Новый РеквизитФормы(ИмяРеквизита, ОписаниеТипаСтрока150, "КроссТаблица",  ОписаниеКолонки.Представление));	
		мДобавляемыеРеквизитыИмя.Добавить(ИмяРеквизита);                           
		
		// колонка, содержащая ключ
		ИмяРеквизита = СтрШаблон("КроссТаблица%1_Ключ", ОписаниеКолонки.Ключ);
		мДобавляемыеРеквизиты.Добавить(Новый РеквизитФормы(ИмяРеквизита, ОписаниеТипаСтрока150, "КроссТаблица",  "Ключ"));	
		мДобавляемыеРеквизитыИмя.Добавить(ИмяРеквизита);

		// колонка, содержащая ссылку (дату) на значение колонки (вид начисления)
		ИмяРеквизита = СтрШаблон("КроссТаблица%1_Ссылка", ОписаниеКолонки.Ключ);
		мДобавляемыеРеквизиты.Добавить(Новый РеквизитФормы(ИмяРеквизита, ОписаниеТипаДаты, "КроссТаблица",  "Колонка ссылка"));	
		мДобавляемыеРеквизитыИмя.Добавить(ИмяРеквизита);
		
		// колонка, содержащая значение
		ИмяРеквизита = СтрШаблон("КроссТаблица%1_Значение", ОписаниеКолонки.Ключ);
		мДобавляемыеРеквизиты.Добавить(Новый РеквизитФормы(ИмяРеквизита, ОписаниеТипаЧисло3_2, "КроссТаблица", "Колонка значение"));	
		мДобавляемыеРеквизитыИмя.Добавить(ИмяРеквизита);
		мКолонкиНаФорму.Добавить(Новый Структура("Имя, Заголовок, Ширина", ИмяРеквизита, ОписаниеКолонки.Представление, 10));
		
		мДобавляемыеЭлементыФормы.Добавить(Новый Структура("Имя, Заголовок, Колонки", 
			ИмяГруппыКолонок, ОписаниеКолонки.Представление, мКолонкиНаФорму));
			
		
	КонецЦикла;
	
	ЭтаФорма.ИзменитьРеквизиты(мДобавляемыеРеквизиты);
	
	ПоместитьВоВременноеХранилище(мДобавляемыеРеквизитыИмя, Адрес_ДобавленныеРеквизитыКроссТаблицы);
	
	Для Каждого ГруппаРеквизитов Из мДобавляемыеЭлементыФормы Цикл
		
		НоваяГруппа									= Элементы.Вставить(ГруппаРеквизитов.Имя, Тип("ГруппаФормы"), Элементы.КроссТаблица, Элементы.КроссТаблицаОтступ);
		Новаягруппа.Заголовок						= ГруппаРеквизитов.Заголовок;
		Новаягруппа.Вид								= ВидГруппыФормы.ГруппаКолонок;
		НоваяГруппа.ОтображатьВШапке				= Ложь;
		Новаягруппа.Группировка						= ГруппировкаКолонок.Горизонтальная;
		Новаягруппа.ГоризонтальноеПоложениеВШапке 	= ГоризонтальноеПоложениеЭлемента.Центр;
		Новаягруппа.ШрифтЗаголовка 					= Новый Шрифт(Новаягруппа.ШрифтЗаголовка,,,Истина);
		//Новаягруппа.Высота							= 2;
		
		Для Каждого ОписаниеРеквизита Из ГруппаРеквизитов.Колонки Цикл

			НовыйЭлемент 								= Элементы.Добавить(ОписаниеРеквизита.Имя, Тип("ПолеФормы"), НоваяГруппа);
			НовыйЭлемент.Вид							= ВидПоляФормы.ПолеВвода;
			НовыйЭлемент.Заголовок 						= ОписаниеРеквизита.Заголовок;
			НовыйЭлемент.ПутьКДанным 					= "КроссТаблица." + ОписаниеРеквизита.Имя;
			НовыйЭлемент.ШрифтЗаголовка 				= Новый Шрифт(НовыйЭлемент.ШрифтЗаголовка,,,Истина);
			НовыйЭлемент.РедактированиеТекста			= Истина;
			НовыйЭлемент.АвтомаксимальнаяШирина			= Ложь;
			НовыйЭлемент.МаксимальнаяШирина				= ОписаниеРеквизита.Ширина;
			НовыйЭлемент.РастягиватьПоГоризонтали		= Ложь;
			НовыйЭлемент.ТолькоПросмотр					= Ложь;
			НовыйЭлемент.ОтображатьВШапке				= Истина;
			НовыйЭлемент.ВысотаЗаголовка				= 2;
			
			НовыйЭлемент.УстановитьДействие("ПриИзменении", "Подключаемый_ПриИзмененииЗначенияКолонки");
			
		КонецЦикла;
		
	КонецЦикла;
	
	#КонецОбласти

	КроссТаблица.Очистить();

	РезультатЗапросаДляКроссТаблицы	= ПодготовитьЗапросДляКроссТаблицы();
	ВыборкаГруппаВидНачисления		= РезультатЗапросаДляКроссТаблицы.Выбрать(ОбходРезультатаЗапроса.ПоГруппировкам, "ВидНачисления");
	ЕстьДобавляемыеКолонки			= (мОписанияКолонок.Количество() > 0);
	
	Пока ВыборкаГруппаВидНачисления.Следующий() Цикл
		
		ВыборкаГруппаПериодДействия = ВыборкаГруппаВидНачисления.Выбрать(ОбходРезультатаЗапроса.ПоГруппировкам, "ПериодДействия");
		
		Пока ВыборкаГруппаПериодДействия.Следующий() Цикл
		
	        СтрокаДетали 				= КроссТаблица.Добавить();
			СтрокаДетали.Представление 	= СтрШаблон("%1%2",
											ВыборкаГруппаПериодДействия.ВидНачисления,
											?(ЗначениеЗаполнено(ВыборкаГруппаПериодДействия.ПериодДействия),
												СтрШаблон(", %1", ВыборкаГруппаПериодДействия.ПериодДействия), ""));
			СтрокаДетали.Сценарий		= ВыборкаГруппаПериодДействия.Сценарий;
			СтрокаДетали.ВидНачисления	= ВыборкаГруппаПериодДействия.ВидНачисления;
			СтрокаДетали.ПериодДействия	= ВыборкаГруппаПериодДействия.ПериодДействия;
			
			ВыборкаКолонки = ВыборкаГруппаПериодДействия.Выбрать(ОбходРезультатаЗапроса.ПоГруппировкам, "Колонка");
			
			Пока ВыборкаКолонки.Следующий() Цикл
				
				ОписаниеКолонки = сОписаниеКолонок.Получить(ВыборкаКолонки.Колонка);
				
				СтрокаДетали[СтрШаблон("КроссТаблица%1_Представление", 	ОписаниеКолонки.Ключ)] = ОписаниеКолонки.Представление;
				СтрокаДетали[СтрШаблон("КроссТаблица%1_Ключ", 			ОписаниеКолонки.Ключ)] = ОписаниеКолонки.Ключ;
				СтрокаДетали[СтрШаблон("КроссТаблица%1_Ссылка", 		ОписаниеКолонки.Ключ)] = ОписаниеКолонки.Ссылка;
				СтрокаДетали[СтрШаблон("КроссТаблица%1_Значение", 		ОписаниеКолонки.Ключ)] = ВыборкаКолонки.ЗначениеКолонки;
				
			КонецЦикла;	
			
		КонецЦикла;
		
	КонецЦикла;
	
	Если КроссТаблица.Количество() > 1 Тогда
		КроссТаблица[1].ЭтоПерваяСтрока 								= Истина;
		КроссТаблица[КроссТаблица.Количество() - 1].ЭтоПоследняяСтрока 	= Истина;
	КонецЕсли;

КонецПроцедуры // ОбновитьКроссТаблицу()

#КонецОбласти

#Область ЗаписьДанных

// <Описание процедуры>
//
// Параметры:
//  <Параметр1>  - <Тип.Вид> - <описание параметра>
//                 <продолжение описания параметра>
//  <Параметр2>  - <Тип.Вид> - <описание параметра>
//                 <продолжение описания параметра>
//
&НаСервере
Процедура УдалитьВыбранныеДанныеНаСервере(Отказ, ВыполнятьВТранзакции = Ложь,
		ДопОтборСценарий = Неопределено, ДопОтборДатаНачала = Неопределено, ДопОтборДатаОкончания = Неопределено, ДопОтборВидНачисления = Неопределено)
	
	Если ВыполнятьВТранзакции Тогда
		
		НачатьТранзакцию();
		
	КонецЕсли;
	
	Документы.УстановкаКоэффициентовНачислений.УдалитьКоэффициенты(
		ДопОтборСценарий,
		ДопОтборДатаНачала,
		ДопОтборДатаОкончания,
		ДопОтборВидНачисления);
	
	Если ВыполнятьВТранзакции Тогда
		
		Если Не Отказ Тогда
			ЗафиксироватьТранзакцию();
		Иначе
			ОтменитьТранзакцию();
		КонецЕсли;
		
	КонецЕсли;

КонецПроцедуры // УдалитьВыбранныеДанныеНаСервере()

// <Описание процедуры>
//
// Параметры:
//  <Параметр1>  - <Тип.Вид> - <описание параметра>
//                 <продолжение описания параметра>
//  <Параметр2>  - <Тип.Вид> - <описание параметра>
//                 <продолжение описания параметра>
//
&НаСервере
Процедура СкопироватьИзДругогоСценарияНаСервере(СценарийИсточник, Отказ)

	Отказ = Ложь;
	
	НачатьТранзакцию();

	#Область УдалениеДанныхПоТекущемуСценарию
	
	УдалитьВыбранныеДанныеНаСервере(Отказ, Ложь, ОтборСценарий, ОтборПериод.ДатаНачала, ОтборПериод.ДатаОкончания);
		
	#КонецОбласти	
	
	#Область ДобавлениеПоНовомуСценарию
	
	Если Не Отказ Тогда
		
		Документы.УстановкаКоэффициентовНачислений.СкопироватьКоэффициентыПоСценарию(СценарийИсточник,
			ОтборСценарий, ОтборПериод.ДатаНачала, ОтборПериод.ДатаОкончания, ОтборВидНачисления);
		
	КонецЕсли;
	
	#КонецОбласти
	
	Если Не Отказ Тогда
		
		ЗафиксироватьТранзакцию();

	Иначе
		
		ОтменитьТранзакцию();
		
	КонецЕсли;	

КонецПроцедуры // СкорпироватьИзДругогоСценарияНаСервере()

// <Описание процедуры>
//
// Параметры:
//  <Параметр1>  - <Тип.Вид> - <описание параметра>
//                 <продолжение описания параметра>
//  <Параметр2>  - <Тип.Вид> - <описание параметра>
//                 <продолжение описания параметра>
//
&НаСервере
Процедура СкопироватьНаДругойГодНаСервере(ГодИсточник, ГодПриемник, Отказ)

	Отказ = Ложь;
	
	НачатьТранзакцию();

	#Область УдалениеДанныхПоТекущемуСценарию
	
	// удаляем ставки по установленным отбором за год - приемник (который + 1 или - 1 от выбранного)
	УдалитьВыбранныеДанныеНаСервере(Отказ, Ложь, ОтборСценарий, НачалоГода(ГодПриемник), КонецГода(ГодПриемник), ОтборВидНачисления);

	#КонецОбласти	
	
	#Область ДобавлениеПоНовомуСценарию
	
	Если Не Отказ Тогда
		
		Документы.УстановкаКоэффициентовНачислений.СкопироватьКоэффициентыПоГоду(ОтборСценарий, ГодИсточник, ГодПриемник, ОтборВидНачисления);
		
	КонецЕсли;
	
	#КонецОбласти
	
	Если Не Отказ Тогда
		ЗафиксироватьТранзакцию();
	Иначе
		ОтменитьТранзакцию();
	КонецЕсли;	

КонецПроцедуры // СкорпироватьИзДругогоСценарияНаСервере()


// <Описание процедуры>
//
// Параметры:
//  <Параметр1>  - <Тип.Вид> - <описание параметра>
//                 <продолжение описания параметра>
//  <Параметр2>  - <Тип.Вид> - <описание параметра>
//                 <продолжение описания параметра>
//
&НаСервере
Процедура ИзменитьЗначенияКолонокНаСервере(мОписанияЯчеек, НовоеЗначениеЯчейки, ОписаниеЯчейкиДоИзменения = Неопределено)
	
	Отказ = Ложь;
	
	НачатьТранзакцию();

	Для Каждого ОписаниеЯчейки Из мОписанияЯчеек Цикл
		
		СтруктураЗаписи = Документы.УстановкаКоэффициентовНачислений.ПолучитьСтруктуруУстановкиКоэффициента();
		
		ЗаполнитьЗначенияСвойств(СтруктураЗаписи, ОписаниеЯчейки);
		
		СтруктураЗаписи.Вставить("Коэффициент", НовоеЗначениеЯчейки);
		
		Попытка
		
			Документы.УстановкаКоэффициентовНачислений.УстановитьКоэффициент(СтруктураЗаписи);
		
		Исключение
			
			ОбщегоНазначенияКлиентСервер.СообщитьПользователю(
				СтрШаблон("Не удалось изменить значение: %1, попробуйте еще раз.", ОписаниеОшибки()), ,,, Отказ);
			
		КонецПопытки;
		
		Если Отказ Тогда
			Прервать;
		КонецЕсли;
		
	КонецЦикла;	
	
	Если Не Отказ Тогда
		
		ЗафиксироватьТранзакцию();
		
		Если ОписаниеЯчейкиДоИзменения = Неопределено Тогда
			
			// это изменение не в режиме редактирования ячейки, а какие то массовые изменения
			// в таком случае значение в ячейку присвивать нужно тоже программно, после успешного изменения документов
			Для Каждого ОписаниеЯчейки Из мОписанияЯчеек Цикл
				// устанавливаем новые значения
				СтрокаТаблицы 										= КроссТаблица.НайтиПоИдентификатору(ОписаниеЯчейки.ИдентификаторСтроки);
				СтрокаТаблицы[ОписаниеЯчейки.ИмяКолонкиЗначения] 	= НовоеЗначениеЯчейки;
			КонецЦикла;	
			
		КонецЕсли;	
	Иначе
		
		Если Не ОписаниеЯчейкиДоИзменения = Неопределено  Тогда
			// изменение непосредственно в ячейке кросс таблицы (при изменении)	
			Для Каждого ОписаниеЯчейки Из мОписанияЯчеек Цикл
				// восстанавливаем значение до изменения
				СтрокаТаблицы = КроссТаблица.НайтиПоИдентификатору(ОписаниеЯчейки.ИдентификаторСтроки);
				Если Не СтрокаТаблицы = Неопределено Тогда
					СтрокаТаблицы[ОписаниеЯчейки.ИмяКолонкиЗначения] = СтароеЗначениеЯчейки.Ставка;
				КонецЕсли;
			КонецЦикла;	
			
		КонецЕсли;	
		
	КонецЕсли;	

КонецПроцедуры //

	
#КонецОбласти

#Область Вспомогательные
   
// <Описание процедуры>
//
// Параметры:
//  <Параметр1>  - <Тип.Вид> - <описание параметра>
//                 <продолжение описания параметра>
//  <Параметр2>  - <Тип.Вид> - <описание параметра>
//                 <продолжение описания параметра>
//
&НаСервере
Процедура ЗагрузитьИзExcelНаСервере(Данные, Отказ)
	
	УстановитьПривилегированныйРежим(Истина);
	
	#Область ЧтениеИПоискСсылок
		
	Если Данные[0].Количество() < 2 Тогда
		ОбщегоНазначенияКлиентСервер.СообщитьПользователю(
			"Нет данных для загрузки",,,, Отказ);
		Возврат;
	КонецЕсли;	
	
	Запрос 					= Новый Запрос("Выбрать Т.Ссылка, Т.Наименование Из Справочник.ВидыНачислений Как Т Где Не Т.ПометкаУдаления");
	ТЗ_ВидыНачислений 		= Запрос.Выполнить().Выгрузить();
	
	Запрос 					= Новый Запрос("Выбрать Т.Ссылка, ПРЕДСТАВЛЕНИЕССЫЛКИ(Т.Ссылка) Как Наименование Из Перечисление.ПериодыХраненияЗначений Как Т");
	ТЗ_ПериодыДействия 		= Запрос.Выполнить().Выгрузить();
	
	сВидыНачислений 		= Новый Соответствие;
	сПериодыДействия		= Новый Соответствие;
	сГоды					= Новый Соответствие;
	
	мОшибкиГодов 			= Новый Массив;
	
	#Область Периоды
	Для ИндексКолонкиФайла = 2 По Данные.ВГраница() Цикл
		
		// в колонке должен быть вид начисления
		СтрокаГод 	= СтрЗаменить(СтрЗаменить(Данные[ИндексКолонкиФайла][0], Символы.НПП, ""), " ", "");
		ДатаГод		= '00010101';
		Если Не СтроковыеФункцииКлиентСервер.ТолькоЦифрыВСтроке(СтрокаГод) Тогда
			мОшибкиГодов.Добавить(СтрШаблон("неправильный формат года: %1", СтрокаГод));
		Иначе
			Попытка
				Год 	= Число(СтрокаГод);
				ДатаГод = Дата(Год, 1, 1);
				сГоды.Вставить(ИндексКолонкиФайла, ДатаГод);
			Исключение
				мОшибкиГодов.Добавить(СтрШаблон("не удалось определить год: %1", СтрокаГод));
			КонецПопытки;
			
		КонецЕсли;
		
	КонецЦикла;
	
	Если мОшибкиГодов.Количество() > 0 Тогда
		ОбщегоНазначенияКлиентСервер.СообщитьПользователю(
			СтрШаблон("Не удалось выполнить загрузку данных: %1", СтрСоединить(мОшибкиГодов)),,,, Отказ);
		Возврат;
	КонецЕсли;

	#КонецОбласти
	
	ТЗ = Новый ТаблицаЗначений;
	ТЗ.Колонки.Добавить("ВидНачисления");
	ТЗ.Колонки.Добавить("ПериодДействия");
	ТЗ.Колонки.Добавить("Коэффициент");
	ТЗ.Колонки.Добавить("Дата");
	
	Для ИндексСтрокиФайла = 1 По Данные[0].ВГраница() Цикл
		
		мОшибкиПоСтроке = Новый Массив;
		
		#Область ВидНачисления
		НаименованиеВидаНачисления 	= Данные[0][ИндексСтрокиФайла];
		СтрокаВидНачисления			= ТЗ_ВидыНачислений.Найти(НаименованиеВидаНачисления, "Наименование");
		СсылкаВидНачисления			= Неопределено;
		Если СтрокаВидНачисления = Неопределено Тогда
			мОшибкиПоСтроке.Добавить(
				СтрШаблон("не найден вид начисления по наименованию: %1", НаименованиеВидаНачисления));
		Иначе
			СсылкаВидНачисления = СтрокаВидНачисления.Ссылка;
		КонецЕсли;
		#КонецОбласти
		
		#Область ПериодДействия
		НаименованиеПериодаДействия 	= Данные[1][ИндексСтрокиФайла];
		СсылкаПериодДействия			= ПредопределенноеЗначение("Перечисление.ПериодыХраненияЗначений.ПустаяСсылка");
		Если ЗначениеЗаполнено(НаименованиеПериодаДействия) Тогда
			СтрокаПериодДействия			= ТЗ_ПериодыДействия.Найти(НаименованиеПериодаДействия, "Наименование");
			Если СтрокаПериодДействия = Неопределено Тогда
				мОшибкиПоСтроке.Добавить(
					СтрШаблон("не найден период действия по наименованию: %1", НаименованиеПериодаДействия));
			Иначе
				СсылкаПериодДействия = СтрокаПериодДействия.Ссылка;
			КонецЕсли;
		КонецЕсли;	
		#КонецОбласти
		
		#Область ЧтениеЧисловыхЗначений
		Если мОшибкиПоСтроке.Количество() = 0 Тогда
			
			Для ИндексКолонкиФайла = 2 По Данные.ВГраница() Цикл
				
				ЗначениеЯчейки 		= Данные[ИндексКолонкиФайла][ИндексСтрокиФайла];
				ЗначениеЯчейкиЧисло = 0;
				Если ЗначениеЗаполнено(ЗначениеЯчейки) Тогда
					Попытка
						ЗначениеЯчейкиЧисло = Число(ЗначениеЯчейки);
					Исключение
						ЗначениеЯчейкиЧисло = Неопределено;
						мОшибкиПоСтроке.Добавить(
							СтрШаблон("Не удалось преобразовать к числу значение ячейки в колонке № %1: %2", (ИндексКолонкиФайла + 1), ЗначениеЯчейки));
					КонецПопытки;
				КонецЕсли;
				
				Если Не ЗначениеЯчейкиЧисло = Неопределено Тогда
					НовСтр 					= ТЗ.Добавить();
	             	НовСтр.ВидНачисления 	= СсылкаВидНачисления;
					НовСтр.ПериодДействия 	= СсылкаПериодДействия;
					НовСтр.Дата				= сГоды.Получить(ИндексКолонкиФайла);
					НовСтр.Коэффициент		= ЗначениеЯчейкиЧисло;
				КонецЕсли;
			КонецЦикла;
			
		КонецЕсли;
		#КонецОбласти
		
		Если Не мОшибкиПоСтроке.Количество() = 0 Тогда
			
			ОбщегоНазначенияКлиентСервер.СообщитьПользователю(
				СтрШаблон("Не удалось прочитать данные в строке файла №%1:%2%3", (ИндексСтрокиФайла + 1), Символы.ПС + СтрСоединить(мОшибкиПоСтроке, Символы.ПС)),,,, Отказ);
				
		КонецЕсли;
			
	КонецЦикла;
	
	#Область ПроверкаПериодов
	ТЗ_Периоды = ТЗ.Скопировать();
	ТЗ_Периоды.Сортировать("Дата");
	Если ТЗ_Периоды.Количество() > 0 Тогда
		НачалоПериодаФайла = ТЗ_Периоды[0].Дата;
		Если НачалоПериодаФайла <> ОтборПериод.ДатаНачала Тогда
			ОбщегоНазначенияКлиентСервер.СообщитьПользователю(
				СтрШаблон("Начало периода в файле <<%1>> не соответствует началу периода выбранного сценария <%2>", НачалоПериодаФайла, ОтборПериод.ДатаНачала),,"ОтборПериод",, Отказ);
		КонецЕсли;
		ОкончаниеПериодаФайла = КонецГода(ТЗ_Периоды[ТЗ_Периоды.Количество() - 1].Дата);
		Если ОкончаниеПериодаФайла <> КонецГода(ОтборПериод.ДатаОкончания) Тогда
			ОбщегоНазначенияКлиентСервер.СообщитьПользователю(
				СтрШаблон("Конец периода в файле <<%1>> не соответствует концу периода выбранного сценария <%2>", ОкончаниеПериодаФайла, КонецГода(ОтборПериод.ДатаОкончания)),,"ОтборПериод",, Отказ);
		КонецЕсли;		
	КонецЕсли;
	#КонецОбласти            
	
	#КонецОбласти
	
	Если Отказ Тогда
		Возврат;
	КонецЕсли;
	
	НачатьТранзакцию();
	
	Для Каждого Стр Из ТЗ Цикл
		
		СтруктураЗаписи = Документы.УстановкаКоэффициентовНачислений.ПолучитьСтруктуруУстановкиКоэффициента();
		ЗаполнитьЗначенияСвойств(СтруктураЗаписи, Стр);
		СтруктураЗаписи.Вставить("Сценарий", ОтборСценарий);
		Документы.УстановкаКоэффициентовНачислений.УстановитьКоэффициент(СтруктураЗаписи);
		
	КонецЦикла;	
	
	Если Не Отказ Тогда
		ЗафиксироватьТранзакцию();               
	Иначе
		ОтменитьТранзакцию();
	КонецЕсли;

КонецПроцедуры // ЗагрузитьИзExcelНаСервере()
     
// <Описание функции>
//
// Параметры:
//  <Параметр1>  - <Тип.Вид> - <описание параметра>
//                 <продолжение описания параметра>
//  <Параметр2>  - <Тип.Вид> - <описание параметра>
//                 <продолжение описания параметра>
//
// Возвращаемое значение:
//   <Тип.Вид>   - <описание возвращаемого значения>
//
&НаСервере
Функция КроссТаблицуВТабличныйДокументДляВыгрузкиВExcel()
	
	#Область ПодготовкаТаблицыЗначенийДляВывода
		
	ТЗ			= Новый ТаблицаЗначений;
	ТЗ.Колонки.Добавить("ВидНачисления");
	ТЗ.Колонки.Добавить("ПериодДействия");

	мКолонки 				= СформироватьОписаниеКолонок();
	сПредставленияКолонок 	= Новый Соответствие;
	
	Для Каждого ОписаниеКолонки Из мКолонки Цикл
		ТЗ.Колонки.Добавить(ОписаниеКолонки.Ключ, ОбщегоНазначения.ОписаниеТипаЧисло(5,2), , 10);
		сПредставленияКолонок.Вставить(Строка(ОписаниеКолонки.Ключ), Формат(ОписаниеКолонки.Ссылка, "ДФ=yyyy"));
	КонецЦикла;
	
	ТЗ_Источник = РеквизитФормыВЗначение("КроссТаблица");
	
	Для Каждого Стр Из ТЗ_Источник Цикл
		
		НовСтр 					= ТЗ.Добавить();
		НовСтр.ВидНачисления 	= (Стр.ВидНачисления);
		НовСтр.ПериодДействия 	= (Стр.ПериодДействия);
		
		Для Каждого ОписаниеКолонки Из мКолонки Цикл
			НовСтр[ОписаниеКолонки.Ключ] = Стр["КроссТаблица" + ОписаниеКолонки.Ключ + "_Значение"];		
		КонецЦикла;
		
	КонецЦикла;	
	
	#КонецОбласти
	
	#Область ВыводВТабличныйДокумент
		
	ДокументРезультат					= Новый ТабличныйДокумент;
	ДокументРезультат.ОтображатьСетку 	= Ложь;
	
	Построитель 						= Новый ПостроительОтчета;                         
	Построитель.ДобавлениеПредставлений = ТипДобавленияПредставлений.НеДобавлять;
	Построитель.ВыводитьЗаголовокОтчета = Ложь;
	Построитель.ВыводитьОбщиеИтоги 		= Ложь;
	Построитель.ВыводитьПодвалОтчета 	= Ложь;
	Построитель.ВыводитьПодвалТаблицы 	= Ложь;
	
	Построитель.ИсточникДанных 			= Новый ОписаниеИсточникаДанных(ТЗ);

	Построитель.Макет 					= Неопределено;
	Макет 								= Построитель.Макет; // форматируем макет
	
	ТекущаяОбласть = Неопределено;
	Для Каждого ОписаниеКолонки Из мКолонки Цикл
		Пока Истина Цикл 
		    ТекущаяОбласть = Макет.НайтиТекст(ОписаниеКолонки.Ключ, ТекущаяОбласть, Макет.Область(), Истина, Истина, Истина, Ложь); 
		    Если ТекущаяОбласть <> Неопределено Тогда 
				ДеталиОбласть 					= Макет.Область(ТекущаяОбласть.Верх + 1, ТекущаяОбласть.Лево, ТекущаяОбласть.Верх + 1, ТекущаяОбласть.Лево);
				ДеталиОбласть.СодержитЗначение 	= Истина;
				ДеталиОбласть.ТипЗначения 		= ОбщегоНазначения.ОписаниеТипаЧисло(5,2);
				ДеталиОбласть.Формат 			= "ЧЦ=5; ЧДЦ=2;";
		    Иначе 
		        Прервать; 
		    КонецЕсли; 
		КонецЦикла;
	КонецЦикла;
	
	Построитель.Макет = Макет;	
	Построитель.Выполнить();
	Построитель.Вывести(ДокументРезультат);
	
	// удаление первого пустого столбца
	ДокументРезультат.УдалитьОбласть(ДокументРезультат.Область(1,1,ДокументРезультат.ВысотаТаблицы,1), ТипСмещенияТабличногоДокумента.ПоГоризонтали);
	
	// ширина колонок вид начисления
	ДокументРезультат.Область(1, 1, 1, 1).ШиринаКолонки = 30;
	ДокументРезультат.Область(1, 2, 1, 2).ШиринаКолонки = 30;

	// оформление колонок ставок
	Для НомерКолонки = 3 По ДокументРезультат.ШиринаТаблицы Цикл
		ТекОбласть 					= ДокументРезультат.Область(1, НомерКолонки, 1, НомерКолонки); 
		ТекОбласть.Текст 			= сПредставленияКолонок.Получить(ТекОбласть.Текст);
		ТекОбласть.ШиринаКолонки 	= 10;
	КонецЦикла;
	
	#КонецОбласти
	
	#Область ПодготовкаДляЗаписиВФайл
		
	Расширение				= "xlsx";
		
	НаименованиеФайла 		= СтрШаблон("Коэффициенты к суммам начисления, %1", ОтборСценарий);
	ИмяВременногоФайла		= ПолучитьИмяВременногоФайла(Расширение);
	ДокументРезультат.Записать(ИмяВременногоФайла, ТипФайлаТабличногоДокумента[Расширение]);
	ДвоичныеДанные			= Новый ДвоичныеДанные(ИмяВременногоФайла);
	
	ДанныеФайла 			= Новый Структура;
	ДанныеФайла.Вставить("СсылкаНаДвоичныеДанныеФайла", 		ПоместитьВоВременноеХранилище(ДвоичныеДанные, УникальныйИдентификатор));
	ДанныеФайла.Вставить("ОтносительныйПуть",                  	"");
	ДанныеФайла.Вставить("ИмяФайла",                            НаименованиеФайла + "." + Расширение);
	ДанныеФайла.Вставить("Наименование",                       	НаименованиеФайла);
	ДанныеФайла.Вставить("Расширение",                         	Расширение);
	ДанныеФайла.Вставить("Размер",                             	ДвоичныеДанные.Размер());
	ДанныеФайла.Вставить("Редактирует",                        	Неопределено);
	ДанныеФайла.Вставить("ПодписанЭП",                         	Ложь);
	ДанныеФайла.Вставить("Зашифрован",                         	Ложь);
	ДанныеФайла.Вставить("ФайлРедактируется",                  	Ложь);
	ДанныеФайла.Вставить("ФайлРедактируетТекущийПользователь", 	Ложь);
	ДанныеФайла.Вставить("ПапкаДляСохранитьКак",				"");
	ДанныеФайла.Вставить("ДатаМодификацииУниверсальная",		ТекущаяУниверсальнаяДата());
	ДанныеФайла.Вставить("ПолноеНаименованиеВерсии",			НаименованиеФайла);
	
	#КонецОбласти

	Возврат ДанныеФайла;

КонецФункции // КроссТаблицуВТабличныйДокументДляВыгрузкиВExcek()

&НаКлиенте
Функция СформироватьСтруктуруЗначенийЯчейкиКроссТаблицы(ИдентификаторСтроки, Знач ИмяКолонки)
	
	СтрокаТаблицы 	= КроссТаблица.НайтиПоИдентификатору(ИдентификаторСтроки);
	
	Возврат Новый Структура("Дата, Сценарий, ВидНачисления, ПериодДействия, Коэффициент,
							|ЭтоПерваяСтрока, ЭтоПоследняяСтрока, ИдентификаторСтроки, ИндексСтроки, ИмяКолонкиЗначения",
					СтрокаТаблицы[СтрЗаменить(ИмяКолонки, "_Значение", 	"_Ссылка")],
					СтрокаТаблицы.Сценарий,
					СтрокаТаблицы.ВидНачисления,
					СтрокаТаблицы.ПериодДействия,
					СтрокаТаблицы[ИмяКолонки],
					
					СтрокаТаблицы.ЭтоПерваяСтрока,
					СтрокаТаблицы.ЭтоПоследняяСтрока,
					ИдентификаторСтроки,
					КроссТаблица.Индекс(СтрокаТаблицы),
					ИмяКолонки);
	
КонецФункции	
     
// <Описание процедуры>
//
// Параметры:
//  <Параметр1>  - <Тип.Вид> - <описание параметра>
//                 <продолжение описания параметра>
//  <Параметр2>  - <Тип.Вид> - <описание параметра>
//                 <продолжение описания параметра>
//
&НаКлиенте
Процедура ОбновитьКонтекстноеМеню(Элемент)
	
	СтароеЗначениеЯчейки = Неопределено;
	
	Если Элемент.ТекущийЭлемент = Неопределено Тогда
		Возврат;
	КонецЕсли;
	
	ИмяКолонки 								= Элемент.ТекущийЭлемент.Имя;
	ТекущиеДанные 							= Элементы.КроссТаблица.ТекущиеДанные;
	
	РазрешеноСкопироватьНаСледующийГод	= Ложь;
	РазрешеноСкопироватьНаПредыдущийГод	= Ложь;
	МножественныйВыбор 					= (Элементы.КроссТаблица.ВыделенныеСтроки.Количество() > 1);
	РазрешеноСкопироватьДоКонцаПериода	= Ложь; 
	РазрешеноСкопироватьДоНачалаПериода	= Ложь; 
	
	Если Не ТекущиеДанные = Неопределено И Не Найти(ИмяКолонки, "КроссТаблицаКолонка_") = 0 Тогда
		
		ОписаниеЯчейки						= СформироватьСтруктуруЗначенийЯчейкиКроссТаблицы(Элементы.КроссТаблица.ТекущаяСтрока, ИмяКолонки);
		
		//РазрешеноСкопироватьВверх 	= Не ОписаниеЯчейки.ЭтоПерваяСтрока; 
		//РазрешеноСкопироватьВниз 	= Не ОписаниеЯчейки.ЭтоПоследняяСтрока;
		
		РазрешеноСкопироватьНаСледующийГод 		= Не МножественныйВыбор И (НачалоГода(ОписаниеЯчейки.Дата) < НачалоГода(ОтборПериод.ДатаОкончания));	 // не первый год в таблице
		РазрешеноСкопироватьНаПредыдущийГод 	= Не МножественныйВыбор И (НачалоГода(ОписаниеЯчейки.Дата) > НачалоГода(ОтборПериод.ДатаНачала));	 // не последний год в таблице

		СтароеЗначениеЯчейки = ОписаниеЯчейки;
		
	КонецЕсли;	
	
	Элементы.КроссТаблицаКонтекстноеМенюКомандаСкопироватьНаПредыдущийГод.Видимость			= РазрешеноСкопироватьНаПредыдущийГод;
	Элементы.КроссТаблицаКонтекстноеМенюКомандаСкопироватьНаСледующийГод.Видимость			= РазрешеноСкопироватьНаСледующийГод;

	Элементы.КроссТаблицаКонтекстноеМенюКомандаРедактироватьВыбранные.Видимость			= МножественныйВыбор;
	
	Элементы.КроссТаблицаКонтекстноеМенюКомандаСкопироватьДоНачалоПериода.Видимость		= РазрешеноСкопироватьНаПредыдущийГод;
	Элементы.КроссТаблицаКонтекстноеМенюКомандаСкопироватьДоКонцаПериода.Видимость		= РазрешеноСкопироватьНаСледующийГод;

КонецПроцедуры // ОбновитьКонтекстноеМеню()

#КонецОбласти



