#Область ПроцедурыОбработчикиСобытийФормы

&НаКлиенте
Процедура ПриОткрытии(Отказ)
	
	ОбновитьКроссТаблицу();
	
КонецПроцедуры

&НаСервере
Процедура ПриСозданииНаСервере(Отказ, СтандартнаяОбработка)
	
	Адрес_ДобавленныеРеквизитыКроссТаблицы 	= ПоместитьВоВременноеХранилище(Новый Массив, УникальныйИдентификатор);
	Адрес_ОписаниеКолонок 					= ПоместитьВоВременноеХранилище(Новый Структура, УникальныйИдентификатор);
	
КонецПроцедуры

#КонецОбласти

#Область ПроцедурыОбработчикиТабличногоПоляКроссТаблица

&НаКлиенте
Процедура КроссТаблицаПриАктивизацииСтроки(Элемент)
	
	ОбновитьКонтекстноеМеню(Элемент);

КонецПроцедуры

&НаКлиенте
Процедура КроссТаблицаПриАктивизацииПоля(Элемент)
	
	ОбновитьКонтекстноеМеню(Элементы.КроссТаблица);
	
КонецПроцедуры
 	   
&НаКлиенте
Процедура КроссТаблицаПередНачаломДобавления(Элемент, Отказ, Копирование, Родитель, Группа, Параметр)
	
	Отказ = Истина;
	
КонецПроцедуры

&НаКлиенте
Процедура КроссТаблицаПередУдалением(Элемент, Отказ)
	
	Отказ = Истина;
	
КонецПроцедуры

// <Описание процедуры>
//
// Параметры:
//  <Параметр1>  - <Тип.Вид> - <описание параметра>
//                 <продолжение описания параметра>
//  <Параметр2>  - <Тип.Вид> - <описание параметра>
//                 <продолжение описания параметра>
//
&НаКлиенте
Процедура Подключаемый_ПриИзмененииЗначенияКолонки(Элемент)
	
	ОписаниеЯчейки = СформироватьСтруктуруЗначенийЯчейкиКроссТаблицы(Элементы.КроссТаблица.ТекущаяСтрока, Элемент.Имя);
	
	Если ЗначениеЗаполнено(ОписаниеЯчейки.Дата)
	Тогда
	
	
		ИзменитьЗначенияКолонокНаСервере(
			ОбщегоНазначенияКлиентСервер.ЗначениеВМассиве(ОписаниеЯчейки),
			ОписаниеЯчейки.Значение,
			СтароеЗначениеЯчейки);
		
	КонецЕсли;	
	
	ОбновитьКонтекстноеМеню(Элементы.КроссТаблица);

КонецПроцедуры // Подключаемый_ПриИзмененииЗначенияКолонки()

#КонецОбласти                                          

#Область ПроцедурыОбработчикиСобытийЭлементовФормы

&НаКлиенте
Процедура ОтборСценарийПриИзменении(Элемент)
	
	ОтборСценарийПриИзмененииНаСервере();
	
	ОбновитьКроссТаблицуНаКлиенте();
	
КонецПроцедуры

// <Описание процедуры>
//
// Параметры:
//  <Параметр1>  - <Тип.Вид> - <описание параметра>
//                 <продолжение описания параметра>
//  <Параметр2>  - <Тип.Вид> - <описание параметра>
//                 <продолжение описания параметра>
//
&НаСервере
Процедура ОтборСценарийПриИзмененииНаСервере()

	Если Не ЗначениеЗаполнено(ОтборСценарий) Тогда
		Возврат;
	КонецЕсли;

	РеквизитыСценария 	= ОбщегоНазначения.ЗначенияРеквизитовОбъекта(ОтборСценарий, "НачалоДействия, ОкончаниеДействия");
	ОтборПериод			= Новый СтандартныйПериод(РеквизитыСценария.НачалоДействия, РеквизитыСценария.ОкончаниеДействия); 	

КонецПроцедуры // Отбор()


&НаКлиенте
Процедура ОтборПериодПриИзменении(Элемент)
	
	Если Не ЗначениеЗаполнено(ОтборПериод.ДатаНачала)
	 Или Не ЗначениеЗаполнено(ОтборПериод.ДатаОкончания)
	Тогда
		ОбщегоНазначенияКлиентСервер.СообщитьПользователю(
			"Выберите дату начала, окончания!", , "ОтборПериод", "");
		Возврат;
		
	Иначе
		НоваяДатаНачала 	= НачалоГода(ОтборПериод.ДатаНачала);
		НоваяДатаОкончания 	= КонецГода(ОтборПериод.ДатаОкончания);
		Если ОтборПериод.ДатаНачала <> НоваяДатаНачала
		 Или ОтборПериод.ДатаОкончания <> НоваяДатаОкончания
		Тогда
			ОтборПериод = Новый СтандартныйПериод(НоваяДатаНачала, НоваяДатаОкончания);
		КонецЕсли;	
	КонецЕсли;
	
	ОбновитьКроссТаблицуНаКлиенте();
	
КонецПроцедуры
      
&НаКлиенте
Процедура ОтборВидВнебюджетногоФондаПриИзменении(Элемент)
	
	ОбновитьКроссТаблицуНаКлиенте();
	
КонецПроцедуры
    
&НаКлиенте
Процедура ОтборВидВнебюджетногоФондаОчистка(Элемент, СтандартнаяОбработка)
	
	ОбновитьКроссТаблицуНаКлиенте();
	
КонецПроцедуры
	
#КонецОбласти

#Область ПроцедурыОбработчикиКоманд

&НаКлиенте
Процедура КомандаОбновить(Команда)
	
	Если Не ПроверитьЗаполнение() Тогда
		Возврат;
	КонецЕсли;
	
	ОбновитьКроссТаблицуНаКлиенте();
	
КонецПроцедуры


&НаКлиенте
Процедура КомандаСкопироватьДоКонцаПериода(Команда)
	
	СкопироватьДоНачалоКонцаПериода(1);
	
КонецПроцедуры
 
&НаКлиенте
Процедура КомандаСкопироватьДоНачалоПериода(Команда)
	
	СкопироватьДоНачалоКонцаПериода(-1);
	
КонецПроцедуры

// <Описание процедуры>
//
// Параметры:
//  <Параметр1>  - <Тип.Вид> - <описание параметра>
//                 <продолжение описания параметра>
//  <Параметр2>  - <Тип.Вид> - <описание параметра>
//                 <продолжение описания параметра>
//
&НаКлиенте
Процедура СкопироватьДоНачалоКонцаПериода(Направление)
	
	ЭлементКолонка	= Элементы.КроссТаблица.ТекущийЭлемент;
	Если ЭлементКолонка = Неопределено Тогда
		Возврат;
	КонецЕсли;
	
	ОписаниеЯчейки	= СформироватьСтруктуруЗначенийЯчейкиКроссТаблицы(Элементы.КроссТаблица.ТекущаяСтрока, ЭлементКолонка.Имя);
	
	Если ОписаниеЯчейки = Неопределено Или ОписаниеЯчейки.ЭтоГруппаСтрок Тогда
		Возврат;
	КонецЕсли;
	
	мЯчейки = Новый Массив;
	
	Если Направление = 1 Тогда
		
		Для Инд = ОписаниеЯчейки.ИндексСтроки По КроссТаблица.Количество() - 1 Цикл
			
			мЯчейки.Добавить(
				СформироватьСтруктуруЗначенийЯчейкиКроссТаблицы(
					КроссТаблица.Получить(Инд).ПолучитьИдентификатор(), ЭлементКолонка.Имя));
					
		КонецЦикла;	

	Иначе
		
		Для Инд = 0 По ОписаниеЯчейки.ИндексСтроки Цикл
			
			мЯчейки.Добавить(
				СформироватьСтруктуруЗначенийЯчейкиКроссТаблицы(
					КроссТаблица.Получить(Инд).ПолучитьИдентификатор(), ЭлементКолонка.Имя));
					
		КонецЦикла;	

	КонецЕсли;
	
	ИзменитьЗначенияКолонокНаСервере(
		мЯчейки,
		ОписаниеЯчейки.Значение);
	
КонецПроцедуры // ¶КонецПроцедуры()


&НаКлиенте
Процедура КомандаРедактироватьВыбранные(Команда)
	
	ЭлементКолонка	= Элементы.КроссТаблица.ТекущийЭлемент;
	Если ЭлементКолонка = Неопределено Тогда
		Возврат;
	КонецЕсли;
	
	мЯчейки = Новый Массив;
	
	Для Каждого ВыделеннаяСтрока Из Элементы.КроссТаблица.ВыделенныеСтроки Цикл
		
		мЯчейки.Добавить(
			СформироватьСтруктуруЗначенийЯчейкиКроссТаблицы(ВыделеннаяСтрока, ЭлементКолонка.Имя));
			
	КонецЦикла;
	
	ИмяРеквизита = "";
	Если мЯчейки.Количество() > 0 Тогда
		ИмяРеквизита = мЯчейки[0].ИмяРеквизита;
	КонецЕсли;
	Если Не ЗначениеЗаполнено(ИмяРеквизита) Тогда
		Возврат;
	КонецЕсли;
	
	ПоказатьВводЧисла(
		Новый ОписаниеОповещения("КомандаРедактироватьВыбранныеВводЗначенияЗавершение",
			ЭтаФорма,
			Новый Структура("мЯчейки", мЯчейки)),
		0,
		"Значение:",
		?(ИмяРеквизита = "ГраницаНалога", 15,5),
		2);
	
КонецПроцедуры

// <Описание процедуры>
//
// Параметры:
//  <Параметр1>  - <Тип.Вид> - <описание параметра>
//                 <продолжение описания параметра>
//  <Параметр2>  - <Тип.Вид> - <описание параметра>
//                 <продолжение описания параметра>
//
&НаКлиенте
Процедура КомандаРедактироватьВыбранныеВводЗначенияЗавершение(Результат, ДополнительныеПараметры) Экспорт
	
	Если Результат = Неопределено Тогда
		// пользователь отказался от ввода
		Возврат;
	КонецЕсли;
	
	ИзменитьЗначенияКолонокНаСервере(
		ДополнительныеПараметры.мЯчейки,
		Результат);

КонецПроцедуры // КомандаРедактироватьВыбранныеВводЗначенияЗавершение()


&НаКлиенте
Процедура КомандаСкопироватьИзДругогоСценария(Команда)
	
	ОткрытьФорму(
		"Справочник.Сценарии.ФормаВыбора",
		Новый Структура("Заголовок", "Выберите сценарий-источник"),
		ЭтаФорма,,,,
		Новый ОписаниеОповещения("КомандаСкопироватьИзДругогоСценарияВыборСценарияЗавершение", ЭтаФорма),
		РежимОткрытияОкнаФормы.БлокироватьВесьИнтерфейс);
	
КонецПроцедуры

// <Описание процедуры>
//
// Параметры:
//  <Параметр1>  - <Тип.Вид> - <описание параметра>
//                 <продолжение описания параметра>
//  <Параметр2>  - <Тип.Вид> - <описание параметра>
//                 <продолжение описания параметра>
//
&НаКлиенте
Процедура КомандаСкопироватьИзДругогоСценарияВыборСценарияЗавершение(Результат, ДополнительныеПараметры) Экспорт

	Если Не ЗначениеЗаполнено(Результат) Тогда
		ПоказатьПредупреждение(, "Не выбран сценарий для копирования!");
		Возврат;
	КонецЕсли;
	
	ТекстВопроса = СтрШаблон("Данные по сценарию <%1>, за период <%2>, %3,будут замещены данными сценария <%4>, продолжить?",
					ОтборСценарий,
					ОтборПериод,
					?(Не ЗначениеЗаполнено(ОтборВидВнебюджетногоФонда), "по всем видам внебюджетных фондов", СтрШаблон("по виду фонда <%1>", ОтборВидВнебюджетногоФонда)),
					Результат);
					
	ПоказатьВопрос(
		Новый ОписаниеОповещения("КомандаСкопироватьИзДругогоСценарияПодтвереждениеОперацииЗавершение", ЭтаФорма, Новый Структура("СценарийИсточник", Результат)),
		ТекстВопроса,
		РежимДиалогаВопрос.ДаНет);
		
КонецПроцедуры // КомандаСкопироватьИзДругогоСценария()

// <Описание процедуры>
//
// Параметры:
//  <Параметр1>  - <Тип.Вид> - <описание параметра>
//                 <продолжение описания параметра>
//  <Параметр2>  - <Тип.Вид> - <описание параметра>
//                 <продолжение описания параметра>
//
&НаКлиенте
Процедура КомандаСкопироватьИзДругогоСценарияПодтвереждениеОперацииЗавершение(Результат, ДополнительныеПараметры) Экспорт
	
	Если Не Результат = КодВозвратаДиалога.Да Тогда
		Возврат;
	КонецЕсли;
	
	Отказ = Ложь;
	
	СкопироватьИзДругогоСценарияНаСервере(ДополнительныеПараметры.СценарийИсточник, Отказ);
	
	Если Не Отказ Тогда
		
		ОбновитьКроссТаблицуНаКлиенте();
		
		ПоказатьОповещениеПользователя("Данные обновлены.");
		
	КонецЕсли;	

КонецПроцедуры // ()


&НаКлиенте
Процедура КомандаУдалитьВыбранное(Команда)

	ТекстВопроса = СтрШаблон("Данные по сценарию <%1>, за период <%2>, %3, %4 будут удалены, продолжить?",
					ОтборСценарий,
					ОтборПериод,
					?(Не ЗначениеЗаполнено(ОтборВидВнебюджетногоФонда),
						"по всем видам внебюджетных фондов",
						СтрШаблон("по виду фонда <%1>", ОтборВидВнебюджетногоФонда)));
					
	ПоказатьВопрос(
		Новый ОписаниеОповещения("КомандаУдалитьВыбранноеПодтвереждениеОперацииЗавершение", ЭтаФорма),
		ТекстВопроса,
		РежимДиалогаВопрос.ДаНет);
	// Вставить содержимое обработчика.
КонецПроцедуры

// <Описание процедуры>
//
// Параметры:
//  <Параметр1>  - <Тип.Вид> - <описание параметра>
//                 <продолжение описания параметра>
//  <Параметр2>  - <Тип.Вид> - <описание параметра>
//                 <продолжение описания параметра>
//
&НаКлиенте
Процедура КомандаУдалитьВыбранноеПодтвереждениеОперацииЗавершение(Результат, ДополнительныеПараметры) Экспорт
	
	Если Не Результат = КодВозвратаДиалога.Да Тогда
		Возврат;
	КонецЕсли;
	
	Отказ = Ложь;
	
	УдалитьВыбранныеДанныеНаСервере(Отказ, Истина, ОтборСценарий, ОтборПериод.ДатаНачала, ОтборПериод.ДатаОкончания, ОтборВидВнебюджетногоФонда);
	
	Если Не Отказ Тогда
		
		ОбновитьКроссТаблицуНаКлиенте();
		
		ПоказатьОповещениеПользователя("Данные обновлены.");
		
	КонецЕсли;	

КонецПроцедуры // КомандаУдалитьВыбранноеПодтвереждениеОперацииЗавершение()

 
&НаКлиенте
Процедура КомандаВыгрузитьВExcel(Команда)
	
	РаботаСФайламиКлиент.СохранитьФайлКак(КроссТаблицуВТабличныйДокументДляВыгрузкиВExcel());
	
КонецПроцедуры
   
   
&НаКлиенте
Процедура КомандаЗагрузитьИзExcel(Команда)
	
	Если Не ЗначениеЗаполнено(ОтборСценарий) Тогда
		ОбщегоНазначенияКлиентСервер.СообщитьПользователю(
			"Укажите сценарий, для которого необходимо загрузить данные!",, "ОтборСценарий",,);
		Возврат;
	КонецЕсли;
	
	ПоказатьВопрос(
		Новый ОписаниеОповещения("КомандаЗагрузитьИзExcelПодтверждениеЗагрузки", ЭтаФорма),
		СтрШаблон("Данные по сценарию <<%1>> будут обновлены данными файла, продолжить?", ОтборСценарий),
		РежимДиалогаВопрос.ДаНет);
		
КонецПроцедуры

&НаКлиенте
Процедура КомандаЗагрузитьИзExcelПодтверждениеЗагрузки(Результат, ДополнительныеПараметры) Экспорт
	
	Если Не Результат = КодВозвратаДиалога.Да Тогда
		Возврат;
	КонецЕсли;

	Диалог 						= Новый ДиалогВыбораФайла(РежимДиалогаВыбораФайла.Открытие);
	Диалог.Заголовок 			= "Выберите файл для загрузки...";
	Диалог.МножественныйВыбор 	= Ложь;
	Диалог.Фильтр 				= "XLS (*.xls;*.xlsx)|*.xls;*.xlsx";
	
	Диалог.Показать(Новый ОписаниеОповещения("ЗагрузитьИзExcelПодтверждениеЗагрузкиВыборФайла", ЭтаФорма));

КонецПроцедуры

// <Описание процедуры>
//
// Параметры:
//  <Параметр1>  - <Тип.Вид> - <описание параметра>
//                 <продолжение описания параметра>
//  <Параметр2>  - <Тип.Вид> - <описание параметра>
//                 <продолжение описания параметра>
//
&НаКлиенте
Процедура ЗагрузитьИзExcelПодтверждениеЗагрузкиВыборФайла(Результат, ДополнительныеПараметры) Экспорт
	
	Если Результат = Неопределено Тогда
		Возврат;
	Иначе
		ПутьКФайлу = Результат[0];
	КонецЕсли;

	Попытка
			
		ExcelОбъект = ПолучитьCOMОбъект(ПутьКФайлу); 
		Страница 	= ExcelОбъект.Sheets(1);
		Диапазон	= Страница.Cells(1,1).SpecialCells(11);
		Данные 		= Страница.Range(Страница.Cells(1,1), Страница.Cells(Диапазон.Row, Диапазон.Column)).Value.Выгрузить();

	Исключение
			
		ОбщегоНазначенияКлиентСервер.СообщитьПользователю(
			СтрШаблон("Не удалось прочитать файл: %1", ОписаниеОшибки()));
		Возврат;
		
	КонецПопытки;
	
	ExcelОбъект = Неопределено;
	
	Отказ = Ложь;
	
	ОчиститьСообщения();
	
	ЗагрузитьИзExcelНаСервере(Данные, Отказ);
	
	Если Не Отказ Тогда
		ОбновитьКроссТаблицу();
		ПоказатьОповещениеПользователя("Данные загружены.");
	КонецЕсли;
	
КонецПроцедуры // ЗагрузитьИзExcelПодтверждениеЗагрузкиВыборФайла()


#КонецОбласти

#Область ЗаполнениеДанных

Функция СформироватьОписаниеКолонок()
	
	мОписаниеКолонок = Новый Массив;
	
	Запрос = Новый Запрос;
	Запрос.Текст = 
	"ВЫБРАТЬ РАЗРЕШЕННЫЕ
	|	ВидыВнебюджетныхФондов.Ссылка КАК Ссылка,
	|	ВидыВнебюджетныхФондов.Наименование КАК Наименование
	|ИЗ
	|	Справочник.ВидыВнебюджетныхФондов КАК ВидыВнебюджетныхФондов
	|ГДЕ
	|	НЕ ВидыВнебюджетныхФондов.ПометкаУдаления
	|	И (&ПоВсемВидамВнебюджетныхФондов
	|			ИЛИ ВидыВнебюджетныхФондов.Ссылка = &ВидВнебюджетногоФонда)
	|
	|УПОРЯДОЧИТЬ ПО
	|	ВидыВнебюджетныхФондов.Наименование";
	Запрос.УстановитьПараметр("ПоВсемВидамВнебюджетныхФондов", 	Не ЗначениеЗаполнено(ОтборВидВнебюджетногоФонда));
	Запрос.УстановитьПараметр("ВидВнебюджетногоФонда", 			ОтборВидВнебюджетногоФонда);
	
	Выборка = Запрос.Выполнить().Выбрать();
	
	Пока Выборка.Следующий() Цикл

		ОписаниеКолонки = Новый Структура("Ключ, Представление, Ссылка",
										СтрШаблон("Колонка_%1", ФорматироватьКлючИД(Выборка.Ссылка.УникальныйИдентификатор())),
										СтрШаблон("%1", Выборка.Наименование),
										Выборка.Ссылка);
		мОписаниеКолонок.Добавить(ОписаниеКолонки);

	КонецЦикла;

	ПоместитьВоВременноеХранилище(мОписаниеКолонок, 		Адрес_ОписаниеКолонок);
	
	Возврат мОписаниеКолонок;
	
КонецФункции	
    // <Описание функции>
//
// Параметры:
//  <Параметр1>  - <Тип.Вид> - <описание параметра>
//                 <продолжение описания параметра>
//  <Параметр2>  - <Тип.Вид> - <описание параметра>
//                 <продолжение описания параметра>
//
// Возвращаемое значение:
//   <Тип.Вид>   - <описание возвращаемого значения>
//
&НаСервере
Функция ПодготовитьЗапросДляКроссТаблицы() 
	
	Запрос 			= Новый Запрос;
	Запрос.Текст 	= 
	"ВЫБРАТЬ
	|	0 КАК Число
	|ПОМЕСТИТЬ ВТ_Числа
	|
	|ОБЪЕДИНИТЬ ВСЕ
	|
	|ВЫБРАТЬ
	|	1
	|
	|ОБЪЕДИНИТЬ ВСЕ
	|
	|ВЫБРАТЬ
	|	2
	|
	|ОБЪЕДИНИТЬ ВСЕ
	|
	|ВЫБРАТЬ
	|	3
	|
	|ОБЪЕДИНИТЬ ВСЕ
	|
	|ВЫБРАТЬ
	|	4
	|
	|ОБЪЕДИНИТЬ ВСЕ
	|
	|ВЫБРАТЬ
	|	5
	|
	|ОБЪЕДИНИТЬ ВСЕ
	|
	|ВЫБРАТЬ
	|	6
	|
	|ОБЪЕДИНИТЬ ВСЕ
	|
	|ВЫБРАТЬ
	|	7
	|
	|ОБЪЕДИНИТЬ ВСЕ
	|
	|ВЫБРАТЬ
	|	8
	|
	|ОБЪЕДИНИТЬ ВСЕ
	|
	|ВЫБРАТЬ
	|	9
	|;
	|
	|////////////////////////////////////////////////////////////////////////////////
	|ВЫБРАТЬ РАЗЛИЧНЫЕ
	|	ДОБАВИТЬКДАТЕ(&НачалоПериода, МЕСЯЦ, ВТ_Числа.Число + ВТ_Числа1.Число * 10 + ВТ_Числа2.Число * 100 + ВТ_Числа3.Число * 1000) КАК Месяц
	|ПОМЕСТИТЬ ВТ_Месяцы
	|ИЗ
	|	ВТ_Числа КАК ВТ_Числа
	|		ПОЛНОЕ СОЕДИНЕНИЕ ВТ_Числа КАК ВТ_Числа1
	|			ПОЛНОЕ СОЕДИНЕНИЕ ВТ_Числа КАК ВТ_Числа2
	|				ПОЛНОЕ СОЕДИНЕНИЕ ВТ_Числа КАК ВТ_Числа3
	|				ПО (ИСТИНА)
	|			ПО (ИСТИНА)
	|		ПО (ИСТИНА)
	|ГДЕ
	|	ДОБАВИТЬКДАТЕ(&НачалоПериода, МЕСЯЦ, ВТ_Числа.Число + ВТ_Числа1.Число * 10 + ВТ_Числа2.Число * 100 + ВТ_Числа3.Число * 1000) <= &КонецПериода
	|	И ВЫБОР
	|			КОГДА &НачалоПериода = ДАТАВРЕМЯ(1, 1, 1)
	|					ИЛИ &КонецПериода = ДАТАВРЕМЯ(1, 1, 1)
	|				ТОГДА ЛОЖЬ
	|			ИНАЧЕ ИСТИНА
	|		КОНЕЦ
	|;
	|
	|////////////////////////////////////////////////////////////////////////////////
	|ВЫБРАТЬ РАЗРЕШЕННЫЕ
	|	ВидыВнебюджетныхФондов.Ссылка КАК ВидВнебюджетногоФонда,
	|	&Сценарий КАК Сценарий
	|ПОМЕСТИТЬ ВТ_ПустыеСтавки
	|ИЗ
	|	Справочник.ВидыВнебюджетныхФондов КАК ВидыВнебюджетныхФондов
	|ГДЕ
	|	НЕ ВидыВнебюджетныхФондов.ПометкаУдаления
	|	И (&ПоВсемВидамВнебюджетныхФондов
	|			ИЛИ ВидыВнебюджетныхФондов.Ссылка = &ВидВнебюджетногоФонда)
	|;
	|
	|////////////////////////////////////////////////////////////////////////////////
	|ВЫБРАТЬ РАЗРЕШЕННЫЕ
	|	ВТ_Месяцы.Месяц КАК Месяц,
	|	ВТ_ПустыеСтавки.ВидВнебюджетногоФонда КАК ВидВнебюджетногоФонда,
	|	ВТ_ПустыеСтавки.Сценарий КАК Сценарий,
	|	0 КАК СтавкаНалога,
	|	0 КАК ГраницаНалога,
	|	0 КАК СтавкаНалогаПослеГраницы,
	|	0 КАК СтавкаНалогаПрочее,
	|	0 КАК СтавкаНалогаКорректировка
	|ПОМЕСТИТЬ ВТ_ПустыеСтавкиПоМесяцам
	|ИЗ
	|	ВТ_ПустыеСтавки КАК ВТ_ПустыеСтавки
	|		ВНУТРЕННЕЕ СОЕДИНЕНИЕ ВТ_Месяцы КАК ВТ_Месяцы
	|		ПО (ИСТИНА)
	|;
	|
	|////////////////////////////////////////////////////////////////////////////////
	|ВЫБРАТЬ РАЗРЕШЕННЫЕ
	|	НАЧАЛОПЕРИОДА(УстановкаСтавокНалогов.Дата, Месяц) КАК Месяц,
	|	УстановкаСтавокНалогов.Сценарий КАК Сценарий,
	|	УстановкаСтавокНалогов.ВидВнебюджетногоФонда КАК Колонка,
	|	УстановкаСтавокНалогов.СтавкаНалога КАК Значение_СтавкаНалога,
	|	УстановкаСтавокНалогов.ГраницаНалога КАК Значение_ГраницаНалога,
	|	УстановкаСтавокНалогов.СтавкаНалогаПослеГраницы КАК Значение_СтавкаНалогаПослеГраницы,
	|	УстановкаСтавокНалогов.СтавкаНалогаПрочее КАК Значение_СтавкаНалогаПрочее,
	|	УстановкаСтавокНалогов.СтавкаНалогаКорректировка КАК Значение_СтавкаНалогаКорректировка
	|ИЗ
	|	Документ.УстановкаСтавокНалогов КАК УстановкаСтавокНалогов
	|ГДЕ
	|	УстановкаСтавокНалогов.Сценарий = &Сценарий
	|	И (&ПоВсемВидамВнебюджетныхФондов
	|			ИЛИ УстановкаСтавокНалогов.ВидВнебюджетногоФонда = &ВидВнебюджетногоФонда)
	|	И УстановкаСтавокНалогов.Проведен
	|	И УстановкаСтавокНалогов.Дата МЕЖДУ &НачалоПериода И &КонецПериода
	|
	|ОБЪЕДИНИТЬ ВСЕ
	|
	|ВЫБРАТЬ
	|	ВТ_ПустыеСтавкиПоМесяцам.Месяц,
	|	ВТ_ПустыеСтавкиПоМесяцам.Сценарий,
	|	ВТ_ПустыеСтавкиПоМесяцам.ВидВнебюджетногоФонда,
	|	ВТ_ПустыеСтавкиПоМесяцам.СтавкаНалога,
	|	ВТ_ПустыеСтавкиПоМесяцам.ГраницаНалога,
	|	ВТ_ПустыеСтавкиПоМесяцам.СтавкаНалогаПослеГраницы,
	|	ВТ_ПустыеСтавкиПоМесяцам.СтавкаНалогаПрочее,
	|	ВТ_ПустыеСтавкиПоМесяцам.СтавкаНалогаКорректировка	
	|ИЗ
	|	ВТ_ПустыеСтавкиПоМесяцам КАК ВТ_ПустыеСтавкиПоМесяцам
	|
	|УПОРЯДОЧИТЬ ПО
	|	Месяц,
	|	Колонка
	|ИТОГИ
	|	МАКСИМУМ(Сценарий),
	|	СУММА(Значение_СтавкаНалога),
	|	СУММА(Значение_ГраницаНалога),
	|	СУММА(Значение_СтавкаНалогаПослеГраницы),
	|	СУММА(Значение_СтавкаНалогаПрочее),
	|	СУММА(Значение_СтавкаНалогаКорректировка)
	|ПО
	|	Месяц,
	|	Колонка";
	
	Запрос.УстановитьПараметр("НачалоПериода", 					ОтборПериод.ДатаНачала);
	Запрос.УстановитьПараметр("КонецПериода", 					ОтборПериод.ДатаОкончания);
	Запрос.УстановитьПараметр("Сценарий", 						ОтборСценарий);
	Запрос.УстановитьПараметр("ПоВсемВидамВнебюджетныхФондов", 	Не ЗначениеЗаполнено(ОтборВидВнебюджетногоФонда));
	Запрос.УстановитьПараметр("ВидВнебюджетногоФонда", 			ОтборВидВнебюджетногоФонда);
	
	Возврат Запрос.Выполнить();

КонецФункции // ПодготовитьЗапросДляКроссТаблицы()

// <Описание процедуры>
//
// Параметры:
//  <Параметр1>  - <Тип.Вид> - <описание параметра>
//                 <продолжение описания параметра>
//  <Параметр2>  - <Тип.Вид> - <описание параметра>
//                 <продолжение описания параметра>
//
&НаКлиенте
Процедура ОбновитьКроссТаблицуНаКлиенте()

	ОбновитьКроссТаблицу();
	
КонецПроцедуры // ОбновитьКроссТаблицуНаКлиенте()
  
// <Описание процедуры>
//
// Параметры:
//  <Параметр1>  - <Тип.Вид> - <описание параметра>
//                 <продолжение описания параметра>
//  <Параметр2>  - <Тип.Вид> - <описание параметра>
//                 <продолжение описания параметра>
//
&НаСервере
Процедура ОбновитьКроссТаблицу()

	#Область РеквизитыФормы
		
	мДобавляемыеРеквизитыИмя 	= ПолучитьИзВременногоХранилища(Адрес_ДобавленныеРеквизитыКроссТаблицы);
	мПутиУдаляемыхРеквизитов 	= Новый Массив;
	Для Каждого РеквизитУдалить Из мДобавляемыеРеквизитыИмя Цикл
		
		Если Найти(РеквизитУдалить, "КроссТаблицаГруппа") = 0 Тогда
			мПутиУдаляемыхРеквизитов.Добавить("КроссТаблица." + РеквизитУдалить); 
		КонецЕсли;
		
		НайденныйЭлемент = Элементы.Найти(РеквизитУдалить);
		Если Не НайденныйЭлемент = Неопределено Тогда
			Элементы.Удалить(НайденныйЭлемент);
		КонецЕсли;
		
	КонецЦикла;	
	
	ЭтаФорма.ИзменитьРеквизиты(, мПутиУдаляемыхРеквизитов);
	
	мОписанияКолонок	= СформироватьОписаниеКолонок();

	ОписаниеТипаСтрока150			= ОбщегоНазначения.ОписаниеТипаСтрока(150);
	ОписаниеТипаЧисло3_2			= ОбщегоНазначения.ОписаниеТипаЧисло(5, 2);
	ОписаниеТипаЧисло15_2			= ОбщегоНазначения.ОписаниеТипаЧисло(15, 2);
	
	мДобавляемыеРеквизиты 			= Новый Массив;
	мДобавляемыеРеквизитыИмя 		= Новый Массив;
	мДобавляемыеЭлементыФормы 		= Новый Массив;
	
	сОписаниеКолонок				= Новый Соответствие;
	
	Для Каждого ОписаниеКолонки Из мОписанияКолонок Цикл
		
		сОписаниеКолонок.Вставить(ОписаниеКолонки.Ссылка, ОписаниеКолонки);
		
		мКолонкиНаФорму = Новый Массив;
		
		ИмяГруппыКолонок = СтрШаблон("КроссТаблицаГруппаКолонокПредставление%1", ОписаниеКолонки.Ключ);
		мДобавляемыеРеквизитыИмя.Добавить(ИмяГруппыКолонок);
		
		// колонка, содержащая ключ представление значения
		ИмяРеквизита = СтрШаблон("КроссТаблица%1_Представление", ОписаниеКолонки.Ключ);
		мДобавляемыеРеквизиты.Добавить(Новый РеквизитФормы(ИмяРеквизита, ОписаниеТипаСтрока150, "КроссТаблица",  ОписаниеКолонки.Представление));	
		мДобавляемыеРеквизитыИмя.Добавить(ИмяРеквизита);                           
		
		// колонка, содержащая ключ
		ИмяРеквизита = СтрШаблон("КроссТаблица%1_Ключ", ОписаниеКолонки.Ключ);
		мДобавляемыеРеквизиты.Добавить(Новый РеквизитФормы(ИмяРеквизита, ОписаниеТипаСтрока150, "КроссТаблица",  "Ключ"));	
		мДобавляемыеРеквизитыИмя.Добавить(ИмяРеквизита);

		// колонка, содержащая ссылку на значение колонки (вид начисления)
		ИмяРеквизита = СтрШаблон("КроссТаблица%1_Ссылка", ОписаниеКолонки.Ключ);
		мДобавляемыеРеквизиты.Добавить(Новый РеквизитФормы(ИмяРеквизита, Новый ОписаниеТипов("СправочникСсылка.ВидыВнебюджетныхФондов"), "КроссТаблица",  "Колонка ссылка"));	
		мДобавляемыеРеквизитыИмя.Добавить(ИмяРеквизита);
		
		// колонка, содержащая значение ставки налога
		ИмяРеквизита = СтрШаблон("КроссТаблица%1_Значение_СтавкаНалога", ОписаниеКолонки.Ключ);
		мДобавляемыеРеквизиты.Добавить(Новый РеквизитФормы(ИмяРеквизита, ОписаниеТипаЧисло3_2, "КроссТаблица", "Ставка, %"));	
		мДобавляемыеРеквизитыИмя.Добавить(ИмяРеквизита);
		мКолонкиНаФорму.Добавить(Новый Структура("Имя, Заголовок, Ширина", ИмяРеквизита, "Ставка, %", 10));
		
		// колонка, содержащая значение границы налога
		ИмяРеквизита = СтрШаблон("КроссТаблица%1_Значение_ГраницаНалога", ОписаниеКолонки.Ключ);
		мДобавляемыеРеквизиты.Добавить(Новый РеквизитФормы(ИмяРеквизита, ОписаниеТипаЧисло15_2, "КроссТаблица", "Граница, руб."));	
		мДобавляемыеРеквизитыИмя.Добавить(ИмяРеквизита);
		мКолонкиНаФорму.Добавить(Новый Структура("Имя, Заголовок, Ширина", ИмяРеквизита, "Граница, руб.", 12));

		// колонка, содержащая значение ставки налога после границы
		ИмяРеквизита = СтрШаблон("КроссТаблица%1_Значение_СтавкаНалогаПослеГраницы", ОписаниеКолонки.Ключ);
		мДобавляемыеРеквизиты.Добавить(Новый РеквизитФормы(ИмяРеквизита, ОписаниеТипаЧисло3_2, "КроссТаблица", "Ставка после границы, %"));	
		мДобавляемыеРеквизитыИмя.Добавить(ИмяРеквизита);
		мКолонкиНаФорму.Добавить(Новый Структура("Имя, Заголовок, Ширина", ИмяРеквизита, "Ставка после границы, %", 10));
		
		// колонка, содержащая значение ставки налога прочее
		ИмяРеквизита = СтрШаблон("КроссТаблица%1_Значение_СтавкаНалогаПрочее", ОписаниеКолонки.Ключ);
		мДобавляемыеРеквизиты.Добавить(Новый РеквизитФормы(ИмяРеквизита, ОписаниеТипаЧисло3_2, "КроссТаблица", "Ставка прочее, %"));	
		мДобавляемыеРеквизитыИмя.Добавить(ИмяРеквизита);
		мКолонкиНаФорму.Добавить(Новый Структура("Имя, Заголовок, Ширина", ИмяРеквизита, "Ставка прочее, %", 10));
		
			// колонка, содержащая значение ставки налога корректировка
		ИмяРеквизита = СтрШаблон("КроссТаблица%1_Значение_СтавкаНалогаКорректировка", ОписаниеКолонки.Ключ);
		мДобавляемыеРеквизиты.Добавить(Новый РеквизитФормы(ИмяРеквизита, ОписаниеТипаЧисло3_2, "КроссТаблица", "Ставка налога (корр.), %"));	
		мДобавляемыеРеквизитыИмя.Добавить(ИмяРеквизита);
		мКолонкиНаФорму.Добавить(Новый Структура("Имя, Заголовок, Ширина", ИмяРеквизита, "Ставка налога (корр.), %", 10));
	
		мДобавляемыеЭлементыФормы.Добавить(Новый Структура("Имя, Заголовок, Колонки", 
			ИмяГруппыКолонок, ОписаниеКолонки.Представление, мКолонкиНаФорму));
			
		
	КонецЦикла;
	
	ЭтаФорма.ИзменитьРеквизиты(мДобавляемыеРеквизиты);
	
	ПоместитьВоВременноеХранилище(мДобавляемыеРеквизитыИмя, Адрес_ДобавленныеРеквизитыКроссТаблицы);
	
	Для Каждого ГруппаРеквизитов Из мДобавляемыеЭлементыФормы Цикл
		
		НоваяГруппа									= Элементы.Вставить(ГруппаРеквизитов.Имя, Тип("ГруппаФормы"), Элементы.КроссТаблица, Элементы.КроссТаблицаОтступ);
		Новаягруппа.Заголовок						= ГруппаРеквизитов.Заголовок;
		Новаягруппа.Вид								= ВидГруппыФормы.ГруппаКолонок;
		НоваяГруппа.ОтображатьВШапке				= Истина;
		Новаягруппа.Группировка						= ГруппировкаКолонок.Горизонтальная;
		Новаягруппа.ГоризонтальноеПоложениеВШапке 	= ГоризонтальноеПоложениеЭлемента.Центр;
		Новаягруппа.ШрифтЗаголовка 					= Новый Шрифт(Новаягруппа.ШрифтЗаголовка,,,Истина);
		Новаягруппа.Заголовок						= ГруппаРеквизитов.Заголовок;
		
		Для Каждого ОписаниеРеквизита Из ГруппаРеквизитов.Колонки Цикл

			НовыйЭлемент 								= Элементы.Добавить(ОписаниеРеквизита.Имя, Тип("ПолеФормы"), НоваяГруппа);
			НовыйЭлемент.Вид							= ВидПоляФормы.ПолеВвода;
			НовыйЭлемент.Заголовок 						= ОписаниеРеквизита.Заголовок;
			НовыйЭлемент.ПутьКДанным 					= "КроссТаблица." + ОписаниеРеквизита.Имя;
			НовыйЭлемент.ШрифтЗаголовка 				= Новый Шрифт(НовыйЭлемент.ШрифтЗаголовка,,,Истина);
			НовыйЭлемент.РедактированиеТекста			= Истина;
			НовыйЭлемент.АвтомаксимальнаяШирина			= Ложь;
			НовыйЭлемент.Ширина							= ОписаниеРеквизита.Ширина;
			НовыйЭлемент.РастягиватьПоГоризонтали		= Ложь;
			НовыйЭлемент.ТолькоПросмотр					= Ложь;
			НовыйЭлемент.ОтображатьВШапке				= Истина;

			НовыйЭлемент.УстановитьДействие("ПриИзменении", "Подключаемый_ПриИзмененииЗначенияКолонки");
			
		КонецЦикла;
		
	КонецЦикла;
	
	#КонецОбласти
	
	#Область УсловноеОформление
	
	мЦвета = Новый Массив;
	//мЦвета.Добавить(WebЦвета.ПризрачноБелый);
	мЦвета.Добавить(НЕопределено);
	мЦвета.Добавить(WebЦвета.МятныйКрем);

	УсловноеОформление.Элементы.Очистить();
		
		ИндЦвета = 0;
		Для Каждого ГруппаРеквизитов Из мДобавляемыеЭлементыФормы Цикл
			
			ЭлементУО 	= УсловноеОформление.Элементы.Добавить();

			Для Каждого ОписаниеРеквизита Из ГруппаРеквизитов.Колонки Цикл
				Поля 							= ЭлементУО.Поля.Элементы;
				Поля.Добавить().Поле 			= Новый ПолеКомпоновкиДанных(ОписаниеРеквизита.Имя);
			КонецЦикла;
			
			ЭлементУО.Оформление.УстановитьЗначениеПараметра("ЦветФона", мЦвета[ИндЦвета]);

			ОтборЭлемента 					= ЭлементУО.Отбор.Элементы.Добавить(Тип("ЭлементОтбораКомпоновкиДанных"));
			ОтборЭлемента.ЛевоеЗначение 	= Новый ПолеКомпоновкиДанных("КроссТаблица.Сценарий");
			ОтборЭлемента.ВидСравнения 		= ВидСравненияКомпоновкиДанных.Заполнено;
			
			Если ИндЦвета = 0 Тогда ИндЦвета = 1 Иначе ИндЦвета = 0; КонецЕсли;
			
		КонецЦикла;
	
	#КонецОбласти
	
	КроссТаблица.Очистить();

	РезультатЗапросаДляКроссТаблицы	= ПодготовитьЗапросДляКроссТаблицы();
	ВыборкаМесяц 					= РезультатЗапросаДляКроссТаблицы.Выбрать(ОбходРезультатаЗапроса.ПоГруппировкам, "Месяц");
	ЕстьДобавляемыеКолонки			= (мОписанияКолонок.Количество() > 0);
	
	Пока ВыборкаМесяц.Следующий() Цикл
		
        СтрокаГруппа 				= КроссТаблица.Добавить();
		СтрокаГруппа.Представление 	= Формат(ВыборкаМесяц.Месяц, "ДФ='МММ yy'");
		СтрокаГруппа.Сценарий		= ВыборкаМесяц.Сценарий;
		СтрокаГруппа.Период			= ВыборкаМесяц.Месяц;
		СтрокаГруппа.ЭтоГруппаСтрок	= Ложь;
		
		ИндексСтрокиГруппыСтрок		= СтрокаГруппа.ПолучитьИдентификатор();
		
		ВыборкаКолонки = ВыборкаМесяц.Выбрать(ОбходРезультатаЗапроса.ПоГруппировкам, "Колонка");
			
		Пока ВыборкаКолонки.Следующий() Цикл
				
			ОписаниеКолонки = сОписаниеКолонок.Получить(ВыборкаКолонки.Колонка);
			
			Если ОписаниеКолонки = Неопределено Тогда
				Продолжить;
			КонецЕсли;
			
			СтрокаГруппа[СтрШаблон("КроссТаблица%1_Представление", 	ОписаниеКолонки.Ключ)] = ОписаниеКолонки.Представление;
			СтрокаГруппа[СтрШаблон("КроссТаблица%1_Ключ", 			ОписаниеКолонки.Ключ)] = ОписаниеКолонки.Ключ;
			СтрокаГруппа[СтрШаблон("КроссТаблица%1_Ссылка", 		ОписаниеКолонки.Ключ)] = ОписаниеКолонки.Ссылка;
			
			СтрокаГруппа[СтрШаблон("КроссТаблица%1_Значение_СтавкаНалога", 					ОписаниеКолонки.Ключ)] 	= ВыборкаКолонки.Значение_СтавкаНалога;
			СтрокаГруппа[СтрШаблон("КроссТаблица%1_Значение_ГраницаНалога", 				ОписаниеКолонки.Ключ)] 	= ВыборкаКолонки.Значение_ГраницаНалога;
			СтрокаГруппа[СтрШаблон("КроссТаблица%1_Значение_СтавкаНалогаПослеГраницы", 		ОписаниеКолонки.Ключ)] 	= ВыборкаКолонки.Значение_СтавкаНалогаПослеГраницы;
			СтрокаГруппа[СтрШаблон("КроссТаблица%1_Значение_СтавкаНалогаПрочее", 			ОписаниеКолонки.Ключ)] 	= ВыборкаКолонки.Значение_СтавкаНалогаПрочее;
			СтрокаГруппа[СтрШаблон("КроссТаблица%1_Значение_СтавкаНалогаКорректировка", 	ОписаниеКолонки.Ключ)] 	= ВыборкаКолонки.Значение_СтавкаНалогаКорректировка;
			
		КонецЦикла;	
		
	КонецЦикла;
	
	Если КроссТаблица.Количество() > 0 Тогда
		КроссТаблица[0].ЭтоПерваяСтрока 								= Истина;
		КроссТаблица[КроссТаблица.Количество() - 1].ЭтоПоследняяСтрока 	= Истина;
	КонецЕсли;

КонецПроцедуры // ОбновитьКроссТаблицу()

#КонецОбласти

#Область ЗаписьДанных

// <Описание процедуры>
//
// Параметры:
//  <Параметр1>  - <Тип.Вид> - <описание параметра>
//                 <продолжение описания параметра>
//  <Параметр2>  - <Тип.Вид> - <описание параметра>
//                 <продолжение описания параметра>
//
&НаСервере
Процедура УдалитьВыбранныеДанныеНаСервере(Отказ, ВыполнятьВТранзакции = Ложь,
		ДопОтборСценарий = Неопределено, ДопОтборДатаНачала = Неопределено, ДопОтборДатаОкончания = Неопределено, ДопОтборВидВнебюджетногоФонда = Неопределено)
	
	Если ВыполнятьВТранзакции Тогда
		
		НачатьТранзакцию();
		
	КонецЕсли;
	
	Документы.УстановкаСтавокНалогов.УдалитьСтавки(
		ДопОтборСценарий,
		ДопОтборДатаНачала,
		ДопОтборДатаОкончания,
		ДопОтборВидВнебюджетногоФонда);
	
	Если ВыполнятьВТранзакции Тогда
		
		Если Не Отказ Тогда
			ЗафиксироватьТранзакцию();
		Иначе
			ОтменитьТранзакцию();
		КонецЕсли;
		
	КонецЕсли;

КонецПроцедуры // УдалитьВыбранныеДанныеНаСервере()

// <Описание процедуры>
//
// Параметры:
//  <Параметр1>  - <Тип.Вид> - <описание параметра>
//                 <продолжение описания параметра>
//  <Параметр2>  - <Тип.Вид> - <описание параметра>
//                 <продолжение описания параметра>
//
&НаСервере
Процедура СкопироватьИзДругогоСценарияНаСервере(СценарийИсточник, Отказ)

	Отказ = Ложь;
	
	НачатьТранзакцию();

	#Область УдалениеДанныхПоТекущемуСценарию
	
	УдалитьВыбранныеДанныеНаСервере(Отказ, Ложь, ОтборСценарий, ОтборПериод.ДатаНачала, ОтборПериод.ДатаОкончания);
		
	#КонецОбласти	
	
	#Область ДобавлениеПоНовомуСценарию
	
	Если Не Отказ Тогда
		
		Документы.УстановкаСтавокНалогов.СкопироватьСтавкиПоСценарию(СценарийИсточник,
			ОтборСценарий, ОтборПериод.ДатаНачала, ОтборПериод.ДатаОкончания, ОтборВидВнебюджетногоФонда);
		
	КонецЕсли;
	
	#КонецОбласти
	
	Если Не Отказ Тогда
		
		ЗафиксироватьТранзакцию();

	Иначе
		
		ОтменитьТранзакцию();
		
	КонецЕсли;	

КонецПроцедуры // СкорпироватьИзДругогоСценарияНаСервере()

// <Описание процедуры>
//
// Параметры:
//  <Параметр1>  - <Тип.Вид> - <описание параметра>
//                 <продолжение описания параметра>
//  <Параметр2>  - <Тип.Вид> - <описание параметра>
//                 <продолжение описания параметра>
//
&НаСервере
Процедура ИзменитьЗначенияКолонокНаСервере(мОписанияЯчеек, НовоеЗначение, ОписаниеЯчейкиДоИзменения = Неопределено)
	
	Отказ = Ложь;
	
	НачатьТранзакцию();

	Для Каждого ОписаниеЯчейки Из мОписанияЯчеек Цикл
		
		Если ОписаниеЯчейки.ЭтоГруппаСтрок Тогда
			Продолжить;
		КонецЕсли;
		
		СтруктураЗаписи = Документы.УстановкаСтавокНалогов.ПолучитьСтруктуруУстановкиСтавки();
		
		ЗаполнитьЗначенияСвойств(СтруктураЗаписи, ОписаниеЯчейки);           
		СтруктураЗаписи.Вставить(ОписаниеЯчейки.ИмяРеквизита, НовоеЗначение);
		
		Попытка
		
			Документы.УстановкаСтавокНалогов.УстановитьСтавку(СтруктураЗаписи);
		
		Исключение
			
			ОбщегоНазначенияКлиентСервер.СообщитьПользователю(
				СтрШаблон("Не удалось изменить значение: %1, попробуйте еще раз.", ОписаниеОшибки()), ,,, Отказ);
			
		КонецПопытки;
		
		Если Отказ Тогда
			Прервать;
		КонецЕсли;
		
	КонецЦикла;	
	
	Если Не Отказ Тогда
		
		ЗафиксироватьТранзакцию();
		
		Если ОписаниеЯчейкиДоИзменения = Неопределено Тогда
			
			// это изменение не в режиме редактирования ячейки, а какие то массовые изменения
			// в таком случае значение в ячейку присвивать нужно тоже программно, после успешного изменения документов
			Для Каждого ОписаниеЯчейки Из мОписанияЯчеек Цикл
				Если ОписаниеЯчейки.ЭтоГруппаСтрок Тогда
					Продолжить;
				КонецЕсли;
				// устанавливаем новые значения
				СтрокаТаблицы 								= КроссТаблица.НайтиПоИдентификатору(ОписаниеЯчейки.ИдентификаторСтроки);
				СтрокаТаблицы[ОписаниеЯчейки.ИмяКолонки] 	= НовоеЗначение;
			КонецЦикла;	
			
		КонецЕсли;	
	Иначе
		
		Если Не ОписаниеЯчейкиДоИзменения = Неопределено  Тогда
			// изменение непосредственно в ячейке кросс таблицы (при изменении)	
			Для Каждого ОписаниеЯчейки Из мОписанияЯчеек Цикл
				Если ОписаниеЯчейки.ЭтоГруппаСтрок Тогда
					Продолжить;
				КонецЕсли;
				// восстанавливаем значение до изменения
				СтрокаТаблицы = КроссТаблица.НайтиПоИдентификатору(ОписаниеЯчейки.ИдентификаторСтроки);
				Если Не СтрокаТаблицы = Неопределено Тогда
					СтрокаТаблицы[ОписаниеЯчейки.ИмяКолонки] = СтароеЗначениеЯчейки.Значение;
				КонецЕсли;
			КонецЦикла;	
			
		КонецЕсли;	
		
	КонецЕсли;	

КонецПроцедуры //

	
#КонецОбласти

#Область Вспомогательные
    
// <Описание процедуры>
//
// Параметры:
//  <Параметр1>  - <Тип.Вид> - <описание параметра>
//                 <продолжение описания параметра>
//  <Параметр2>  - <Тип.Вид> - <описание параметра>
//                 <продолжение описания параметра>
//
&НаСервере
Процедура ЗагрузитьИзExcelНаСервере(Данные, Отказ)
	
	УстановитьПривилегированныйРежим(Истина);
	
	#Область ЧтениеИПоискСсылок
		
	Если Данные[0].Количество() < 2 Тогда
		ОбщегоНазначенияКлиентСервер.СообщитьПользователю(
			"Нет данных для загрузки",,,, Отказ);
		Возврат;
	КонецЕсли;	
	
	Запрос 					= Новый Запрос("Выбрать Т.Ссылка, Т.Наименование Из Справочник.ВидыВнебюджетныхФондов Как Т Где Не Т.ПометкаУдаления");
	ТЗ_ВидыФондов 			= Запрос.Выполнить().Выгрузить();
	
	СоответствиеПоказателей = ПолучитьСтруктуруПоказателейДляExcel(Истина);
	
	с_ВидыФондов 			= Новый Соответствие;
	с_Показатели			= Новый Соответствие;
	
	мОшибкиВидыФондов 	= Новый Массив;
	
	#Область ВидыФондов
	Для ИндексКолонкиФайла = 1 По Данные.ВГраница() Цикл
		
		// в колонке должен быть вид фонда + ", " + Представление показателей
		НаименованиеКолонки = Данные[ИндексКолонкиФайла][0];
		ПозицияРазделителя	= СтрНайти(НаименованиеКолонки, ",");
		Если ПозицияРазделителя = 0 Тогда
			
			мОшибкиВидыФондов.Добавить(
				СтрШаблон("Колонка файла %1, не удалось определить вид внебюджетного фонда по наименованию колонки: %2", (ИндексКолонкиФайла + 1), НаименованиеКолонки));
			
		Иначе
			
			НаименованиеВидФонда 	= СокрЛП(Лев(НаименованиеКолонки, ПозицияРазделителя - 1));
			НаименованиеПоказателя 	= СокрЛП(Сред(НаименованиеКолонки, ПозицияРазделителя + 1)); 
		
			СтрокаВидФонда			= ТЗ_ВидыФондов.Найти(НаименованиеВидФонда, "Наименование");
			Если СтрокаВидФонда = Неопределено Тогда
				мОшибкиВидыФондов.Добавить(
					СтрШаблон("Колонка файла %1, не найден вид внебюджетного фонда по наименованию: %2", (ИндексКолонкиФайла + 1), НаименованиеВидФонда));
			Иначе
				с_ВидыФондов.Вставить(ИндексКолонкиФайла, СтрокаВидФонда.Ссылка);
			КонецЕсли;
			
			ИдентификаторПоказателя = СоответствиеПоказателей.Получить(НаименованиеПоказателя);
			Если Не ЗначениеЗаполнено(ИдентификаторПоказателя) Тогда
				мОшибкиВидыФондов.Добавить(
					СтрШаблон("Колонка файла %1, не найден показатель по наименованию: %2", (ИндексКолонкиФайла + 1), НаименованиеПоказателя));
			Иначе
				с_Показатели.Вставить(ИндексКолонкиФайла, ИдентификаторПоказателя);
			КонецЕсли;	
		КонецЕсли;	
	КонецЦикла;
	
	Если мОшибкиВидыФондов.Количество() > 0 Тогда
		ОбщегоНазначенияКлиентСервер.СообщитьПользователю(
			СтрШаблон("Не удалось выполнить загрузку данных: %1", СтрСоединить(мОшибкиВидыФондов)),,,, Отказ);
		Возврат;
	КонецЕсли;
	#КонецОбласти
	
	ТЗ = Новый ТаблицаЗначений;
	ТЗ.Колонки.Добавить("ВидВнебюджетногоФонда");
	ТЗ.Колонки.Добавить("Дата");
	Для Каждого КлючЗначение Из СоответствиеПоказателей Цикл
		ТЗ.Колонки.Добавить(КлючЗначение.Значение);
	КонецЦикла;	
	
	Для ИндексСтрокиФайла = 1 По Данные[0].ВГраница() Цикл
		
		мОшибкиПоСтроке = Новый Массив;
		
		#Область ПолучениеПериода
		СтрокаМесяц 	= Данные[0][ИндексСтрокиФайла];
		ДатаМесяц		= '00010101';
		Попытка
			мЧастиМесяца 	= СтроковыеФункцииКлиентСервер.РазложитьСтрокуВМассивСлов(СтрокаМесяц);
			Год 			= Число(Формат(мЧастиМесяца[2], "ЧГ=0"));
			Месяц 			= Число(Формат(мЧастиМесяца[1], "ЧГ=0"));
			ДатаМесяц		= Дата(Год, Месяц, 1);
		Исключение
			мОшибкиПоСтроке.Добавить(СтрШаблон("не удалось определить месяц: %1", СтрокаМесяц));
		КонецПопытки;
			
		#КонецОбласти
		
		#Область ЧтениеЧисловыхЗначений
		Если мОшибкиПоСтроке.Количество() = 0 Тогда
			
			Для ИндексКолонкиФайла = 1 По Данные.ВГраница() Цикл
				
				ЗначениеЯчейки 		= Данные[ИндексКолонкиФайла][ИндексСтрокиФайла];
				ЗначениеЯчейкиЧисло = 0;
				Если ЗначениеЗаполнено(ЗначениеЯчейки) Тогда
					Попытка
						ЗначениеЯчейкиЧисло = Число(ЗначениеЯчейки);
					Исключение
						ЗначениеЯчейкиЧисло = Неопределено;
						мОшибкиПоСтроке.Добавить(
							СтрШаблон("Не удалось преобразовать к числу значение ячейки в колонке № %1: %2", (ИндексКолонкиФайла + 1), ЗначениеЯчейки));
					КонецПопытки;
				КонецЕсли;
				
				Если Не ЗначениеЯчейкиЧисло = Неопределено Тогда
					
					ВидФондаСсылка						= с_ВидыФондов.Получить(ИндексКолонкиФайла);
					мСтроки								= ТЗ.НайтиСтроки(Новый Структура("ВидВнебюджетногоФонда, Дата", ВидФондаСсылка, ДатаМесяц));
					Если мСтроки.Количество() = 0 Тогда
						НовСтр							= ТЗ.Добавить();
						НовСтр.ВидВнебюджетногоФонда	= ВидФондаСсылка;
						НовСтр.Дата						= ДатаМесяц;
					Иначе
						НовСтр							= мСтроки[0];
					КонецЕсли;
					
					НовСтр[с_Показатели.Получить(ИндексКолонкиФайла)] = ЗначениеЯчейкиЧисло;
				КонецЕсли;
				
			КонецЦикла;
			
		КонецЕсли;
		#КонецОбласти
		
		Если Не мОшибкиПоСтроке.Количество() = 0 Тогда
			
			ОбщегоНазначенияКлиентСервер.СообщитьПользователю(
				СтрШаблон("Не удалось прочитать данные в строке файла №%1:%2%3", (ИндексСтрокиФайла + 1), Символы.ПС + СтрСоединить(мОшибкиПоСтроке, Символы.ПС)),,,, Отказ);
				
		КонецЕсли;
			
	КонецЦикла;
	
	#Область ПроверкаПериодов
	ТЗ_Периоды = ТЗ.Скопировать();
	ТЗ_Периоды.Сортировать("Дата");
	Если ТЗ_Периоды.Количество() > 0 Тогда
		НачалоПериодаФайла = ТЗ_Периоды[0].Дата;
		Если НачалоПериодаФайла <> ОтборПериод.ДатаНачала Тогда
			ОбщегоНазначенияКлиентСервер.СообщитьПользователю(
				СтрШаблон("Начало периода в файле <<%1>> не соответствует началу периода выбранного сценария <%2>", НачалоПериодаФайла, ОтборПериод.ДатаНачала),,"ОтборПериод",, Отказ);
		КонецЕсли;
		ОкончаниеПериодаФайла = КонецМесяца(ТЗ_Периоды[ТЗ_Периоды.Количество() - 1].Дата);
		Если ОкончаниеПериодаФайла <> КонецМесяца(ОтборПериод.ДатаОкончания) Тогда
			ОбщегоНазначенияКлиентСервер.СообщитьПользователю(
				СтрШаблон("Конец периода в файле <<%1>> не соответствует концу периода выбранного сценария <%2>", ОкончаниеПериодаФайла, КонецМесяца(ОтборПериод.ДатаОкончания)),,"ОтборПериод",, Отказ);
		КонецЕсли;		
	КонецЕсли;
	#КонецОбласти            
	
	#КонецОбласти
	
	Если Отказ Тогда
		Возврат;
	КонецЕсли;
	
	НачатьТранзакцию();
	
	Для Каждого Стр Из ТЗ Цикл
		
		СтруктураЗаписи = Документы.УстановкаСтавокНалогов.ПолучитьСтруктуруУстановкиСтавки();
		ЗаполнитьЗначенияСвойств(СтруктураЗаписи, Стр);
		СтруктураЗаписи.Вставить("Сценарий", ОтборСценарий);
		Документы.УстановкаСтавокНалогов.УстановитьСтавку(СтруктураЗаписи);
		
	КонецЦикла;	
	
	Если Не Отказ Тогда
		ЗафиксироватьТранзакцию();               
	Иначе
		ОтменитьТранзакцию();
	КонецЕсли;

КонецПроцедуры // ЗагрузитьИзExcelНаСервере()

// <Описание функции>
//
// Параметры:
//  <Параметр1>  - <Тип.Вид> - <описание параметра>
//                 <продолжение описания параметра>
//  <Параметр2>  - <Тип.Вид> - <описание параметра>
//                 <продолжение описания параметра>
//
// Возвращаемое значение:
//   <Тип.Вид>   - <описание возвращаемого значения>
//
&НаСервере
Функция ПолучитьСтруктуруПоказателейДляExcel(ВернутьСоответствие = Ложь)
	
	// ВернутьСоответствие - перевернуть - ключом будет представление, значением идентификатор
	
	Результат = Новый Структура("СтавкаНалога, ГраницаНалога, СтавкаНалогаПослеГраницы, СтавкаНалогаПрочее, СтавкаНалогаКорректировка",
												"Ставка налога", "Граница налога", "Ставка налога после границы", "Ставка налога прочее", "Ставка налога корректировка");
	Если Не ВернутьСоответствие Тогда
		Возврат Результат;
	КонецЕсли;
	
	СтруктураПолей 	= ОбщегоНазначения.СкопироватьРекурсивно(Результат);
	Результат		= Новый Соответствие;
	Для Каждого КлючЗначение Из СтруктураПолей Цикл
		Результат.Вставить(КлючЗначение.Значение, КлючЗначение.Ключ);
	КонецЦикла;
	
	Возврат Результат;
	
КонецФункции // ПолучитьСтруктуруПоказателейДляExcel()


// <Описание функции>
//
// Параметры:
//  <Параметр1>  - <Тип.Вид> - <описание параметра>
//                 <продолжение описания параметра>
//  <Параметр2>  - <Тип.Вид> - <описание параметра>
//                 <продолжение описания параметра>
//
// Возвращаемое значение:
//   <Тип.Вид>   - <описание возвращаемого значения>
//
&НаСервере
Функция КроссТаблицуВТабличныйДокументДляВыгрузкиВExcel()
	
	#Область ПодготовкаТаблицыЗначенийДляВывода
		
	ТЗ			= Новый ТаблицаЗначений;
	ТЗ.Колонки.Добавить("Период");

	мКолонки 				= СформироватьОписаниеКолонок();
	сПредставленияКолонок 	= Новый Соответствие;
	
	Показатели				= ПолучитьСтруктуруПоказателейДляExcel();
	
	Для Каждого ОписаниеКолонки Из мКолонки Цикл
		Для Каждого КлючЗначение Из Показатели Цикл
			ИмяКолонки = ОписаниеКолонки.Ключ + "_" + КлючЗначение.Ключ;
			ТЗ.Колонки.Добавить(ИмяКолонки);
			сПредставленияКолонок.Вставить(ИмяКолонки, СтрШаблон("%1, %2", ОписаниеКолонки.Ссылка, КлючЗначение.Значение));
		КонецЦикла;	
	КонецЦикла;
	
	ТЗ_Источник = РеквизитФормыВЗначение("КроссТаблица");
	
	Для Каждого Стр Из ТЗ_Источник Цикл
		
		НовСтр 			= ТЗ.Добавить();
		НовСтр.Период 	= Формат(Стр.Период, "ДФ=dd.MM.yyyy");
		
		Для Каждого ОписаниеКолонки Из мКолонки Цикл
			Для Каждого КлючЗначение Из Показатели Цикл
				ИмяКолонки = ОписаниеКолонки.Ключ + "_" + КлючЗначение.Ключ;
				НовСтр[ИмяКолонки] = Стр["КроссТаблица" + ОписаниеКолонки.Ключ + "_Значение_" + КлючЗначение.Ключ];		
			КонецЦикла;
		КонецЦикла;
		
	КонецЦикла;	
	
	#КонецОбласти
	
	#Область ВыводВТабличныйДокумент
		
	ДокументРезультат					= Новый ТабличныйДокумент;
	ДокументРезультат.ОтображатьСетку 	= Ложь;
	
	Построитель 						= Новый ПостроительОтчета;                         
	Построитель.ДобавлениеПредставлений = ТипДобавленияПредставлений.НеДобавлять;
	Построитель.ВыводитьЗаголовокОтчета = Ложь;
	Построитель.ВыводитьОбщиеИтоги 		= Ложь;
	Построитель.ВыводитьПодвалОтчета 	= Ложь;
	Построитель.ВыводитьПодвалТаблицы 	= Ложь;
	
	Построитель.ИсточникДанных 			= Новый ОписаниеИсточникаДанных(ТЗ);

	Построитель.Макет 					= Неопределено;
	Макет 								= Построитель.Макет; // форматируем макет
	
	ТекущаяОбласть = Неопределено;
	Для Каждого ОписаниеКолонки Из мКолонки Цикл
		Пока Истина Цикл 
		    ТекущаяОбласть = Макет.НайтиТекст(ОписаниеКолонки.Ключ, ТекущаяОбласть, Макет.Область(), Истина, Истина, Истина, Ложь); 
		    Если ТекущаяОбласть <> Неопределено Тогда 
				ДеталиОбласть 					= Макет.Область(ТекущаяОбласть.Верх + 1, ТекущаяОбласть.Лево, ТекущаяОбласть.Верх + 1, ТекущаяОбласть.Лево);
				ДеталиОбласть.СодержитЗначение 	= Истина;
				//ДеталиОбласть.ТипЗначения 		= ОбщегоНазначения.ОписаниеТипаЧисло(5,2);
				//ДеталиОбласть.Формат 			= "ЧЦ=5; ЧДЦ=2;";
		    Иначе 
		        Прервать; 
		    КонецЕсли; 
		КонецЦикла;
	КонецЦикла;
	
	Построитель.Макет = Макет;	
	Построитель.Выполнить();
	Построитель.Вывести(ДокументРезультат);
	
	// удаление первого пустого столбца
	ДокументРезультат.УдалитьОбласть(ДокументРезультат.Область(1,1,ДокументРезультат.ВысотаТаблицы,1), ТипСмещенияТабличногоДокумента.ПоГоризонтали);
	
	// ширина колонок вид начисления
	ДокументРезультат.Область(1, 1, 1, 1).ШиринаКолонки = 10;
	//ДокументРезультат.Область(1, 2, 1, 2).ШиринаКолонки = 30;

	// оформление колонок ставок
	Для НомерКолонки = 2 По ДокументРезультат.ШиринаТаблицы Цикл
		ТекОбласть 					= ДокументРезультат.Область(1, НомерКолонки, 1, НомерКолонки); 
		ТекОбласть.Текст 			= сПредставленияКолонок.Получить(ТекОбласть.Текст);
		ТекОбласть.ШиринаКолонки 	= 20;
		ТекОбласть.РазмещениеТекста	=ТипРазмещенияТекстаТабличногоДокумента.Переносить;
	КонецЦикла;
	
	#КонецОбласти
	
	#Область ПодготовкаДляЗаписиВФайл
		
	Расширение				= "xlsx";
		
	НаименованиеФайла 		= СтрШаблон("Ставки налогов, %1", ОтборСценарий);
	ИмяВременногоФайла		= ПолучитьИмяВременногоФайла(Расширение);
	ДокументРезультат.Записать(ИмяВременногоФайла, ТипФайлаТабличногоДокумента[Расширение]);
	ДвоичныеДанные			= Новый ДвоичныеДанные(ИмяВременногоФайла);
	
	ДанныеФайла 			= Новый Структура;
	ДанныеФайла.Вставить("СсылкаНаДвоичныеДанныеФайла", 		ПоместитьВоВременноеХранилище(ДвоичныеДанные, УникальныйИдентификатор));
	ДанныеФайла.Вставить("ОтносительныйПуть",                  	"");
	ДанныеФайла.Вставить("ИмяФайла",                            НаименованиеФайла + "." + Расширение);
	ДанныеФайла.Вставить("Наименование",                       	НаименованиеФайла);
	ДанныеФайла.Вставить("Расширение",                         	Расширение);
	ДанныеФайла.Вставить("Размер",                             	ДвоичныеДанные.Размер());
	ДанныеФайла.Вставить("Редактирует",                        	Неопределено);
	ДанныеФайла.Вставить("ПодписанЭП",                         	Ложь);
	ДанныеФайла.Вставить("Зашифрован",                         	Ложь);
	ДанныеФайла.Вставить("ФайлРедактируется",                  	Ложь);
	ДанныеФайла.Вставить("ФайлРедактируетТекущийПользователь", 	Ложь);
	ДанныеФайла.Вставить("ПапкаДляСохранитьКак",				"");
	ДанныеФайла.Вставить("ДатаМодификацииУниверсальная",		ТекущаяУниверсальнаяДата());
	ДанныеФайла.Вставить("ПолноеНаименованиеВерсии",			НаименованиеФайла);
	
	#КонецОбласти

	Возврат ДанныеФайла;

КонецФункции // КроссТаблицуВТабличныйДокументДляВыгрузкиВExcek()

// <Описание функции>
//
// Параметры:
//  <Параметр1>  - <Тип.Вид> - <описание параметра>
//                 <продолжение описания параметра>
//  <Параметр2>  - <Тип.Вид> - <описание параметра>
//                 <продолжение описания параметра>
//
// Возвращаемое значение:
//   <Тип.Вид>   - <описание возвращаемого значения>
//
&НаСервереБезКонтекста
Функция ФорматироватьКлючИД(Ключ)

	Возврат СтрЗаменить(Строка(Ключ), "-", "_");	

КонецФункции // ФорматироватьКлючИД()

&НаКлиенте
Функция СформироватьСтруктуруЗначенийЯчейкиКроссТаблицы(ИдентификаторСтроки, Знач ИмяКолонки)
	
	СтрокаТаблицы 	= КроссТаблица.НайтиПоИдентификатору(ИдентификаторСтроки); // Имя колонки КроссТаблицаКолонка_010c1c9a_080e_11ed_8116_b49691c52d8c_Ссылка
	
	мИменаКолонок 	= СтроковыеФункцииКлиентСервер.РазложитьСтрокуВМассивПодстрок(ИмяКолонки, "_");
	ИмяРеквизита	= "";
	Если мИменаКолонок.Количество() > 1 Тогда
		ИмяРеквизита = мИменаКолонок[мИменаКолонок.ВГраница()];
		мИменаКолонок.Удалить(мИменаКолонок.ВГраница());
	КонецЕсли;
	ПрефиксИмениКолонки = СтрСоединить(мИменаКолонок, "_");

	Возврат Новый Структура("Дата, Сценарий, ВидВнебюджетногоФонда, ЭтоГруппаСтрок, ИмяКолонки, Значение, ИмяРеквизита,
	
							|СтавкаНалога, ГраницаНалога, СтавкаНалогаПослеГраницы, СтавкаНалогаПрочее, СтавкаНалогаКорректировка,
							
							|ЭтоПерваяСтрока, ЭтоПоследняяСтрока, ИдентификаторСтроки, ИндексСтроки",
	
					СтрокаТаблицы.Период,
					СтрокаТаблицы.Сценарий,
					СтрокаТаблицы[СтрЗаменить(ПрефиксИмениКолонки, "_Значение", "_Ссылка")],
					СтрокаТаблицы.ЭтоГруппаСтрок,
					ИмяКолонки,
					СтрокаТаблицы[ИмяКолонки],
					ИмяРеквизита,
					
					СтрокаТаблицы[ПрефиксИмениКолонки + "_СтавкаНалога"],
					СтрокаТаблицы[ПрефиксИмениКолонки + "_ГраницаНалога"],
					СтрокаТаблицы[ПрефиксИмениКолонки + "_СтавкаНалогаПослеГраницы"],
					СтрокаТаблицы[ПрефиксИмениКолонки + "_СтавкаНалогаПрочее"],
					СтрокаТаблицы[ПрефиксИмениКолонки + "_СтавкаНалогаКорректировка"],
					
					СтрокаТаблицы.ЭтоПерваяСтрока,
					СтрокаТаблицы.ЭтоПоследняяСтрока,
					ИдентификаторСтроки,
					КроссТаблица.Индекс(СтрокаТаблицы));
	
КонецФункции	
     
// <Описание процедуры>
//
// Параметры:
//  <Параметр1>  - <Тип.Вид> - <описание параметра>
//                 <продолжение описания параметра>
//  <Параметр2>  - <Тип.Вид> - <описание параметра>
//                 <продолжение описания параметра>
//
&НаКлиенте
Процедура ОбновитьКонтекстноеМеню(Элемент)

	СтароеЗначениеЯчейки = Неопределено;
	
	Если Элемент.ТекущийЭлемент = Неопределено Тогда
		Возврат;
	КонецЕсли;
	
	ИмяКолонки 								= Элемент.ТекущийЭлемент.Имя;
	ТекущиеДанные 							= Элементы.КроссТаблица.ТекущиеДанные;
	
	РазрешеноСкопироватьВверх 		= Ложь;
	РазрешеноСкопироватьВниз 		= Ложь;
	РазрешеноРедактировать 			= Ложь;
	
	Если Не ТекущиеДанные = Неопределено И Не Найти(ИмяКолонки, "КроссТаблицаКолонка_") = 0 Тогда
		
		ОписаниеЯчейки						= СформироватьСтруктуруЗначенийЯчейкиКроссТаблицы(Элементы.КроссТаблица.ТекущаяСтрока, ИмяКолонки);
		
		Если Не ОписаниеЯчейки.ЭтоГруппаСтрок Тогда
			
			РазрешеноСкопироватьВверх 	= Не ОписаниеЯчейки.ЭтоПерваяСтрока; 
			РазрешеноСкопироватьВниз 	= Не ОписаниеЯчейки.ЭтоПоследняяСтрока;
			РазрешеноРедактировать		= Элементы.КроссТаблица.ВыделенныеСтроки.Количество() > 1;
			
		Иначе
			
		КонецЕсли;
		
		СтароеЗначениеЯчейки = ОписаниеЯчейки;
		
	КонецЕсли;	
	
	Элементы.КроссТаблицаКонтекстноеМенюКомандаРедактироватьВыбранные.Видимость			= РазрешеноРедактировать;
	Элементы.КроссТаблицаКонтекстноеМенюКомандаСкопироватьДоНачалоПериода.Видимость		= РазрешеноСкопироватьВверх И Не РазрешеноРедактировать;
	Элементы.КроссТаблицаКонтекстноеМенюКомандаСкопироватьДоКонцаПериода.Видимость		= РазрешеноСкопироватьВниз И Не РазрешеноРедактировать;

КонецПроцедуры // ОбновитьКонтекстноеМеню()

#КонецОбласти



