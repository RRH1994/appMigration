#Если Сервер Или ТолстыйКлиентОбычноеПриложение Или ВнешнееСоединение Тогда

#Область ПрограммныйИнтерфейс

#Область ДляВызоваИзДругихПодсистем

// СтандартныеПодсистемы.ВерсионированиеОбъектов

// Определяет настройки объекта для подсистемы ВерсионированиеОбъектов.
//
// Параметры:
//  Настройки - Структура - настройки подсистемы.
Процедура ПриОпределенииНастроекВерсионированияОбъектов(Настройки) Экспорт

КонецПроцедуры
// Конец СтандартныеПодсистемы.ВерсионированиеОбъектов

// СтандартныеПодсистемы.УправлениеДоступом

// См. УправлениеДоступомПереопределяемый.ПриЗаполненииСписковСОграничениемДоступа.
Процедура ПриЗаполненииОграниченияДоступа(Ограничение) Экспорт
	
	//Ограничение.Текст =
	//"РазрешитьЧтениеИзменение
	//|ГДЕ
	//|	ДляВсехСтрок( ЗначениеРазрешено(ФизическиеЛица.ФизическоеЛицо, NULL КАК ИСТИНА)
	//|	) И ЗначениеРазрешено(Организация)";   
	
	Ограничение.Текст =
	"ПрисоединитьДополнительныеТаблицы
	|ЭтотСписок КАК Т
	|
	|ЛЕВОЕ СОЕДИНЕНИЕ Документ.РасчетПоказателейБюджетовПрогнозов.Начисления КАК Т1
	|	ПО Т1.Ссылка = Т.Ссылка  
	|
	|ЛЕВОЕ СОЕДИНЕНИЕ Документ.РасчетПоказателейБюджетовПрогнозов.Взносы КАК Т2
	|	ПО Т2.Ссылка = Т.Ссылка
	|;
	|РазрешитьЧтениеИзменение
	|ГДЕ
	|	ЗначениеРазрешено(Т.Организация)
	|	И (Т1.Подразделение ЕСТЬ NULL
	|		ИЛИ ДляВсехСтрок(
	|			ЗначениеРазрешено(Т1.Подразделение)
	|			ИЛИ ДляОднойИзСтрок(ЗначениеРазрешено(Т1.Подразделение)))
	|		ИЛИ ДляВсехСтрок(
	|			ЗначениеРазрешено(Т2.Подразделение)
	|			ИЛИ ДляОднойИзСтрок(ЗначениеРазрешено(Т2.Подразделение))))";
	
КонецПроцедуры

// Конец СтандартныеПодсистемы.УправлениеДоступом

#КонецОбласти

// Проводит документ по учетам. Если в параметре ВидыУчетов передано Неопределено, то документ проводится по всем учетам.
// Процедура вызывается из обработки проведения и может вызываться из вне.
// 
// Параметры:
//  ДокументСсылка	- ДокументСсылка.РасчетПоказателейБюджетовПрогнозов - Ссылка на документ
//  РежимПроведения - РежимПроведенияДокумента - Режим проведения документа (оперативный, неоперативный)
//  Отказ 			- Булево - Признак отказа от выполнения проведения
//  ВидыУчетов 		- Строка - Список видов учета, по которым необходимо провести документ. Если параметр пустой или Неопределено, то документ проведется по всем учетам
//  Движения 		- Коллекция движений документа - Передается только при вызове из обработки проведения документа
//  Объект			- ДокументОбъект.РасчетПоказателейБюджетовПрогнозов - Передается только при вызове из обработки проведения документа
//  ДополнительныеПараметры - Структура - Дополнительные параметры, необходимые для проведения документа.
//
Процедура ПровестиПоУчетам(ДокументСсылка, РежимПроведения, Отказ, ВидыУчетов = Неопределено, Движения = Неопределено, Объект = Неопределено, ДополнительныеПараметры = Неопределено) Экспорт
	
	СтруктураВидовУчета = ПроведениеРасширенныйСервер.СтруктураВидовУчета();
	ПроведениеРасширенныйСервер.ПодготовитьНаборыЗаписейКРегистрацииДвиженийПоВидамУчета(РежимПроведения, ДокументСсылка, СтруктураВидовУчета, ВидыУчетов, Движения, Объект, Отказ);
	
	// Предусмотрен режим проведения по отдельным физическим лицам.
	СписокФизическихЛиц = Неопределено;
	Если ДополнительныеПараметры <> Неопределено  
		И ДополнительныеПараметры.Свойство("ФизическиеЛица")
		И ДополнительныеПараметры.ФизическиеЛица.Количество() > 0 Тогда
		СписокФизическихЛиц = ДополнительныеПараметры.ФизическиеЛица
	КонецЕсли;
	
	РеквизитыДляПроведения = РеквизитыДляПроведения(ДокументСсылка);
	ДанныеДляПроведения = ДанныеДляПроведения(РеквизитыДляПроведения, РеквизитыДляПроведения.ПериодРегистрации, СписокФизическихЛиц, СтруктураВидовУчета);

	Если СтруктураВидовУчета.Начисления Тогда
		РасчетБюджетированиеРасширенный.СформироватьДвиженияФункциональныхБюджетов(Движения, Отказ, РеквизитыДляПроведения.Организация, КонецМесяца(РеквизитыДляПроведения.ПериодРегистрации), ДанныеДляПроведения.Начисления, Истина);
	КонецЕсли;
	
	ПроведениеРасширенныйСервер.ЗаписьДвиженийПоУчетам(Движения, СтруктураВидовУчета);
	
КонецПроцедуры

#КонецОбласти

#КонецЕсли

#Область ОбработчикиСобытий

#КонецОбласти

#Если Сервер Или ТолстыйКлиентОбычноеПриложение Или ВнешнееСоединение Тогда
	
#Область СлужебныйПрограммныйИнтерфейс

#КонецОбласти

#Область СлужебныеПроцедурыИФункции

#Область Печать

#КонецОбласти

Процедура ПодготовитьДанныеДляЗаполнения(СтруктураПараметров, АдресХранилища) Экспорт
	
	РезультатЗаполнения = Новый Структура;
	
	// Получение данных для заполнения табличных частей документа.
	ОписаниеДокумента = СтруктураПараметров.ОписаниеДокумента;
	Организация = СтруктураПараметров.Организация;
	МесяцНачисления = СтруктураПараметров.МесяцНачисления;
	
	ДополнительныеПараметры = РасчетБюджетированиеРасширенный.ДополнительныеПараметрыЗаполненияТаблицДокумента();
	ДополнительныеПараметры.ДокументСсылка = СтруктураПараметров.ДокументСсылка;
	ДополнительныеПараметры.Подразделение = СтруктураПараметров.Подразделение;
	ДополнительныеПараметры.МестоВозникновенияЗатрат = СтруктураПараметров.МестоВозникновенияЗатрат;
	ДополнительныеПараметры.НачалоПериода = СтруктураПараметров.НачалоПериода;
	ДополнительныеПараметры.ОкончаниеПериода = СтруктураПараметров.ОкончаниеПериода;
	ДополнительныеПараметры.ВидРасчета = СтруктураПараметров.ВидРасчета;
	ДополнительныеПараметры.Сценарий = СтруктураПараметров.Сценарий;
	
	ДанныеЗаполнения = РасчетБюджетированиеРасширенный.ДанныеДляЗаполненияТаблицДокумента(ОписаниеДокумента, Организация, МесяцНачисления, ДополнительныеПараметры);
	
	РезультатЗаполнения.Вставить("ДанныеДляЗаполненияТаблицДокумента", ДанныеЗаполнения);
	
	ПоместитьВоВременноеХранилище(РезультатЗаполнения, АдресХранилища);
	
КонецПроцедуры

#Область ПервоначальноеЗаполнениеИОбновлениеИнформационнойБазы

#КонецОбласти   

Функция ДанныеДляПроведения(РеквизитыДляПроведения, МесяцНачисления, СписокФизическихЛиц, СтруктураВидовУчета)
	
	ДанныеДляПроведения = РасчетБюджетирование.СоздатьДанныеДляПроведенияНачисленияБюджетирования();
	
	ОстальныеВидыУчета = Ложь;
	Для Каждого КлючИЗначение Из СтруктураВидовУчета Цикл
		Если ТипЗнч(КлючИЗначение.Значение) = Тип("Булево") 
			И КлючИЗначение.Значение Тогда 
			ОстальныеВидыУчета = Истина;
			Прервать;
		КонецЕсли;
	КонецЦикла;
	
	Если ОстальныеВидыУчета Тогда
		РасчетБюджетированиеРасширенный.ЗаполнитьНачисления(ДанныеДляПроведения, РеквизитыДляПроведения.Ссылка, "Начисления,Взносы", "Начисления.МесяцНачисления", "Начисления.Ссылка.ДатаНачала", "Начисления.Ссылка.ДатаОкончания", , СписокФизическихЛиц);
	Конецесли;	
		
	Возврат ДанныеДляПроведения;
	
КонецФункции

Функция РеквизитыДляПроведения(ДокументСсылка)
	
	Запрос = Новый Запрос;
	Запрос.Текст = 
	"ВЫБРАТЬ РАЗРЕШЕННЫЕ
	|	РасчетПоказателейБюджетовПрогнозов.Ссылка КАК Ссылка,
	|	РасчетПоказателейБюджетовПрогнозов.Ссылка.Сценарий КАК Сценарий,
	|	РасчетПоказателейБюджетовПрогнозов.Дата КАК Дата,
	|	РасчетПоказателейБюджетовПрогнозов.МесяцНачисления КАК ПериодРегистрации,
	|	ВЫБОР
	|		КОГДА РасчетПоказателейБюджетовПрогнозов.Ссылка.Статус = ЗНАЧЕНИЕ(Перечисление.СтатусыДокументовБюджетирования.Утвержден)
	|			ТОГДА ИСТИНА
	|		ИНАЧЕ ЛОЖЬ
	|	КОНЕЦ КАК Утвержден,
	|	НАЧАЛОПЕРИОДА(РасчетПоказателейБюджетовПрогнозов.ДатаНачала, МЕСЯЦ) КАК МесяцНачала,
	|	КОНЕЦПЕРИОДА(РасчетПоказателейБюджетовПрогнозов.ДатаОкончания, МЕСЯЦ) КАК МесяцОкончания,
	|	РасчетПоказателейБюджетовПрогнозов.Организация КАК Организация,
	|	РасчетПоказателейБюджетовПрогнозов.Подразделение КАК Подразделение,
	|	РасчетПоказателейБюджетовПрогнозов.МестоВозникновенияЗатрат КАК МестоВозникновенияЗатрат
	|ИЗ
	|	Документ.РасчетПоказателейБюджетовПрогнозов КАК РасчетПоказателейБюджетовПрогнозов
	|ГДЕ
	|	РасчетПоказателейБюджетовПрогнозов.Ссылка = &Ссылка
	|;
	|
	|////////////////////////////////////////////////////////////////////////////////
	|ВЫБРАТЬ РАЗРЕШЕННЫЕ
	|	РПБПНачисления.ФизическоеЛицо КАК ФизическоеЛицо,
	|	РПБПНачисления.МесяцНачисления КАК МесяцНачисления,
	|	РПБПНачисления.Подразделение КАК Подразделение,
	|	РПБПНачисления.МестоВозникновенияЗатрат КАК МестоВозникновенияЗатрат,
	|	РПБПНачисления.ПозицияШтатногоРасписания КАК ПозицияШтатногоРасписания,
	|	РПБПНачисления.Ссылка.ДатаНачала КАК НачалоПериода,
	|	РПБПНачисления.Ссылка.ДатаОкончания КАК ОкончаниеПериода,
	|	РПБПНачисления.ВидНачисления КАК ВидНачисления,
	|	NULL КАК ВидВнебюджетногоФонда,
	|	NULL КАК ТипНалога,
	|	NULL КАК ПериодДействия,
	|	РПБПНачисления.Результат КАК Сумма
	|ИЗ
	|	Документ.РасчетПоказателейБюджетовПрогнозов.Начисления КАК РПБПНачисления
	|ГДЕ
	|	РПБПНачисления.Ссылка = &Ссылка
	|
	|ОБЪЕДИНИТЬ ВСЕ
	|
	|ВЫБРАТЬ
	|	РПБПВзносы.ФизическоеЛицо,
	|	РПБПВзносы.МесяцНачисления,
	|	РПБПВзносы.Подразделение,
	|	РПБПВзносы.МестоВозникновенияЗатрат,
	|	РПБПВзносы.ПозицияШтатногоРасписания,
	|	РПБПВзносы.Ссылка.ДатаНачала,
	|	РПБПВзносы.Ссылка.ДатаОкончания,
	|	РПБПВзносы.ВидНачисления,
	|	РПБПВзносы.ВидВнебюджетногоФонда,
	|	РПБПВзносы.ТипНалога,
	|	РПБПВзносы.ПериодДействия,
	|	РПБПВзносы.Результат
	|ИЗ
	|	Документ.РасчетПоказателейБюджетовПрогнозов.Взносы КАК РПБПВзносы
	|ГДЕ
	|	РПБПВзносы.Ссылка = &Ссылка";
	
	Запрос.УстановитьПараметр("Ссылка", ДокументСсылка);
	Результаты = Запрос.ВыполнитьПакет();
	
	РеквизитыДляПроведения = РеквизитыДляПроведенияПустаяСтруктура();
	
	ВыборкаРеквизиты = Результаты[0].Выбрать();
	
	Пока ВыборкаРеквизиты.Следующий() Цикл
		ЗаполнитьЗначенияСвойств(РеквизитыДляПроведения, ВыборкаРеквизиты);
	КонецЦикла;
	
	НачисленияВзносы = Результаты[1].Выгрузить();
	РеквизитыДляПроведения.НачисленияВзносы = НачисленияВзносы;
	
	Возврат РеквизитыДляПроведения;
	
КонецФункции

Функция РеквизитыДляПроведенияПустаяСтруктура()
	
	РеквизитыДляПроведенияПустаяСтруктура = Новый Структура("Ссылка, Сценарий, Дата, ПериодРегистрации, МесяцНачала, МесяцОкончания, Утвержден, Организация, Подразделение, МестоВозникновенияЗатрат, НачисленияВзносы");	
	
	Возврат РеквизитыДляПроведенияПустаяСтруктура;
	
КонецФункции

Функция РазложитьПолноеИмяОбъекта(ПолноеИмяОбъекта) Экспорт
	Результат = СтрРазделить(ПолноеИмяОбъекта, ".", Ложь);
	
	ИмяОбъекта = Новый Структура;
	ИмяОбъекта.Вставить("ПолноеИмяОбъекта", ПолноеИмяОбъекта);
	ИмяОбъекта.Вставить("ТипОбъекта");
	ИмяОбъекта.Вставить("НазваниеОбъекта");
	ИмяОбъекта.Вставить("ИмяТабличнойЧасти");
	
	Если Результат.Количество() = 2 Тогда
		Если Результат[0] = "Документ" ИЛИ Результат[0] = "Справочник" ИЛИ Результат[0] = "БизнесПроцесс" 
			ИЛИ Результат[0] = "Перечисление" ИЛИ Результат[0] = "ПланВидовХарактеристик"
			ИЛИ Результат[0] = "ПланСчетов" Тогда
				ИмяОбъекта.ТипОбъекта = Результат[0];
				ИмяОбъекта.НазваниеОбъекта = Результат[1];
		Иначе
				ИмяОбъекта.ТипОбъекта = ОпределитьТипОбъектаМетаданныхПоИмени(Результат[0]);
				ИмяОбъекта.НазваниеОбъекта = Результат[0];
				ИмяОбъекта.ИмяТабличнойЧасти = Результат[1];
		КонецЕсли;
	ИначеЕсли Результат.Количество() = 3 Тогда
		ИмяОбъекта.ТипОбъекта = Результат[0];
		ИмяОбъекта.НазваниеОбъекта = Результат[1];
		ИмяОбъекта.ИмяТабличнойЧасти = Результат[2];
	ИначеЕсли Результат.Количество() = 4 Тогда 
		ИмяОбъекта.ТипОбъекта = Результат[0];
		ИмяОбъекта.НазваниеОбъекта = Результат[1];
		ИмяОбъекта.ИмяТабличнойЧасти = Результат[3];
	ИначеЕсли Результат.Количество() = 1 Тогда
		ИмяОбъекта.ТипОбъекта = ОпределитьТипОбъектаМетаданныхПоИмени(Результат[0]);
		ИмяОбъекта.НазваниеОбъекта = Результат[0];
	КонецЕсли;

	Возврат ИмяОбъекта;
	
КонецФункции

Функция ОпределитьТипОбъектаМетаданныхПоИмени(Имя)
	Для каждого Объект Из Метаданные.Справочники Цикл 
		Если Объект.Имя = Имя Тогда 
			Возврат "Справочник";
		КонецЕсли;
	КонецЦикла;
	
	Для каждого Объект Из Метаданные.Документы Цикл 
		Если Объект.Имя = Имя Тогда 
			Возврат "Документ";
		КонецЕсли;
	КонецЦикла;
	
	Возврат Неопределено;
КонецФункции

#КонецОбласти

#КонецЕсли