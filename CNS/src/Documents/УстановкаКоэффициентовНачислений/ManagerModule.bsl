#Если Сервер Или ТолстыйКлиентОбычноеПриложение Или ВнешнееСоединение Тогда

#Область ПрограммныйИнтерфейс

#Область ДляВызоваИзДругихПодсистем

// СтандартныеПодсистемы.ВерсионированиеОбъектов

// Определяет настройки объекта для подсистемы ВерсионированиеОбъектов.
//
// Параметры:
//  Настройки - Структура - настройки подсистемы.
Процедура ПриОпределенииНастроекВерсионированияОбъектов(Настройки) Экспорт

КонецПроцедуры
// Конец СтандартныеПодсистемы.ВерсионированиеОбъектов

// СтандартныеПодсистемы.УправлениеДоступом

// См. УправлениеДоступомПереопределяемый.ПриЗаполненииСписковСОграничениемДоступа.
Процедура ПриЗаполненииОграниченияДоступа(Ограничение) Экспорт
	
	//Ограничение.Текст =
	//"РазрешитьЧтениеИзменение
	//|ГДЕ
	//|	// ДляВсехСтрок( ЗначениеРазрешено(ФизическиеЛица.ФизическоеЛицо, NULL КАК ИСТИНА)
	//|	//) И 
	//|   ЗначениеРазрешено(Организация)";   
	
	Ограничение.Текст =
	"ПрисоединитьДополнительныеТаблицы
	|ЭтотСписок КАК Т
	|
	|ЛЕВОЕ СОЕДИНЕНИЕ Документ.УстановкаКоэффициентовНачислений КАК Т1
	|	ПО Т1.Ссылка = Т.Ссылка
	|
	|;
	|РазрешитьЧтениеИзменение
	|ГДЕ
	| Истина";
	
КонецПроцедуры

// Конец СтандартныеПодсистемы.УправлениеДоступом

#КонецОбласти	
	
#КонецОбласти	

#Область СлужебныеПроцедурыИФункции

// <Описание функции>
//
// Параметры:
//  <Параметр1>  - <Тип.Вид> - <описание параметра>
//                 <продолжение описания параметра>
//  <Параметр2>  - <Тип.Вид> - <описание параметра>
//                 <продолжение описания параметра>
//
// Возвращаемое значение:
//   <Тип.Вид>   - <описание возвращаемого значения>
//
Функция ПолучитьСтруктуруУстановкиКоэффициента() Экспорт
	
	Возврат Новый Структура(
		"Сценарий, ВидНачисления, ПериодДействия, Коэффициент, Дата"); 

КонецФункции // ПолучитьСтруктуруУстановкиКоэффициента()

// <Описание процедуры>
//
// Параметры:
//  <Параметр1>  - <Тип.Вид> - <описание параметра>
//                 <продолжение описания параметра>
//  <Параметр2>  - <Тип.Вид> - <описание параметра>
//                 <продолжение описания параметра>
//
Процедура УстановитьКоэффициент(СтруктураКоэффициента) Экспорт
	
	УстановитьПривилегированныйРежим(Истина);	
	
	Запрос 			= Новый Запрос;
	Запрос.Текст 	= 
	"ВЫБРАТЬ
	|	Т.Ссылка КАК Ссылка,
	|	Т.ПометкаУдаления КАК ПометкаУдаления,
	|	Т.Проведен КАК Проведен
	|ИЗ
	|	Документ.УстановкаКоэффициентовНачислений КАК Т
	|ГДЕ
	|	&ТекстУсловия";
	
	ТекстУсловия = "ИСТИНА";
	
	Для Каждого КлючЗначение Из СтруктураКоэффициента Цикл
		
		Если КлючЗначение.Ключ = "Коэффициент" Тогда
			Продолжить;
		КонецЕсли;
		
		ТекстУсловия = ТекстУсловия + " И " + "Т." + КлючЗначение.Ключ + " = &" + КлючЗначение.Ключ;
		Запрос.УстановитьПараметр(КлючЗначение.Ключ, КлючЗначение.Значение);
		
	КонецЦикла;
	
	Запрос.Текст = СтрЗаменить(Запрос.Текст, "&ТекстУсловия", ТекстУсловия);
	
	Выборка = Запрос.Выполнить().Выбрать();
	
	Если Выборка.Следующий() Тогда
		ДокументОбъект = Выборка.Ссылка.ПолучитьОбъект();
		Если ДокументОбъект.ПометкаУдаления Тогда
			ДокументОбъект.ПометкаУдаления = Ложь;
		КонецЕсли;
	Иначе
		ДокументОбъект = Документы.УстановкаКоэффициентовНачислений.СоздатьДокумент();
	КонецЕсли;
	
	ЗаполнитьЗначенияСвойств(ДокументОбъект, СтруктураКоэффициента);
	
	Если ДокументОбъект.Коэффициент = 0 Тогда
		Если ДокументОбъект.Проведен Тогда
			ДокументОбъект.Записать(РежимЗаписиДокумента.ОтменаПроведения);
		КонецЕсли;
		ДокументОбъект.ПометкаУдаления = Истина;
		ДокументОбъект.Записать();
	Иначе
		ДокументОбъект.Записать(РежимЗаписиДокумента.Проведение);
	КонецЕсли;
	
КонецПроцедуры // УстановитьКоэффициент()

Функция ПолучитьЗапросПоДокументамУстановкаКоэффициентов(Сценарий, НачалоПериода, ОкончаниеПериода, ВидНачисления = Неопределено) Экспорт

	УстановитьПривилегированныйРежим(Истина);	
	
	Запрос 			= Новый Запрос;
	Запрос.Текст 	= 
	"ВЫБРАТЬ
	|	УстановкаКоэффициентовНачислений.Ссылка КАК Ссылка,
	|	УстановкаКоэффициентовНачислений.ПометкаУдаления КАК ПометкаУдаления,
	|	УстановкаКоэффициентовНачислений.Номер КАК Номер,
	|	УстановкаКоэффициентовНачислений.Дата КАК Дата,
	|	УстановкаКоэффициентовНачислений.Проведен КАК Проведен,
	|	УстановкаКоэффициентовНачислений.Сценарий КАК Сценарий,
	|	УстановкаКоэффициентовНачислений.ВидНачисления КАК ВидНачисления,
	|	УстановкаКоэффициентовНачислений.ПериодДействия КАК ПериодДействия,
	|	УстановкаКоэффициентовНачислений.Коэффициент КАК Коэффициент
	|ИЗ
	|	Документ.УстановкаКоэффициентовНачислений КАК УстановкаКоэффициентовНачислений
	|ГДЕ
	|	УстановкаКоэффициентовНачислений.Проведен
	|	И УстановкаКоэффициентовНачислений.Сценарий = &Сценарий
	|	И УстановкаКоэффициентовНачислений.Дата МЕЖДУ &НачалоПериода И &ОкончаниеПериода
	|	И (&ПоВсемВидамНачислений
	|			ИЛИ УстановкаКоэффициентовНачислений.ВидНачисления = &ВидНачисления)";
	
	Запрос.УстановитьПараметр("Сценарий", 				Сценарий);
	Запрос.УстановитьПараметр("НачалоПериода", 			НачалоПериода);
	Запрос.УстановитьПараметр("ОкончаниеПериода", 		ОкончаниеПериода);
	Запрос.УстановитьПараметр("ВидНачисления", 			ВидНачисления);
	Запрос.УстановитьПараметр("ПоВсемВидамНачислений", 	Не ЗначениеЗаполнено(ВидНачисления));
	
	Возврат Запрос;
	
КонецФункции	

Процедура УдалитьКоэффициенты(Сценарий, НачалоПериода, ОкончаниеПериода, ВидНачисления = Неопределено) Экспорт
	
	Запрос 	= ПолучитьЗапросПоДокументамУстановкаКоэффициентов(Сценарий, НачалоПериода, ОкончаниеПериода, ВидНачисления);
	Выборка = Запрос.Выполнить().Выбрать();
	
	Пока Выборка.Следующий() Цикл
		
		ДокОбъект 					= Выборка.Ссылка.ПолучитьОбъект();
		ДокОбъект.Записать(РежимЗаписиДокумента.ОтменаПроведения);
		ДокОбъект.ПометкаУдаления 	= Истина;
		ДокОбъект.Записать(РежимЗаписиДокумента.Запись);
		
	КонецЦикла;	
	
КонецПроцедуры	

// <Описание процедуры>
//
// Параметры:
//  <Параметр1>  - <Тип.Вид> - <описание параметра>
//                 <продолжение описания параметра>
//  <Параметр2>  - <Тип.Вид> - <описание параметра>
//                 <продолжение описания параметра>
//
Процедура СкопироватьКоэффициентыПоСценарию(СценарийИсточник, СценарийПриемник, НачалоПериода, ОкончаниеПериода, ВидНачисления = Неопределено) Экспорт

	Запрос 	= ПолучитьЗапросПоДокументамУстановкаКоэффициентов(СценарийИсточник, НачалоПериода, ОкончаниеПериода, ВидНачисления);
	Выборка = Запрос.Выполнить().Выбрать();
	
	Пока Выборка.Следующий() Цикл
		
		СтруктураКоэффициентов = ПолучитьСтруктуруУстановкиКоэффициента();
		ЗаполнитьЗначенияСвойств(СтруктураКоэффициентов, Выборка);
		
		СтруктураКоэффициентов.Вставить("Сценарий", СценарийПриемник);
		
		УстановитьКоэффициент(СтруктураКоэффициентов);
		
	КонецЦикла;	

КонецПроцедуры // СкопироватьКоэффициенты()

// <Описание процедуры>
//
// Параметры:
//  <Параметр1>  - <Тип.Вид> - <описание параметра>
//                 <продолжение описания параметра>
//  <Параметр2>  - <Тип.Вид> - <описание параметра>
//                 <продолжение описания параметра>
//
Процедура СкопироватьКоэффициентыПоГоду(Сценарий, НачалоГодаИсточник, НачалоГодаПриемник, ВидНачисления = Неопределено) Экспорт

	Запрос 	= ПолучитьЗапросПоДокументамУстановкаКоэффициентов(Сценарий, НачалоГода(НачалоГодаИсточник), КонецГода(НачалоГодаИсточник), ВидНачисления);
	Выборка = Запрос.Выполнить().Выбрать();
	
	Пока Выборка.Следующий() Цикл
		
		СтруктураКоэффициентов = ПолучитьСтруктуруУстановкиКоэффициента();
		ЗаполнитьЗначенияСвойств(СтруктураКоэффициентов, Выборка);
		
		СтруктураКоэффициентов.Вставить("Дата", НачалоГодаПриемник);
		
		УстановитьКоэффициент(СтруктураКоэффициентов);
		
	КонецЦикла;	

КонецПроцедуры // СкопироватьКоэффициенты()

#КонецОбласти	
	
#КонецЕсли




