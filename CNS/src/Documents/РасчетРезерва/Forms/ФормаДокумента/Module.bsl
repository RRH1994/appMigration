#Область ОписаниеПеременных

&НаКлиенте
Перем ПараметрыОбработчикаОжидания;

&НаКлиенте
Перем ФормаДлительнойОперации;

#КонецОбласти


#Область ИнициализацияФормы

&НаСервере
Процедура ПриПолученииДанныхНаСервере(ТекущийОбъект, ДополнятьФорму = Истина)
	
	НовыйДокумент = Параметры.Ключ.Пустая();
	НачатьИнициализациюФормы(ДополнятьФорму, НовыйДокумент);
	БюджетированиеРасширенный.ИзменитьРеквизитыФормы(ЭтаФорма);
	ЗавершитьИнициализациюФормы(ТекущийОбъект, ДополнятьФорму, НовыйДокумент);
	
	РеквизитыКДобавлению.Очистить();
	РеквизитыКУдалению.Очистить();
	
КонецПроцедуры

&НаСервере
Процедура НачатьИнициализациюФормы(ДополнятьФорму, НовыйДокумент = Истина)
	
	ЗаполнитьВидыОпераций(НовыйДокумент);
	
	// Устанавливаем значения реквизитов формы.
	УстановитьФункциональныеОпцииФормы();
	
	Если Не ФормаДополнена И ДополнятьФорму Тогда
		// Вызываем ДополнитьФорму() в режиме отложенных вызовов.
		ДополнитьФорму(Истина);
	КонецЕсли;
	
КонецПроцедуры

&НаСервере
Процедура ДанныеВРеквизиты(ТекущийОбъект)
	//Здесь можем перезаполнить реквизиты объекта
КонецПроцедуры

&НаСервере
Процедура ЗавершитьИнициализациюФормы(ТекущийОбъект, ДополнятьФорму, НовыйДокумент = Истина)

	Если Не ФормаДополнена И ДополнятьФорму Тогда 
		// Досоздаем форму.
		ДополнитьФорму(Ложь);
		ДополнитьФорму();
	КонецЕсли;
	
	// чтение данных
	ДанныеВРеквизиты(ТекущийОбъект);
	
	БюджетированиеКлиентСервер.ЗаполнитьМесяцПоДате(ЭтаФорма, "Объект.МесяцНачисления", "МесяцНачисленияСтрокой");
	
	// Раскомментировать и при необходимости прописать процеудры
	//ЗагрузитьНастройки();
	//ЗаполнитьЗаголовокФормы();
	//УстановитьДоступностьКомандыЗаполнитьССохранениемИсправлений(НайденыИсправленияПриОткрытии);
	//УстановитьДоступностьКомандыДозаполнить();
	
КонецПроцедуры

&НаСервере
Процедура УстановитьФункциональныеОпцииФормы()
	
	ПараметрыФО = Новый Структура("Организация, Период", Объект.Организация, НачалоДня(Объект.МесяцНачисления));
	
	УстановитьПараметрыФункциональныхОпцийФормы(ПараметрыФО);
	
КонецПроцедуры

// Подключение универсальных механизмов.
&НаСервере
Процедура ДополнитьФорму(ОтложенноеИзменение = Неопределено, КонтролируемыеПоля = Неопределено, ОписаниеПанелиВычеты = Неопределено, ОписаниеКлючевыхРеквизитов = Неопределено, ТаблицыОчищаемыеПриИзменении = Неопределено)
	
	Если ОтложенноеИзменение = Неопределено Тогда // Выполняем процедуры, не нуждающиеся в механизме отложенного создания.	
		
		// Создание реквизитов.
		
		// Создание элементов
		
		// Установка свойств элементов формы.
		//ОбщегоНазначенияКлиентСервер.УстановитьСвойствоЭлементаФормы(Элементы, "Имя группы", "Видимость", Объект.Элемент?);
		
		//Добавение кнопок 
		
	Иначе
		
		Если ОтложенноеИзменение Тогда
			ДобавлятьЭлементыФормы = Ложь;
			ДобавлятьРеквизитыФормы = Истина;
		Иначе
			ДобавлятьЭлементыФормы = Истина;
			ДобавлятьРеквизитыФормы = Ложь;
		КонецЕсли;
		
		ДополнительныеПараметры = Новый Структура(
			"КонтролируемыеПоля, 
			|ДобавлятьЭлементыФормы, 
			|ДобавлятьРеквизитыФормы, 
			|ОтложенноеИзменение");
		ДополнительныеПараметры.КонтролируемыеПоля = КонтролируемыеПоля; 
		ДополнительныеПараметры.ДобавлятьЭлементыФормы = ДобавлятьЭлементыФормы;
		ДополнительныеПараметры.ДобавлятьРеквизитыФормы = ДобавлятьРеквизитыФормы;
		ДополнительныеПараметры.ОтложенноеИзменение = ОтложенноеИзменение;
		
		РасчетБюджетированиеРасширенныйФормы.ДокументыРасчетПоказателейБюджетовПрогнозовДополнитьФорму(
			ЭтаФорма, ОписаниеТаблицыНачислений(ЭтаФорма), "Начисления", "НачисленияАвтоКоманды", , КонтролируемыеПоля, ДобавлятьЭлементыФормы, ДобавлятьРеквизитыФормы, ОтложенноеИзменение, Объект.Ссылка);
			
	КонецЕсли;
	
КонецПроцедуры

&НаСервере
Процедура ЗаполнитьВидыОпераций(НовыйДокумент)
	
	//В дальнейшем будем здась устаанвливать занчения реквизитовформы

КонецПроцедуры	

#КонецОбласти

&НаСервере 
Процедура ОбновитьВидимостьПодразделенияМестаВозникновенияЗатрат()

	Если ЗначениеЗаполнено(Объект.Подразделение) Тогда
		ЭтотОбъект.Элементы.НачисленияПодразделение.Видимость = Ложь;
		ЭтотОбъект.Элементы.ВзносыПодразделение.Видимость = Ложь;
	Иначе
		ЭтотОбъект.Элементы.НачисленияПодразделение.Видимость = Истина;
		ЭтотОбъект.Элементы.ВзносыПодразделение.Видимость = Истина;
	КонецЕсли;	
	
	Если ЗначениеЗаполнено(Объект.МестоВозникновенияЗатрат) Тогда
		ЭтотОбъект.Элементы.НачисленияМестоВозникновенияЗатрат.Видимость = Ложь;
		ЭтотОбъект.Элементы.ВзносыМестоВозникновенияЗатрат.Видимость = Ложь;
	Иначе
		ЭтотОбъект.Элементы.НачисленияМестоВозникновенияЗатрат.Видимость = Истина;
		ЭтотОбъект.Элементы.ВзносыМестоВозникновенияЗатрат.Видимость = Истина;
	КонецЕсли;	
	
КонецПроцедуры

&НаСервере
Процедура ЗаполнитьСтрокаНачисления(ТаблицаНачислений, ВидНачисления, СпособОтраженияРезультата, ПериодыРасчетов)

	Для Каждого ТекущийПериод Из ПериодыРасчетов Цикл
		НоваяСтрока = ТаблицаНачислений.Добавить();
		НоваяСтрока.ВидНачисления = ВидНачисления;
		НоваяСтрока.СпособОтраженияРезультата = СпособОтраженияРезультата;
		НоваяСтрока.МесяцНачисления = ТекущийПериод;
		НоваяСтрока.Подразделение = Объект.Подразделение;
		НоваяСтрока.МестоВозникновенияЗатрат = Объект.МестоВозникновенияЗатрат;
	КонецЦикла;

КонецПроцедуры

&НаСервере
Процедура ВыполнитьПервоначальноеЗаполнениеТабличнойЧасти()
	ЗаполнитьНачисленияПоУмолчанию();	
КонецПроцедуры	

&НаСервере
Функция СоздатьОписаниеТаблицыНачислений()
	
	ТаблицаНачислений = РасчетБюджетированиеРасширенный.ПустаяТаблицаНачисления();
	ТаблицаНачислений.Колонки.Добавить("СпособОтраженияРезультата", Новый ОписаниеТипов("ПеречислениеСсылка.СпособыОтраженияРезультатов"));

	Возврат ТаблицаНачислений;
	
КонецФункции	

&НаСервере
Процедура ЗаполнитьНачисленияПоУмолчанию()

	ДанныеЗаполнения = Новый Структура("Начисления");
	ДанныеЗаполнения.Вставить("Начисления", СоздатьОписаниеТаблицыНачислений());
	
	МесяцыРасчета = Новый Массив;
	МесяцНачисления = Объект.ДатаНачала;
	
	Пока МесяцНачисления <= Объект.ДатаОкончания Цикл
		МесяцыРасчета.Добавить(МесяцНачисления);
		МесяцНачисления = ДобавитьМесяц(МесяцНачисления, 1);
	КонецЦикла;
	
	//1. Годовая премия
	ЗаполнитьСтрокаНачисления(ДанныеЗаполнения.Начисления,
							  ПредопределенноеЗначение("Справочник.ВидыНачислений.ГодоваяПремия"),
							  ПредопределенноеЗначение("Перечисление.СпособыОтраженияРезультатов.РасчетПоФормулеКорректировка"),
							  МесяцыРасчета);
							  
	НетиповыеВидыНачислений = РасчетБюджетированиеРасширенный.ПолучитьМассивНетиповыхВидовНачислений();						  
	
	Для Каждого СтрокаНетиповогоВидаНачисления Из НетиповыеВидыНачислений Цикл
		ВидНачисленияСсылка = СтрокаНетиповогоВидаНачисления.ВидНачисленияСсылка;
		Если ЗначениеЗаполнено(ВидНачисленияСсылка) И Не ВидНачисленияСсылка = Неопределено Тогда
			ЗаполнитьСтрокаНачисления(ДанныеЗаполнения.Начисления,
									  ВидНачисленияСсылка,
									  ПредопределенноеЗначение("Перечисление.СпособыОтраженияРезультатов.ПустаяСсылка"),
									  МесяцыРасчета);
		КонецЕсли;
	КонецЦикла;
							  
	//4. Квартальная премия
	ЗаполнитьСтрокаНачисления(ДанныеЗаполнения.Начисления,
							  ПредопределенноеЗначение("Справочник.ВидыНачислений.КвартальнаяПремия"),
							  ПредопределенноеЗначение("Перечисление.СпособыОтраженияРезультатов.РасчетПоФормулеКорректировка"),
							  МесяцыРасчета);
							  
	//13. ДМС
	ЗаполнитьСтрокаНачисления(ДанныеЗаполнения.Начисления,
							  ПредопределенноеЗначение("Справочник.ВидыНачислений.ДМС"),
							  ПредопределенноеЗначение("Перечисление.СпособыОтраженияРезультатов.ПустаяСсылка"),
							  МесяцыРасчета);
							  
	//13. НС и болезни
	ЗаполнитьСтрокаНачисления(ДанныеЗаполнения.Начисления,
							  ПредопределенноеЗначение("Справочник.ВидыНачислений.НСиБолезни"),
							  ПредопределенноеЗначение("Перечисление.СпособыОтраженияРезультатов.ПустаяСсылка"),
							  МесяцыРасчета);
							  
							  
	Если (ТипЗнч(ДанныеЗаполнения.Начисления)=Тип("ТаблицаЗначений")
			ИЛИ ТипЗнч(ДанныеЗаполнения.Начисления)=Тип("ДанныеФормыКоллекция"))						  
				И ДанныеЗаполнения.Начисления.Количество()>0 Тогда
		ОчиститьТаблицыДокументаНаСервере();
		ДанныеДляЗаполненияВДанныеФормы(ДанныеЗаполнения, Объект);
	КонецЕсли;
	
КонецПроцедуры	

&НаСервере
Процедура ОбновитьДатыПериодаРасчетаДокумента()

	Если ЗначениеЗаполнено(Объект.Сценарий) Тогда

		Если Не Объект.ДатаНачала = Объект.Сценарий.НачалоДействия Тогда
			ДатаНачала = Объект.Сценарий.НачалоДействия;
			Модифицированность = Истина;
		Иначе
			ДатаНачала = Объект.ДатаНачала;
		КонецЕсли;	
		Если Не Объект.ДатаОкончания = Объект.Сценарий.ОкончаниеДействия Тогда
			ДатаОкончания = Объект.Сценарий.ОкончаниеДействия;
			Модифицированность = Истина;
		Иначе
			ДатаОкончания = Объект.ДатаОкончания;
		КонецЕсли;	
		
	КонецЕсли;
	
КонецПроцедуры

&НаСервере
Процедура ПриЧтенииНаСервере(ТекущийОбъект)

	// СтандартныеПодсистемы.ДатыЗапретаИзменения
	ДатыЗапретаИзменения.ОбъектПриЧтенииНаСервере(ЭтаФорма, ТекущийОбъект);
	// Конец СтандартныеПодсистемы.ДатыЗапретаИзменения
	
	ЗаполнениеВыполнено = Ложь;
	ПриПолученииДанныхНаСервере(ТекущийОбъект);
	
	// СтандартныеПодсистемы.ПодключаемыеКоманды
	ПодключаемыеКомандыКлиентСервер.ОбновитьКоманды(ЭтотОбъект, Объект);
	// Конец СтандартныеПодсистемы.ПодключаемыеКоманды
	
	// СтандартныеПодсистемы.УправлениеДоступом 
	Если ОбщегоНазначения.ПодсистемаСуществует("СтандартныеПодсистемы.УправлениеДоступом") Тогда
		МодульУправлениеДоступом = ОбщегоНазначения.ОбщийМодуль("УправлениеДоступом");
		МодульУправлениеДоступом.ПриЧтенииНаСервере(ЭтотОбъект, ТекущийОбъект);
	КонецЕсли;
	// Конец СтандартныеПодсистемы.УправлениеДоступом 
	
	
	// СтандартныеПодсистемы.Свойства
	УправлениеСвойствами.ПриЧтенииНаСервере(ЭтотОбъект, ТекущийОбъект);
	// Конец СтандартныеПодсистемы.Свойства
	
	// СтандартныеПодсистемы.КонтрольВеденияУчета
	//КонтрольВеденияУчетаБЗК.ПриЧтенииНаСервере(ЭтотОбъект, ТекущийОбъект);
	// Конец СтандартныеПодсистемы.КонтрольВеденияУчета
	
	// ПроцессыОбработкиДокументов
	//Если ОбщегоНазначения.ПодсистемаСуществует("БюджетированиеПриложения.ПроцессыОбработкиДокументовБюджетирования") Тогда
	//	МодульПроцессыОбработкиДокументовБюджетирования = ОбщегоНазначения.ОбщийМодуль("ПроцессыОбработкиДокументовБюджетирования");
	//	МодульПроцессыОбработкиДокументовБюджетирования.ПриЧтенииНаСервере(ЭтотОбъект, ТекущийОбъект);
	//КонецЕсли;	
	// Конец ПроцессыОбработкиДокументов

КонецПроцедуры

&НаСервере
Процедура ПриСозданииНаСервере(Отказ, СтандартнаяОбработка)

	Если Параметры.Ключ.Пустая() Тогда

	    КлючиИдентификаторовЗаполнения = "Организация, Ответственный, МесяцРасчета";
		ЗначенияДляЗаполнения = Новый Структура(КлючиИдентификаторовЗаполнения, "Объект.Организация", "Объект.Ответственный", "Объект.МесяцНачисления");
		Бюджетирование.ЗаполнитьПервоначальныеЗначенияВФорме(ЭтаФорма, ЗначенияДляЗаполнения);
		
	КонецЕсли;
	
	// Обработчик подсистемы "ВерсионированиеОбъектов".
	ВерсионированиеОбъектов.ПриСозданииНаСервере(ЭтаФорма);
	// СтандартныеПодсистемы.ПодключаемыеКоманды
	ПараметрыРазмещения = ПодключаемыеКоманды.ПараметрыРазмещения();
	ПараметрыРазмещения.КоманднаяПанель = Элементы.ФормаКоманднаяПанель;
	ПодключаемыеКоманды.ПриСозданииНаСервере(ЭтотОбъект, ПараметрыРазмещения);
	// Конец СтандартныеПодсистемы.ПодключаемыеКоманды

	// СтандартныеПодсистемы.Свойства
	//ДополнительныеПараметры = Новый Структура;
	//ДополнительныеПараметры.Вставить("ИмяЭлементаДляРазмещения", "ГруппаДополнительныеРеквизиты");
	//УправлениеСвойствами.ПриСозданииНаСервере(ЭтотОбъект, ДополнительныеПараметры);
	// Конец СтандартныеПодсистемы.Свойства
	
	ОбновитьДатыПериодаРасчетаДокумента();
	
	ОбновитьВидимостьПодразделенияМестаВозникновенияЗатрат(); 
	
КонецПроцедуры

&НаКлиенте
Процедура Заполнить(Команда)
	ВыполнитьПервоначальноеЗаполнениеТабличнойЧасти();
КонецПроцедуры

&НаСервере
Процедура ОчиститьНаСервере()
	ОчиститьТаблицыДокументаНаСервере();
КонецПроцедуры

&НаКлиенте
Процедура Очистить(Команда)
	ОчиститьНаСервере();
КонецПроцедуры

&НаСервере
Процедура ОбработатьИзменениеМесяцНачисленияНаСервере()
	
	УстановитьФункциональныеОпцииФормы();
	
	ЗаполнениеВыполнено = Ложь;
	
КонецПроцедуры

&НаКлиенте
Процедура ПриИзмененииМесяцаНачисления()  
	
	БюджетированиеРасширенныйКлиент.КлючевыеРеквизитыЗаполненияФормыОчиститьТаблицы(ЭтаФорма);
	ОбработатьИзменениеМесяцНачисленияНаСервере();
	
КонецПроцедуры

&НаКлиенте
Процедура МесяцНачисленияСтрокойПриИзменении(Элемент)
	
	БюджетированиеКлиент.ВводМесяцаПриИзменении(ЭтаФорма, "Объект.МесяцНачисления", "МесяцНачисленияСтрокой", Модифицированность);   // ВводМесяцаПриИзменении
	
	ПриИзмененииМесяцаНачисления();
	
КонецПроцедуры

&НаКлиенте
Процедура МесяцНачисленияСтрокойНачалоВыбора(Элемент, ДанныеВыбора, СтандартнаяОбработка)
	
	Оповещение = Новый ОписаниеОповещения("МесяцНачисленияСтрокойНачалоВыбораЗавершение", ЭтотОбъект);
	БюджетированиеКлиент.ВводМесяцаНачалоВыбора(ЭтаФорма, ЭтаФорма, "Объект.МесяцНачисления", "МесяцНачисленияСтрокой", , Оповещение);

КонецПроцедуры

&НаКлиенте
Процедура МесяцНачисленияСтрокойНачалоВыбораЗавершение(ЗначениеВыбрано, ДополнительныеПараметры) Экспорт
	
	ПриИзмененииМесяцаНачисления();
	
КонецПроцедуры

&НаКлиенте
Процедура МесяцНачисленияСтрокойРегулирование(Элемент, Направление, СтандартнаяОбработка)
	
	БюджетированиеКлиент.ВводМесяцаРегулирование(ЭтаФорма, "Объект.МесяцНачисления", "МесяцНачисленияСтрокой", Направление, Модифицированность);
	ПодключитьОбработчикОжидания("ОбработчикОжиданияМесяцНачисленияПриИзменении", 0.3, Истина); 
	
КонецПроцедуры

&НаКлиенте
Процедура МесяцНачисленияСтрокойАвтоПодбор(Элемент, Текст, ДанныеВыбора, ПараметрыПолученияДанных, Ожидание, СтандартнаяОбработка)
	
	БюджетированиеКлиент.ВводМесяцаАвтоПодборТекста(Текст, ДанныеВыбора, СтандартнаяОбработка);

КонецПроцедуры

&НаКлиенте
Процедура МесяцНачисленияСтрокойОкончаниеВводаТекста(Элемент, Текст, ДанныеВыбора, ПараметрыПолученияДанных, СтандартнаяОбработка)
	
	БюджетированиеКлиент.ВводМесяцаОкончаниеВводаТекста(Текст, ДанныеВыбора, СтандартнаяОбработка);

КонецПроцедуры

&НаКлиенте
Процедура СценарийПриИзменении(Элемент)
	
	ОбновитьДатыПериодаРасчетаДокумента(); 
	БюджетированиеКлиент.ВводЗначенияПриИзменении(ЭтаФорма, "Объект.ДатаНачала", "ДатаНачала", Модифицированность);   // ВводМесяцаПриИзменении
	БюджетированиеКлиент.ВводЗначенияПриИзменении(ЭтаФорма, "Объект.МесяцНачисления", "ДатаНачала", Модифицированность);
	БюджетированиеКлиент.ВводЗначенияПриИзменении(ЭтаФорма, "Объект.ДатаОкончания", "ДатаОкончания", Модифицированность);   // ВводМесяцаПриИзменении
	БюджетированиеКлиент.ВводМесяцаПриИзмененииДатыСценария(ЭтаФорма, "ДатаНачала", "МесяцНачисленияСтрокой", Модифицированность);
	
КонецПроцедуры

&НаКлиенте
Процедура СценарийОчистка(Элемент, СтандартнаяОбработка)
	// Вставить содержимое обработчика.
КонецПроцедуры

&НаКлиенте
Процедура ПриИзмененииДатыНачала()

	БюджетированиеРасширенныйКлиент.КлючевыеРеквизитыЗаполненияФормыОчиститьТаблицы(ЭтаФорма);
    ОбработатьИзменениеМесяцНачисленияНаСервере();
	
КонецПроцедуры

&НаКлиенте
Процедура ПриИзмененииДатаОкончания()

	БюджетированиеРасширенныйКлиент.КлючевыеРеквизитыЗаполненияФормыОчиститьТаблицы(ЭтаФорма);
    ОбработатьИзменениеМесяцНачисленияНаСервере();
	
КонецПроцедуры

&НаКлиенте
Процедура ПриОткрытии(Отказ)
	
	Если Модифицированность Тогда
		
		Если Не Объект.ДатаНачала = ДатаНачала Тогда
			БюджетированиеКлиент.ВводЗначенияПриИзменении(ЭтаФорма, "Объект.ДатаНачала", "ДатаНачала", Модифицированность);   // ВводМесяцаПриИзменении
			БюджетированиеКлиент.ВводЗначенияПриИзменении(ЭтаФорма, "Объект.МесяцНачисления", "ДатаНачала", Модифицированность);
			БюджетированиеКлиент.ВводМесяцаПриИзмененииДатыСценария(ЭтаФорма, "Объект.МесяцНачисления", "МесяцНачисленияСтрокой", Модифицированность);
			
			ПриИзмененииДатыНачала();
		КонецЕсли;
		
		Если Не Объект.ДатаОкончания = ДатаОкончания Тогда
			БюджетированиеКлиент.ВводЗначенияПриИзменении(ЭтаФорма, "Объект.ДатаОкончания", "ДатаОкончания", Модифицированность);   // ВводМесяцаПриИзменении
		    ПриИзмененииДатаОкончания();
		КонецЕсли;
		
	КонецЕсли;
	
КонецПроцедуры

&НаСервере
Процедура ОчиститьТаблицыДокументаНаСервере(ЭтоПередЗагрузкойРезультатаПоНачислению = Ложь)
	
	УстановитьПривилегированныйРежим(Истина);
	Для Каждого ТабличнаяЧасть Из Метаданные.Документы.РасчетПоказателейБюджетовПрогнозов.ТабличныеЧасти Цикл
		Если ЭтоПередЗагрузкойРезультатаПоНачислению И ТабличнаяЧасть.Имя = "Начисления" Тогда
			Продолжить;
		КонецЕсли;	
		Если Объект.Свойство(ТабличнаяЧасть.Имя) Тогда 
			Объект[ТабличнаяЧасть.Имя].Очистить();
		КонецЕсли;
	КонецЦикла; 
	СтрокиТаблицДокументаПоФизическимЛицам = Неопределено;
	УстановитьПривилегированныйРежим(Ложь);
	
КонецПроцедуры

&НаСервере
Процедура ЗаполнениеПослеВыполненияДлительнойОперации()
	
	СтруктураДанных = ПолучитьИзВременногоХранилища(АдресХранилища);
	Если ТипЗнч(СтруктураДанных) <> Тип("Структура") Тогда
		Возврат;
	КонецЕсли;
	
	ДанныеДляЗаполнения = Неопределено;
	ФизическиеЛица = Неопределено;
	
	Если СтруктураДанных.Свойство("ДанныеДляЗаполненияТаблицДокумента", ДанныеДляЗаполнения) Тогда
		// Заполнение табличных частей и вторичных данных коллекций, которые с ней связаны.
		ЭтоПередЗагрузкойРезультатаПоНачислению = Объект.ВидРасчета = ПредопределенноеЗначение("Перечисление.ВидыРасчетаБюджетныхПоказателей.ПоНачислению");
		ОчиститьТаблицыДокументаНаСервере(ЭтоПередЗагрузкойРезультатаПоНачислению);
		
		ДанныеДляЗаполненияВДанныеФормы(ДанныеДляЗаполнения, Объект);

	КонецЕсли; 
	
	ОбновитьВидимостьПодразделенияМестаВозникновенияЗатрат();
	
КонецПроцедуры	

&НаСервереБезКонтекста
Процедура ДанныеДляЗаполненияВДанныеФормы(ДанныеЗаполнения, Объект, ПозицииВставки = Неопределено, ФизическиеЛица = Неопределено)
	
	Начисления = Неопределено;
	Если ДанныеЗаполнения.Свойство("Начисления", Начисления) И Не Начисления = Неопределено Тогда
		//Начисления
		ТаблицыНачислений = РасчетБюджетированиеРасширенныйФормы.ТаблицыНачисленийФормы();
		ТаблицыНачислений.Начисления = Объект.Начисления;
		
		//Начисления
		РасчетБюджетированиеРасширенныйФормы.РасчетНачисленияВДанныеФормы(ТаблицыНачислений, Начисления, Объект.Организация, Объект.МесяцНачисления, ПозицииВставки);
	КонецЕсли;

	Взносы = Неопределено;
	Если ДанныеЗаполнения.Свойство("Взносы", Взносы) И Не Взносы = Неопределено Тогда
		//Взносы
		РасчетБюджетированиеРасширенныйФормы.РасчетВзносыВДанныеФормы(Объект.Взносы, Взносы, ПозицииВставки);
	КонецЕсли;
	
КонецПроцедуры  

&НаСервере
Функция РезультатЗаполненияВДлительнойОперации()
	
	//РасчетБюджетированиеРасширенныйКлиентСервер.ОчиститьТаблицыДокумента(ЭтаФорма, ОписаниеДокумента(ЭтотОбъект));
	
	СтруктураПараметров = РасчетБюджетированиеРасширенныйКлиентСервер.ПараметрыПолученияДанныхЗаполненияДокумента();
	СтруктураПараметров.ОписаниеДокумента = ОписаниеДокумента(ЭтотОбъект);
	СтруктураПараметров.Организация = Объект.Организация;
	СтруктураПараметров.ДокументСсылка = Объект.Ссылка;
	СтруктураПараметров.Подразделение = Объект.Подразделение;
	СтруктураПараметров.МестоВозникновенияЗатрат = Объект.МестоВозникновенияЗатрат;
	СтруктураПараметров.ВидРасчета = Объект.ВидРасчета;
	СтруктураПараметров.Сценарий = Объект.Сценарий;
	СтруктураПараметров.МесяцНачисления = Объект.МесяцНачисления;
	СтруктураПараметров.НачалоПериода = НачалоМесяца(Объект.ДатаНачала);
	СтруктураПараметров.ОкончаниеПериода = КонецМесяца(Объект.ДатаОкончания);  
	СтруктураПараметров.ФизическиеЛица = Неопределено;
	СтруктураПараметров.ФизическиеЛицаПериодДействияПерерасчет = Неопределено; 
	
	СтруктураПараметров.ИсточникРасчета = СоздатьОписаниеТаблицыНачислений();
	Для i=0 По Объект.Начисления.Количество()-1 Цикл
		НовыйИсточник = СтруктураПараметров.ИсточникРасчета.Добавить();
		ЗаполнитьЗначенияСвойств(НовыйИсточник, Объект.Начисления[i]);
		НовыйИсточник.Организация = Объект.Организация;
		НовыйИсточник.ДатаНачала = НачалоМесяца(Объект.ДатаНачала);
		НовыйИсточник.ДатаОкончания = КонецМесяца(Объект.ДатаОкончания);
	КонецЦикла;	
	
	НаименованиеЗадания = НСтр("ru = 'Заполнение документа «Расчет резерва (корректировок, прочих)»'");
	
	Результат = ДлительныеОперации.ЗапуститьВыполнениеВФоне(
		УникальныйИдентификатор,
		"Документы.РасчетРезерва.ПодготовитьДанныеДляЗаполнения",
		СтруктураПараметров,
		НаименованиеЗадания);
	
	АдресХранилища = Результат.АдресХранилища;
	
	Если Результат.ЗаданиеВыполнено Тогда
		ЗаполнениеПослеВыполненияДлительнойОперации();
	КонецЕсли;
		
	Возврат Результат;
	
КонецФункции

&НаКлиентеНаСервереБезКонтекста
Функция ТаблицыДокументаСДаннымиВидовНачислений()
	
	ТаблицыДокумента = Новый Массив;
	ТаблицыДокумента.Добавить("Начисления");
	
	Возврат ТаблицыДокумента;
	
КонецФункции

&НаСервереБезКонтекста
Функция НовыйСтруктураТаблицДокумента()
	
	СтруктураТаблиц = Новый Структура("СодержимоеДокумента,ШаблонСтрокТаблиц", Новый Структура, Новый Структура);
	ТаблицыСодержимогоДокумента = РасчетБюджетированиеРасширенный.НовыйСодержимоеДокументаРасчетаБюджетирования();
	Для Каждого КлючИЗначение Из ТаблицыСодержимогоДокумента Цикл
		ТаблицаЗначений = КлючИЗначение.Значение;
		СтруктураСтрокой = "";
		НужнаЗапятая = Ложь;
		Для Каждого Колонка Из ТаблицаЗначений.Колонки Цикл
			Если НужнаЗапятая Тогда
				СтруктураСтрокой = СтруктураСтрокой + ",";
			КонецЕсли;
			СтруктураСтрокой = СтруктураСтрокой + Колонка.Имя;
			НужнаЗапятая = Истина;
		КонецЦикла;
		СтруктураТаблиц.ШаблонСтрокТаблиц.Вставить(КлючИЗначение.Ключ, Новый ФиксированнаяСтруктура(СтруктураСтрокой));
		СтруктураТаблиц.СодержимоеДокумента.Вставить(КлючИЗначение.Ключ, Новый Массив);
	КонецЦикла;
	
	Возврат СтруктураТаблиц;
	
КонецФункции

&НаКлиентеНаСервереБезКонтекста
Функция НоваяСтрокаДанныхПоВидамНачислений(ТаблицаДанных, ШаблонСтроки)
	
	НоваяСтрока = Новый Структура(ШаблонСтроки);
	ТаблицаДанных.Добавить(НоваяСтрока);
	Возврат НоваяСтрока;
	
КонецФункции

&НаСервереБезКонтекста
Функция СтрокиТаблицПоВидамНачислений(Документ, ЗаполнятьИдентификаторы = Истина, ТаблицыДокумента = Неопределено)
	
	Если ТаблицыДокумента = Неопределено Тогда
		ТаблицыДокумента = ТаблицыДокументаСДаннымиВидовНачислений();
	КонецЕсли;
	
	Если Не ЗаполнятьИдентификаторы Тогда
		ШаблонСтрок = НовыйСтруктураТаблицДокумента().ШаблонСтрокТаблиц;
	КонецЕсли;
	
	ВидНачисленияПоИдентификаторам = Новый Соответствие;
	СтрокиПоВидамНачислений = Новый Соответствие;
	
	Для Каждого ИмяТаблицы Из ТаблицыДокумента Цикл
		Для Каждого СтрокаТаблицы Из Документ[ИмяТаблицы] Цикл
			
			ВидНачисления = СтрокаТаблицы.ВидНачисления;
			
			Если Не ЗначениеЗаполнено(ВидНачисления) Тогда
				Продолжить;
			КонецЕсли;
			
			Если СтрокиПоВидамНачислений[ВидНачисления] = Неопределено Тогда
				ТаблицыВидаНачисления = Новый Структура;
				СтрокиПоВидамНачислений.Вставить(ВидНачисления, ТаблицыВидаНачисления);
			Иначе
				ТаблицыВидаНачисления = СтрокиПоВидамНачислений[ВидНачисления];
			КонецЕсли;
			
			Если Не ТаблицыВидаНачисления.Свойство(ИмяТаблицы) Тогда
				ТаблицыВидаНачисления.Вставить(ИмяТаблицы, Новый Массив);
			КонецЕсли;
			
			Если ЗаполнятьИдентификаторы Тогда
				ТаблицыВидаНачисления[ИмяТаблицы].Добавить(СтрокаТаблицы.ПолучитьИдентификатор());
			Иначе
				НоваяСтрока = НоваяСтрокаДанныхПоВидамНачислений(ТаблицыВидаНачисления[ИмяТаблицы], ШаблонСтрок[ИмяТаблицы]);
				ЗаполнитьЗначенияСвойств(НоваяСтрока, СтрокаТаблицы);
			КонецЕсли;
		КонецЦикла;
	КонецЦикла;
	
	Возврат СтрокиПоВидамНачислений;
	
КонецФункции

&НаСервереБезКонтекста
Функция СодержимоеДокументаПослеРасчета(АдресХранилища, ОписаниеТаблиц)
	
	СтруктураДанных = ПолучитьИзВременногоХранилища(АдресХранилища);
	Если ТипЗнч(СтруктураДанных) <> Тип("Структура") Тогда
		Возврат Неопределено;
	КонецЕсли;
	
	ДанныеДляЗаполнения = Неопределено;
	Если Не СтруктураДанных.Свойство("ДанныеДляПерезаполненияТаблицДокумента", ДанныеДляЗаполнения) Тогда
		Возврат Неопределено;
	КонецЕсли;
	
	СтруктураПараметров = СтруктураДанных.СтруктураПараметров;
	
	СодержимоеДокумента = РасчетБюджетированиеРасширенный.НовыйСодержимоеДокументаРасчетаБюджетирования();
	СодержимоеДокумента.Вставить("Ссылка", СтруктураПараметров.ДокументСсылка);
	СодержимоеДокумента.Вставить("ВидыНачислений", СтруктураПараметров.ВидыНачислений);
	СодержимоеДокумента.Вставить("Организация", СтруктураПараметров.Организация);
	СодержимоеДокумента.Вставить("МесяцНачисления", СтруктураПараметров.МесяцНачисления);
	
	СодержимоеФормыДляЗаполнения = Новый Структура("Объект", СодержимоеДокумента);
	ВидыНачислений = СтруктураПараметров.ВидыНачислений;
	
	ДанныеДляЗаполненияВДанныеФормы(ДанныеДляЗаполнения, СодержимоеДокумента,, ВидыНачислений);
	
	СтрокиТаблицПоВидамНачислений = СтрокиТаблицПоВидамНачислений(СодержимоеДокумента, Ложь);
	
	Если ЗначениеЗаполнено(СтруктураПараметров.ВидыНачислений) Тогда
		ВидыНачислений = СтруктураПараметров.ВидыНачислений;
	Иначе
		ВидыНачислений = ОбщегоНазначения.ВыгрузитьКолонку(СтрокиТаблицПоВидамНачислений, "Ключ");	
	КонецЕсли;
		
	Результат = Новый Структура;
	Результат.Вставить("СтрокиТаблицДокументаПоВидамНачислений", СтрокиТаблицПоВидамНачислений);
	Результат.Вставить("ВидыНачислений", ВидыНачислений);
		
	Возврат Результат;
	
КонецФункции

&НаКлиентеНаСервереБезКонтекста
Функция ОписаниеТаблицыНачислений(Форма)
	
	Описание = РасчетБюджетированиеРасширенныйКлиентСервер.ОписаниеТаблицыРасчета();
	Описание.СодержитПолеФизическоеЛицо = Ложь;
	Описание.ИмяРеквизитаФизическоеЛицо = "ФизическоеЛицо";
	
	Описание.РаспределениеРезультатовЗависимыеТаблицы = "Начисления";
	
	Возврат Описание;
	
КонецФункции

&НаКлиентеНаСервереБезКонтекста
Процедура ЗаменитьСтрокиТаблицДокумента(Объект, ВидыНачислений, СтрокиТаблицДокумента, СтрокиТаблицРасчета, ТаблицыДокумента = Неопределено)
	
	Если ТаблицыДокумента = Неопределено Тогда
		ТаблицыДокумента = ТаблицыДокументаСДаннымиВидовНачислений();
	КонецЕсли;
	
	Для Каждого ВидНачисления Из ВидыНачислений Цикл
		
		ТаблицыДокументаВидаНачисления = СтрокиТаблицДокумента[ВидНачисления];
		ТаблицыРасчетаВидаНачисления = СтрокиТаблицРасчета[ВидНачисления];
		
		Для Каждого ИмяТаблицы Из ТаблицыДокумента Цикл
			СтрокиТаблицыДокумента = Неопределено;
			Если Не ТаблицыДокументаВидаНачисления.Свойство(ИмяТаблицы, СтрокиТаблицыДокумента) Тогда
				СтрокиТаблицыДокумента = Новый Массив;
				ТаблицыДокументаВидаНачисления.Вставить(ИмяТаблицы, СтрокиТаблицыДокумента);
			КонецЕсли;
			
			СтрокиТаблицыРасчета = Неопределено;
			ОбходТаблицыИдентификаторов = РасчетБюджетированиеРасширенныйКлиентСервер.НовыйОбходИдентификаторовТаблицы(СтрокиТаблицыДокумента);
			
			Если ТаблицыРасчетаВидаНачисления <> Неопределено
				И ТаблицыРасчетаВидаНачисления.Свойство(ИмяТаблицы, СтрокиТаблицыРасчета) Тогда
				
				Для Каждого СтрокаИсточника Из СтрокиТаблицыРасчета Цикл
					СтрокаФормы = РасчетБюджетированиеРасширенныйКлиентСервер.СтрокаТаблицыПоНачальнойПозиции(Объект[ИмяТаблицы], ОбходТаблицыИдентификаторов);
					ЗаполнитьЗначенияСвойств(СтрокаФормы, СтрокаИсточника);
				КонецЦикла;
			КонецЕсли;
			
			РасчетБюджетированиеРасширенныйКлиентСервер.УдалитьНеактуальныеСтрокиФормы(Объект[ИмяТаблицы], ОбходТаблицыИдентификаторов);
		КонецЦикла;
	КонецЦикла;
КонецПроцедуры

&НаКлиентеНаСервереБезКонтекста
Функция ОписаниеДокумента(Форма)
	
	Описание = РасчетБюджетированиеРасширенныйКлиентСервер.ОписаниеРасчетногоДокумента();
	Описание.ЭтоРасчетПоказателейБюджетовПрогнозов = Истина;
	
	//Добавляем описание для начислений окладов
	Описание.НачисленияИмя = "Начисления";
	Описание.ОписанияТаблиц.Вставить("Начисления", ОписаниеТаблицыНачислений(Форма));
	
	Описание.ЗаполнятьПерерасчеты = Ложь;
	
	Описание.ОбязательныеПоля.Добавить(РасчетБюджетированиеРасширенныйКлиентСервер.ОписаниеОбязательногоПоляДокумента(НСтр("ru = 'МесяцНачисления'"), "МесяцНачисленияСтрокой"));
	
	Возврат Описание;
	
КонецФункции

&НаКлиентеНаСервереБезКонтекста
Функция МассивОписанийТаблицФормы(Форма)
	
	МассивОписанийТаблицФормы = Новый Структура;
	
	МассивОписанийТаблицФормы.Вставить("Начисления", ОписаниеТаблицыНачислений(Форма)); 
			
Конецфункции

#Область КлючевыеРеквизитыЗаполненияФормы

// Функция возвращает описание таблиц формы подключенных к механизму ключевых реквизитов формы.
&НаСервере
Функция КлючевыеРеквизитыЗаполненияФормыТаблицыОчищаемыеПриИзменении() Экспорт
	
	Массив = КлючевыеРеквизитыТаблицыОчищаемыеПриИзменении();
	Возврат Массив;
	
КонецФункции 

// Функция возвращает массив реквизитов формы подключенных к механизму ключевых реквизитов формы.
&НаСервере
Функция КлючевыеРеквизитыЗаполненияФормыОписаниеКлючевыхРеквизитов() Экспорт
	
	Массив = КлючевыеРеквизитыОписаниеКлючевыхРеквизитов();
	Возврат Массив;
	
КонецФункции

&НаКлиентеНаСервереБезКонтекста
Функция КлючевыеРеквизитыТаблицыОчищаемыеПриИзменении() 
	
	Массив = Новый Массив;
	Массив.Добавить("Объект.Начисления");
	
	Возврат Массив;
	
КонецФункции 

&НаКлиентеНаСервереБезКонтекста
Функция КлючевыеРеквизитыОписаниеКлючевыхРеквизитов() 
	
	Массив = Новый Массив;
	Массив.Добавить(Новый Структура("ЭлементФормы, Представление", "Организация",					НСтр("ru = 'организации'")));
	Массив.Добавить(Новый Структура("ЭлементФормы, Представление", "Подразделение",					НСтр("ru = 'подразделения'"))); 
	Массив.Добавить(Новый Структура("ЭлементФормы, Представление", "МестоВозникновенияЗатрат",		НСтр("ru = 'место возникновения затрат'"))); 
	Массив.Добавить(Новый Структура("ЭлементФормы, Представление", "МесяцНачисленияСтрокой", 		НСтр("ru = 'месяца начисления'")));
	
	Возврат Массив;
	
КонецФункции

#КонецОбласти

#Область ОбработчикиКомандФормы

// СтандартныеПодсистемы.ПодключаемыеКоманды
&НаКлиенте
Процедура Подключаемый_ВыполнитьКоманду(Команда)
	ПодключаемыеКомандыКлиент.ВыполнитьКоманду(ЭтотОбъект, Команда, Объект);
КонецПроцедуры   

&НаКлиенте
Процедура Рассчитать(Команда)

	ОчиститьСообщения();

	РасчетБюджетированиеРасширенныйКлиент.ЗаполнитьДокументРасчетПоказателейБюджетовПрогнозов(ЭтаФорма);

КонецПроцедуры

&НаКлиенте
Процедура ЗаполнениеПослеВыполненияДлительнойОперацииНаКлиенте()
	
	СодержимоеДокумента = СодержимоеДокументаПослеРасчета(
		АдресХранилища,
		МассивОписанийТаблицФормы(ЭтаФорма));
		
	ВидыНачислений = СодержимоеДокумента.ВидыНачислений;
	СтрокиТаблицРасчетаПоВидамНачислений = СодержимоеДокумента.СтрокиТаблицРасчетаПоВидамНачислений;
	Если СодержимоеДокумента = Неопределено Тогда
		Возврат;
	КонецЕсли;
	
	ЗаменитьСтрокиТаблицДокумента(Объект, ВидыНачислений, СтрокиТаблицДокументаПоВидамНачислений, СтрокиТаблицРасчетаПоВидамНачислений);
	
	ЗаполнениеВыполнено = Истина;
	ПересчетФизическогоЛицаВДлительнойОперации = Ложь;
	
КонецПроцедуры

&НаКлиенте
Процедура ЗаполнитьДанныеФормыНаКлиенте() Экспорт
	
	УИДЗамера = ОценкаПроизводительностиКлиент.ЗамерВремени("ЗаполнениеДокументаРасчетРезервов",,Ложь);
	
	Результат = РезультатЗаполненияВДлительнойОперации();
	
	Если Результат.ЗаданиеВыполнено Тогда
		
		Если ЗначениеЗаполнено(УИДЗамера) Тогда
			ОценкаПроизводительностиКлиент.ЗавершитьЗамерВремени(УИДЗамера);
			УИДЗамера = Неопределено;
		КонецЕсли;
		
		ОповеститьОбИзмененииОбъекта();
	
	Иначе 
		
		ИдентификаторЗадания = Результат.ИдентификаторЗадания;
		АдресХранилища		 = Результат.АдресХранилища;
		
		ДлительныеОперацииКлиент.ИнициализироватьПараметрыОбработчикаОжидания(ПараметрыОбработчикаОжидания);
		ПодключитьОбработчикОжидания("Подключаемый_ПроверитьВыполнениеЗадания", 1, Истина);
		ФормаДлительнойОперации = ДлительныеОперацииКлиент.ОткрытьФормуДлительнойОперации(ЭтаФорма, ИдентификаторЗадания);
		
	КонецЕсли;
	
КонецПроцедуры

&НаКлиенте
Процедура ОповеститьОбИзмененииОбъекта()
	
	ОповеститьОбИзменении(Объект.Ссылка);
	
	ПараметрыОповещения = Новый Структура("Сценарий,Организация,ДатаНачала,ДатаОкончания");
	ЗаполнитьЗначенияСвойств(ПараметрыОповещения, Объект);
	
	Оповестить("Запись_РасчетРезервов", ПараметрыОповещения, Объект.Ссылка);
	
КонецПроцедуры  

&НаСервереБезКонтекста
Функция ЗаданиеВыполнено(ИдентификаторЗадания)
	
	Возврат ДлительныеОперации.ЗаданиеВыполнено(ИдентификаторЗадания);
	
КонецФункции

&НаСервереБезКонтекста
Функция СообщенияФоновогоЗадания(ИдентификаторЗадания)
	
	СообщенияПользователю = Новый Массив;
	ФоновоеЗадание = ФоновыеЗадания.НайтиПоУникальномуИдентификатору(ИдентификаторЗадания);
	Если ФоновоеЗадание <> Неопределено Тогда
		СообщенияПользователю = ФоновоеЗадание.ПолучитьСообщенияПользователю();
	КонецЕсли;
	
	Возврат СообщенияПользователю;
	
КонецФункции

&НаСервере
Процедура ПроведениеПослеВыполненияДлительнойОперации()
	
	Модифицированность = Ложь;
	Если Не ЗакрыватьПослеЗаписи Тогда
		ДокументОбъект = Бюджетирование.ДесериализоватьОбъектИзДвоичныхДанных(ПолучитьИзВременногоХранилища(АдресХранилища));
		ЗначениеВРеквизитФормы(ДокументОбъект, "Объект");
		ПриПолученииДанныхНаСервере(ДокументОбъект, Ложь);
	КонецЕсли;
	
	Если ОбщегоНазначения.ПодсистемаСуществует("БюджетированиеПриложения.ОтложенноеПроведениеДокументов") Тогда 
		Модуль = ОбщегоНазначения.ОбщийМодуль("ОтражениеДокументовВУчетеСтраховыхВзносов");
		Модуль.ОтразитьДокументыВУчетеСтраховыхВзносовВДлительнойОперации(Объект.Организация, Объект.Ссылка);
	КонецЕсли;
	
КонецПроцедуры

&НаКлиенте
Процедура Подключаемый_ПроверитьВыполнениеЗадания()
	
	ЗаданиеВыполненоВДлительнойОперации = Ложь;
	
	Попытка
		Если ФормаДлительнойОперации.Открыта() 
			И ФормаДлительнойОперации.ИдентификаторЗадания = ИдентификаторЗадания Тогда
			Если ЗаданиеВыполнено(ИдентификаторЗадания) Тогда
				Если ПроведениеВДлительнойОперации Тогда
					ПроведениеПослеВыполненияДлительнойОперации();
					Если ЗначениеЗаполнено(УИДЗамера) Тогда
						ОценкаПроизводительностиКлиент.ЗавершитьЗамерВремени(УИДЗамера);
						УИДЗамера = Неопределено;
					КонецЕсли;
				Иначе
					РасчетБюджетированиеРасширенныйКлиент.ОчиститьСписокФизическиеЛицаКРасчету(ЭтаФорма);
					Если ПересчетВидаНачисленияВДлительнойОперации Тогда
						ЗаполнениеПослеВыполненияДлительнойОперацииНаКлиенте();
					Иначе
						ЗаполнениеПослеВыполненияДлительнойОперации();
					КонецЕсли;
					Если ЗначениеЗаполнено(УИДЗамера) Тогда
						ОценкаПроизводительностиКлиент.ЗавершитьЗамерВремени(УИДЗамера);
						УИДЗамера = Неопределено;
					КонецЕсли;
				КонецЕсли;
				ДлительныеОперацииКлиент.ЗакрытьФормуДлительнойОперации(ФормаДлительнойОперации);
				ЗаданиеВыполненоВДлительнойОперации = Истина;
				ОповеститьОбИзмененииОбъекта();
			Иначе
				ДлительныеОперацииКлиент.ОбновитьПараметрыОбработчикаОжидания(ПараметрыОбработчикаОжидания);
				ПодключитьОбработчикОжидания(
					"Подключаемый_ПроверитьВыполнениеЗадания",
					ПараметрыОбработчикаОжидания.ТекущийИнтервал,
					Истина);
			КонецЕсли;
		ИначеЕсли Не ФормаДлительнойОперации.Открыта() И ПроведениеВДлительнойОперации Тогда
			ПроведениеВДлительнойОперации = Ложь;
			ЗакрыватьПослеЗаписи = Ложь;
		КонецЕсли;
	Исключение
		ДлительныеОперацииКлиент.ЗакрытьФормуДлительнойОперации(ФормаДлительнойОперации);
		
		СообщенияПользователю = СообщенияФоновогоЗадания(ИдентификаторЗадания);
		Если СообщенияПользователю <> Неопределено Тогда
			Для каждого СообщениеФоновогоЗадания Из СообщенияПользователю Цикл
				СообщениеФоновогоЗадания.Сообщить();
			КонецЦикла;
		КонецЕсли;
		
		ВызватьИсключение;
	КонецПопытки;
	
	Если ПроведениеВДлительнойОперации И ЗаданиеВыполненоВДлительнойОперации Тогда
		Если ЗакрыватьПослеЗаписи Тогда
			ПодключитьОбработчикОжидания("ЗакрытьФорму", 0.1, Истина);
		КонецЕсли;
		ПроведениеВДлительнойОперации = Ложь;
	КонецЕсли;
	
КонецПроцедуры

&НаКлиенте
Процедура ПередЗакрытием(Отказ, ЗавершениеРаботы, ТекстПредупреждения, СтандартнаяОбработка)

	ЗакрыватьПослеЗаписи = Истина;

КонецПроцедуры

&НаКлиенте
Процедура ПередЗаписью(Отказ, ПараметрыЗаписи)

	ЗакрыватьПослеЗаписи = Ложь;

КонецПроцедуры

&НаКлиенте
Процедура ОрганизацияПриИзменении(Элемент)
	БюджетированиеРасширенныйКлиент.КлючевыеРеквизитыЗаполненияФормыОчиститьТаблицы(ЭтаФорма);
	ОбработатьИзменениеМесяцНачисленияНаСервере();
КонецПроцедуры

&НаСервере
Процедура ПослеЗаписиНаСервере(ТекущийОбъект, ПараметрыЗаписи)
	
	// СтандартныеПодсистемы.УправлениеДоступом
	Если ОбщегоНазначения.ПодсистемаСуществует("СтандартныеПодсистемы.УправлениеДоступом") Тогда
		МодульУправлениеДоступом = ОбщегоНазначения.ОбщийМодуль("УправлениеДоступом");
		МодульУправлениеДоступом.ПослеЗаписиНаСервере(ЭтотОбъект, ТекущийОбъект, ПараметрыЗаписи);
	КонецЕсли;
	// Конец СтандартныеПодсистемы.УправлениеДоступом
	
КонецПроцедуры

&НаКлиенте
Процедура ПриИзмененииПодразделения() 
	
	ОбновитьВидимостьПодразделенияМестаВозникновенияЗатрат(); 
	
КонецПроцедуры

&НаКлиенте
Процедура ПодразделениеПриИзменении(Элемент) 
	
	ПриИзмененииПодразделения();
	
КонецПроцедуры

&НаКлиенте
Процедура ПодразделениеОчистка(Элемент, СтандартнаяОбработка) 
	
	//ПриИзмененииПодразделения(Истина);
	
КонецПроцедуры

&НаКлиенте
Процедура ПодразделениеОбработкаВыбора(Элемент, ВыбранноеЗначение, СтандартнаяОбработка)
	// Вставить содержимое обработчика.
КонецПроцедуры

&НаКлиенте
Процедура ПриИзмененииМестаВозникновенияЗатрат()
	
	ОбновитьВидимостьПодразделенияМестаВозникновенияЗатрат();
	
КонецПроцедуры

&НаКлиенте
Процедура МестоВозникновенияЗатратПриИзменении(Элемент) 
	
	ПриИзмененииМестаВозникновенияЗатрат();
	
КонецПроцедуры

&НаКлиенте
Процедура МестоВозникновенияЗатратОчистка(Элемент, СтандартнаяОбработка)
	
	//ПриИзмененииМестаВозникновенияЗатрат(Истина);
	
КонецПроцедуры

&НаКлиенте
Процедура МестоВозникновенияЗатратОбработкаВыбора(Элемент, ВыбранноеЗначение, СтандартнаяОбработка)
	// Вставить содержимое обработчика.
КонецПроцедуры

#КонецОбласти