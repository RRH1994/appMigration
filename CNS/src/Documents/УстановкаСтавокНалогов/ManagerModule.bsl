#Если Сервер Или ТолстыйКлиентОбычноеПриложение Или ВнешнееСоединение Тогда

#Область ПрограммныйИнтерфейс

#Область ДляВызоваИзДругихПодсистем

// СтандартныеПодсистемы.ВерсионированиеОбъектов

// Определяет настройки объекта для подсистемы ВерсионированиеОбъектов.
//
// Параметры:
//  Настройки - Структура - настройки подсистемы.
Процедура ПриОпределенииНастроекВерсионированияОбъектов(Настройки) Экспорт

КонецПроцедуры
// Конец СтандартныеПодсистемы.ВерсионированиеОбъектов

// СтандартныеПодсистемы.УправлениеДоступом

// См. УправлениеДоступомПереопределяемый.ПриЗаполненииСписковСОграничениемДоступа.
Процедура ПриЗаполненииОграниченияДоступа(Ограничение) Экспорт
	
	//Ограничение.Текст =
	//"РазрешитьЧтениеИзменение
	//|ГДЕ
	//|	// ДляВсехСтрок( ЗначениеРазрешено(ФизическиеЛица.ФизическоеЛицо, NULL КАК ИСТИНА)
	//|	//) И 
	//|   ЗначениеРазрешено(Организация)";   
	
	Ограничение.Текст =
	"ПрисоединитьДополнительныеТаблицы
	|ЭтотСписок КАК Т
	|
	|ЛЕВОЕ СОЕДИНЕНИЕ Документ.УстановкаСтавокНалогов КАК Т1
	|	ПО Т1.Ссылка = Т.Ссылка
	|
	|;
	|РазрешитьЧтениеИзменение
	|ГДЕ
	| Истина";
	
КонецПроцедуры

// Конец СтандартныеПодсистемы.УправлениеДоступом

#КонецОбласти	
	
#КонецОбласти	

#Область СлужебныеПроцедурыИФункции

// <Описание функции>
//
// Параметры:
//  <Параметр1>  - <Тип.Вид> - <описание параметра>
//                 <продолжение описания параметра>
//  <Параметр2>  - <Тип.Вид> - <описание параметра>
//                 <продолжение описания параметра>
//
// Возвращаемое значение:
//   <Тип.Вид>   - <описание возвращаемого значения>
//
Функция ПолучитьСтруктуруУстановкиСтавки() Экспорт
	
	Возврат Новый Структура(
		"Сценарий, ВидВнебюджетногоФонда, СтавкаНалога, ГраницаНалога, СтавкаНалогаПослеГраницы, СтавкаНалогаПрочее, СтавкаНалогаКорректировка, Дата"); 

КонецФункции // ПолучитьСтруктуруУстановкиСтавки()

// <Описание процедуры>
//
// Параметры:
//  <Параметр1>  - <Тип.Вид> - <описание параметра>
//                 <продолжение описания параметра>
//  <Параметр2>  - <Тип.Вид> - <описание параметра>
//                 <продолжение описания параметра>
//
Процедура УстановитьСтавку(СтруктураСтавок) Экспорт
	
	УстановитьПривилегированныйРежим(Истина);	
	
	Запрос 			= Новый Запрос;
	Запрос.Текст 	= 
	"ВЫБРАТЬ
	|	Т.Ссылка КАК Ссылка,
	|	Т.ПометкаУдаления КАК ПометкаУдаления,
	|	Т.Проведен КАК Проведен
	|ИЗ
	|	Документ.УстановкаСтавокНалогов КАК Т
	|ГДЕ
	|	&ТекстУсловия";
	
	ТекстУсловия = "ИСТИНА";
	
	Для Каждого КлючЗначение Из СтруктураСтавок Цикл
		
		Если Не (КлючЗначение.Ключ = "Сценарий" Или КлючЗначение.Ключ = "Дата" Или КлючЗначение.Ключ = "ВидВнебюджетногоФонда") Тогда
			Продолжить;
		КонецЕсли;
		
		ТекстУсловия = ТекстУсловия + " И " + "Т." + КлючЗначение.Ключ + " = &" + КлючЗначение.Ключ;
		Запрос.УстановитьПараметр(КлючЗначение.Ключ, КлючЗначение.Значение);
		
	КонецЦикла;
	
	Запрос.Текст = СтрЗаменить(Запрос.Текст, "&ТекстУсловия", ТекстУсловия);
	
	Выборка = Запрос.Выполнить().Выбрать();
	
	Если Выборка.Следующий() Тогда
		ДокументОбъект = Выборка.Ссылка.ПолучитьОбъект();
		Если ДокументОбъект.ПометкаУдаления Тогда
			ДокументОбъект.ПометкаУдаления = Ложь;
		КонецЕсли;
	Иначе
		ДокументОбъект = Документы.УстановкаСтавокНалогов.СоздатьДокумент();
	КонецЕсли;
	
	ЗаполнитьЗначенияСвойств(ДокументОбъект, СтруктураСтавок);
	
	Если ДокументОбъект.СтавкаНалога = 0
	   И ДокументОбъект.ГраницаНалога = 0
	   И ДокументОбъект.СтавкаНалогаПослеГраницы = 0
	   И ДокументОбъект.СтавкаНалогаПрочее = 0
	   И ДокументОбъект.СтавкаНалогаКорректировка = 0
	Тогда
		Если ДокументОбъект.Проведен Тогда
			ДокументОбъект.Записать(РежимЗаписиДокумента.ОтменаПроведения);
		КонецЕсли;
		ДокументОбъект.ПометкаУдаления = Истина;
		ДокументОбъект.Записать();
	Иначе
		ДокументОбъект.Записать(РежимЗаписиДокумента.Проведение);
	КонецЕсли;
	
КонецПроцедуры // УстановитьСтавку()

Функция ПолучитьЗапросПоДокументамУстановкаСтавок(Сценарий, НачалоПериода, ОкончаниеПериода, ВидВнебюджетногоФонда = Неопределено) Экспорт

	УстановитьПривилегированныйРежим(Истина);	
	
	Запрос 			= Новый Запрос;
	Запрос.Текст 	= 
	"ВЫБРАТЬ
	|	УстановкаСтавокНалогов.Ссылка КАК Ссылка,
	|	УстановкаСтавокНалогов.ПометкаУдаления КАК ПометкаУдаления,
	|	УстановкаСтавокНалогов.Номер КАК Номер,
	|	УстановкаСтавокНалогов.Дата КАК Дата,
	|	УстановкаСтавокНалогов.Проведен КАК Проведен,
	|	УстановкаСтавокНалогов.Сценарий КАК Сценарий,
	|	УстановкаСтавокНалогов.ВидВнебюджетногоФонда КАК ВидВнебюджетногоФонда,
	|	УстановкаСтавокНалогов.СтавкаНалога КАК СтавкаНалога,
	|	УстановкаСтавокНалогов.ГраницаНалога КАК ГраницаНалога,
	|	УстановкаСтавокНалогов.СтавкаНалогаПослеГраницы КАК СтавкаНалогаПослеГраницы,
	|	УстановкаСтавокНалогов.СтавкаНалогаПрочее КАК СтавкаНалогаПрочее,
	|	УстановкаСтавокНалогов.СтавкаНалогаКорректировка КАК СтавкаНалогаКорректировка
	|ИЗ
	|	Документ.УстановкаСтавокНалогов КАК УстановкаСтавокНалогов
	|ГДЕ
	|	УстановкаСтавокНалогов.Проведен
	|	И УстановкаСтавокНалогов.Сценарий = &Сценарий
	|	И УстановкаСтавокНалогов.Дата МЕЖДУ &НачалоПериода И &ОкончаниеПериода
	|	И (&ПоВсемВидамВнебюджетныхФондов
	|			ИЛИ УстановкаСтавокНалогов.ВидВнебюджетногоФонда = &ВидВнебюджетногоФонда)";
	
	Запрос.УстановитьПараметр("Сценарий", 						Сценарий);
	Запрос.УстановитьПараметр("НачалоПериода", 					НачалоПериода);
	Запрос.УстановитьПараметр("ОкончаниеПериода", 				ОкончаниеПериода);
	Запрос.УстановитьПараметр("ВидВнебюджетногоФонда", 			ВидВнебюджетногоФонда);
	Запрос.УстановитьПараметр("ПоВсемВидамВнебюджетныхФондов", 	Не ЗначениеЗаполнено(ВидВнебюджетногоФонда));
	
	Возврат Запрос;
	
КонецФункции	

Процедура УдалитьСтавки(Сценарий, НачалоПериода, ОкончаниеПериода, ВидВнебюджетногоФонда = Неопределено) Экспорт
	
	Запрос 	= ПолучитьЗапросПоДокументамУстановкаСтавок(Сценарий, НачалоПериода, ОкончаниеПериода, ВидВнебюджетногоФонда);
	Выборка = Запрос.Выполнить().Выбрать();
	
	Пока Выборка.Следующий() Цикл
		
		ДокОбъект 					= Выборка.Ссылка.ПолучитьОбъект();
		ДокОбъект.Записать(РежимЗаписиДокумента.ОтменаПроведения);
		ДокОбъект.ПометкаУдаления 	= Истина;
		ДокОбъект.Записать(РежимЗаписиДокумента.Запись);
		
	КонецЦикла;	
	
КонецПроцедуры	

// <Описание процедуры>
//
// Параметры:
//  <Параметр1>  - <Тип.Вид> - <описание параметра>
//                 <продолжение описания параметра>
//  <Параметр2>  - <Тип.Вид> - <описание параметра>
//                 <продолжение описания параметра>
//
Процедура СкопироватьСтавкиПоСценарию(СценарийИсточник, СценарийПриемник, НачалоПериода, ОкончаниеПериода, ВидВнебюджетногоФонда = Неопределено) Экспорт

	Запрос 	= ПолучитьЗапросПоДокументамУстановкаСтавок(СценарийИсточник, НачалоПериода, ОкончаниеПериода, ВидВнебюджетногоФонда);
	Выборка = Запрос.Выполнить().Выбрать();
	
	Пока Выборка.Следующий() Цикл
		
		СтруктураСтавок = ПолучитьСтруктуруУстановкиСтавки();
		ЗаполнитьЗначенияСвойств(СтруктураСтавок, Выборка);
		
		СтруктураСтавок.Вставить("Сценарий", СценарийПриемник);
		
		УстановитьСтавку(СтруктураСтавок);
		
	КонецЦикла;	

КонецПроцедуры // СкопироватьСтавки()

// <Описание процедуры>
//
// Параметры:
//  <Параметр1>  - <Тип.Вид> - <описание параметра>
//                 <продолжение описания параметра>
//  <Параметр2>  - <Тип.Вид> - <описание параметра>
//                 <продолжение описания параметра>
//
Процедура СкопироватьСтавкиПоГоду(Сценарий, НачалоМесяцаИсточник, КонецМесяцаИсточник, ВидВнебюджетногоФонда = Неопределено) Экспорт // ИМЗЕНИТЬ

	Запрос 	= ПолучитьЗапросПоДокументамУстановкаСтавок(Сценарий, НачалоМесяца(НачалоМесяцаИсточник), НачалоМесяца(КонецМесяцаИсточник), ВидВнебюджетногоФонда);
	Выборка = Запрос.Выполнить().Выбрать();
	
	Пока Выборка.Следующий() Цикл
		
		СтруктураСтавок = ПолучитьСтруктуруУстановкиСтавки();
		ЗаполнитьЗначенияСвойств(СтруктураСтавок, Выборка);
		
		СтруктураСтавок.Вставить("Дата", НачалоМесяцаИсточник);
		
		УстановитьСтавку(СтруктураСтавок);
		
	КонецЦикла;	

КонецПроцедуры // СкопироватьСтавки()

#КонецОбласти	
	
#КонецЕсли




