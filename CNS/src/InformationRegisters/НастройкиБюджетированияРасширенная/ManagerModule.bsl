#Если Сервер Или ТолстыйКлиентОбычноеПриложение Или ВнешнееСоединение Тогда

#Область ПрограммныйИнтерфейс

#Область ДляВызоваИзДругихПодсистем

// СтандартныеПодсистемы.УправлениеДоступом

// См. УправлениеДоступомПереопределяемый.ПриЗаполненииСписковСОграничениемДоступа.
Процедура ПриЗаполненииОграниченияДоступа(Ограничение) Экспорт
	Ограничение.Текст =
	"РазрешитьЧтениеИзменение
	|ГДЕ
	|	ЗначениеРазрешено(Организация)";
КонецПроцедуры

// Конец СтандартныеПодсистемы.УправлениеДоступом

#КонецОбласти

#КонецОбласти

#Область СлужебныйПрограммныйИнтерфейс

Функция СведенияОНастройкахОрганизации(Организация, Период, Сценарий,  Сведения) Экспорт
	
	Если ТипЗнч(Сведения) = Тип("Строка") Тогда
		СтруктураСведений = Новый Структура(Сведения);
		
	ИначеЕсли ТипЗнч(Сведения) = Тип("Структура")
	      ИЛИ ТипЗнч(Сведения) = Тип("ФиксированнаяСтруктура") Тогда
		
		СтруктураСведений = Сведения;
		
	ИначеЕсли ТипЗнч(Сведения) = Тип("Массив")
	      ИЛИ ТипЗнч(Сведения) = Тип("ФиксированныйМассив") Тогда
		
		СтруктураСведений = Новый Структура;
		Для каждого Сведение Из Сведения Цикл
			СтруктураСведений.Вставить(Сведение);
		КонецЦикла;
	Иначе
		ВызватьИсключение СтроковыеФункцииКлиентСервер.ПодставитьПараметрыВСтроку(
			НСтр("ru = 'Неверный тип второго параметра Сведения: %1'"),
			Строка(ТипЗнч(Сведения)));
	КонецЕсли;
	
	ТекстПолей = "";
	Для Каждого КлючИЗначение Из СтруктураСведений Цикл
		Если КлючИЗначение.Ключ = "" Тогда //Исключаемое поле - добавить
			Продолжить;
		КонецЕсли;
		
		ИмяПоля   = ?(ЗначениеЗаполнено(КлючИЗначение.Значение),
		              СокрЛП(КлючИЗначение.Значение),
		              СокрЛП(КлючИЗначение.Ключ));
		
		Псевдоним = СокрЛП(КлючИЗначение.Ключ);
		
		ТекстПолей  = ТекстПолей + ?(ПустаяСтрока(ТекстПолей), "", ",") + "
		|	" + ИмяПоля + " КАК " + Псевдоним;
		
	КонецЦикла;
	
	Запрос = Новый Запрос;
	Запрос.УстановитьПараметр("Организация", Организация); 
	Запрос.УстановитьПараметр("Период", 	 Период);
	Запрос.УстановитьПараметр("Сценарий", 	 Сценарий);
	Запрос.Текст =
	"ВЫБРАТЬ
	|" + ТекстПолей + "
	|ИЗ
	|	РегистрСведений.НастройкиБюджетированияРасширенная КАК ПсевдонимЗаданнойТаблицы
	|ГДЕ
	|	ПсевдонимЗаданнойТаблицы.Организация = &Организация
	|   И ПсевдонимЗаданнойТаблицы.Период = &Период
	|   И ПсевдонимЗаданнойТаблицы.Сценарий = &Сценарий 
	|";
	Выборка = Запрос.Выполнить().Выбрать();
	Выборка.Следующий();
	
	Результат = Новый Структура;
	Для Каждого КлючИЗначение Из СтруктураСведений Цикл
		Результат.Вставить(КлючИЗначение.Ключ);
	КонецЦикла;
	ЗаполнитьЗначенияСвойств(Результат, Выборка);
	
	Если СтруктураСведений.Свойство("МесяцПервогоПлатежаЗаДМС") Тогда
		Если Не ЗначениеЗаполнено(Результат.МесяцПервогоПлатежаЗаДМС) Тогда
			Результат.МесяцПервогоПлатежаЗаДМС = Перечисления.МесяцыГода.Август;
		КонецЕсли;
	КонецЕсли;
	Если СтруктураСведений.Свойство("МесяцВторогоПлатежаЗаДМС") Тогда
		Если Не ЗначениеЗаполнено(Результат.МесяцВторогоПлатежаЗаДМС) Тогда
			Результат.МесяцВторогоПлатежаЗаДМС = Перечисления.МесяцыГода.Декабрь;
		КонецЕсли;
	КонецЕсли;
	Если СтруктураСведений.Свойство("МесяцВыплатыКвартальнойПремииЗа4Квартал") Тогда
		Если Не ЗначениеЗаполнено(Результат.МесяцВыплатыКвартальнойПремииЗа4Квартал) Тогда
			Результат.МесяцВыплатыКвартальнойПремииЗа4Квартал = Перечисления.МесяцыГода.Декабрь;
		КонецЕсли;
	КонецЕсли;
	Если СтруктураСведений.Свойство("МесяцВыплатыГодовойПремии") Тогда
		Если Не ЗначениеЗаполнено(Результат.МесяцВыплатыГодовойПремии) Тогда
			Результат.МесяцВыплатыГодовойПремии = Перечисления.МесяцыГода.Август;
		КонецЕсли;
	КонецЕсли;
	Если СтруктураСведений.Свойство("МесяцВыплатыПремииЗа2Полугодие") Тогда
		Если Не ЗначениеЗаполнено(Результат.МесяцВыплатыПремииЗа2Полугодие) Тогда
			Результат.МесяцВыплатыПремииЗа2Полугодие = Перечисления.МесяцыГода.Январь;
		КонецЕсли;
	КонецЕсли;
	Если СтруктураСведений.Свойство("МесяцУплатыНалоговСНачисленийДекабря") Тогда
		Если Не ЗначениеЗаполнено(Результат.МесяцУплатыНалоговСНачисленийДекабря) Тогда
			Результат.МесяцУплатыНалоговСНачисленийДекабря = Перечисления.МесяцыГода.Декабрь;
		КонецЕсли;
	КонецЕсли;
	
	Возврат Результат;
	
КонецФункции

#КонецОбласти

#КонецЕсли